ACTOR GosimersExplosivepuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	+PUFFONACTORS
	+PUFFGETSOWNER
	+FORCERADIUSDMG
	DamageType "PlayerExplosivePuff"
	RenderStyle ADD
	Alpha 1.0
	Scale 0.3
	Mass 5
	States
	{
	Spawn:
	// Intentional fall-through
		TNT1 A 0
	xdeath:
	Melee:
		D6EX A 0 A_Explode(5, 48, 0, false, 32)
		D6EX ABCDEFGHIJKLMNOPQ 1 Bright
		Stop
	}
}

actor CritNadeFireTrail : Particle_Base
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.3
	States
	{
		Spawn:
			TNT1 A 2
			TNT1 A 0 A_ChangeVelocity(frandom(-0.75, 0.75), frandom(-0.75, 0.75), frandom(-0.75, 0.75), CVF_RELATIVE)
			D6EX ABCDEF 1 Bright A_Fadeout(0.15)
			stop
	}
}

actor NadeSmoke : Particle_Base
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.6
	States
	{
		Spawn:
			TNT1 A 2
			TNT1 A 0 A_ChangeVelocity(frandom(-0.75, 0.75), frandom(-0.75, 0.75), frandom(-0.75, 0.75), CVF_RELATIVE)
			goto Spawn1
		Spawn1:
			D3DS A 1 A_FadeOut(0.01)
			D3DS A 0 A_SetScale(scalex + 0.025)
			D3DS A 0 A_JumpIf(alpha <= 0.45, "Spawn2")
			loop
		Spawn2:
			D3DS B 1 A_FadeOut(0.01)
			D3DS B 0 A_SetScale(scalex + 0.025)
			D3DS B 0 A_JumpIf(alpha <= 0.3, "Spawn3")
			loop
		Spawn3:
			D3DS C 1 A_FadeOut(0.01)
			D3DS C 0 A_SetScale(scalex + 0.025)
			D3DS C 0 A_JumpIf(alpha <= 0.15, "Spawn4")
			loop
		Spawn4:
			D3DS D 1 A_FadeOut(0.01)
			D3DS D 0 A_SetScale(scalex + 0.025)
			loop
	}
}

Actor Nadez_Base
{
	Radius 8
	Height 8
	Speed 40
	Damage 20
	Gravity 0.5
	Projectile
	-NOGRAVITY
	+NOTIMEFREEZE
	States
	{
		Spawn:
			SGRN A 1 Bright A_SpawnItemEx("NadeSmoke", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			loop
		Death:
			DEXP A 0 Bright A_SetTranslucent(1.0, 1)
			DEXP A 0 Bright A_SetScale(0.1, 0.1)
			DEXP A 0 Bright A_PlaySound("Weapon/Nadeboom", CHAN_AUTO)
			DEXP A 0 Bright A_ChangeFlag("NoGravity", 1)
			DEXP A 0 Bright A_Explode(128, 192)
			DEXP ABCDEFGHIJKLMNOPQRST 1 Bright A_SetScale(scalex+0.1, scaley+0.1)
			stop
	}
}

Actor CritNadez_Base : Nadez_Base
{
	DamageType Fire
	SeeSound "Weapons/GrenLF"
	+FORCERADIUSDMG
	+EXTREMEDEATH
	States
	{
		Spawn:
			SGRN A 1 Bright A_SpawnItemEx("NadeSmoke", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			SGRN A 0 Bright A_SpawnItemEx("CritNadeFireTrail", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			loop
		Death:
			DEXP A 0 Bright A_SetTranslucent(1.0, 1)
			DEXP A 0 Bright A_SetScale(0.2, 0.2)
			DEXP A 0 Bright A_PlaySound("Weapon/Nadeboom", CHAN_AUTO)
			DEXP A 0 Bright A_ChangeFlag("NoGravity", 1)
			DEXP A 0 Bright A_Explode(192, 192)
			DEXP ABCDEFGHIJKLMNOPQRST 1 Bright A_SetScale(scalex+0.1, scaley+0.1)
			stop
	}
}

Actor BouncyNadez : Nadez_Base
{
	BounceType Doom
	BounceSound "Weapon/nadebounce"
}

Actor ImpactNadez : Nadez_Base
{
	Speed 40
	Damage 40
}

Actor CritBouncyNadez : CritNadez_Base
{
	BounceType Doom
	BounceSound "Weapon/nadebounce"
	Damage 40
}

Actor CritImpactNadez : CritNadez_Base
{
	Speed 40
	Damage 60
}



ACTOR BoomToob : SH_Weapon //Sprites from Serghei â†’ Typical.SF / Gosimer did gold frames
{
	Weapon.PreferredSkin "Grenade-Marine"
	Weapon.SelectionOrder 2500
	Weapon.SlotNumber 6
	Weapon.AmmoType1 "Roketammo"
	Weapon.AmmoType2 "Roketammo"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 0 // Handled in ACS
	Weapon.AmmoGive2 0 // Handled in ACS
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX .5
	Weapon.Bobstyle	"Smooth"
	Weapon.SwayStyle "HorizontalOnly"
	Weapon.ViewSwaySpeed 1.0
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Inventory.ForbiddenTo	"Fiend"
	Inventory.ForbiddenTo	"Kunai"
	Inventory.RestrictedTo	"Gosimer"
	Inventory.Forbiddento	"Maximusprime"
	Inventory.PickupMessage "You got the Boom shoot"
	Inventory.PickupSound	"Weapon/Nadeget"
	States
	{
	Ready:
		TNT1 A 0 A_Jumpifinventory("Tier5upgradetoken",1,"Goldready")
		GNTR A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_Jumpifinventory("Tier5upgradetoken",1,"Golddeselect")
		GNTR A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_Jumpifinventory("Tier5upgradetoken",1,"Goldselect")
		GNTR A 1 A_Raise
		Loop
	Fire:	//Bounce Grenade
		GNTR A 0 A_Jumpifinventory("Tier5upgradetoken",1,"Goldfire")
		GNTR A 1 A_Gunflash
		GNTR B 1 Bright A_Playsound ("Gosimer/GrenadeL_POOT",Chan_weapon)
		GNTR B 0 A_FireCustomMissile("BouncyNadez",0,1,0,0,0,7)
		GNTR CDE 1 Bright
		GNTR FA 2
		GNTR A 0 A_Jumpifnoammo("Ready")
		GNTR GHI 3 //FRAME G Reload start
		GNTR I 5 A_Playsound ("Gosimer/GrenadeL_open",Chan_Weapon)
		GNTR JKL 3 //FRAME L Break open
		GNTR M 4 A_Playsound ("Gosimer/GrenadeL_Close",Chan_6) //This Sound has "Insertshell" and "close" sfx baked in!
		GNTR NOP 4
		GNTR QR 3
		GNTR ST 3 //FRAME T CLOSE Barrel
		GNTR U 5
		GNTR VX 3
		GNTR X 0 A_Refire
		Goto Ready
	Altfire: //Contact grenades
		GNTR A 0 A_Jumpifinventory("Tier5upgradetoken",1,"Goldaltfire")
		GNTR A 1 A_Gunflash
		GNTR B 1 Bright A_Playsound ("Gosimer/GrenadeL_POOT",Chan_weapon)
		GNTR B 0 A_FireCustomMissile("ImpactNadez",0,1,0,0,0,7)
		GNTR CDE 1 Bright
		GNTR FA 2
		GNTR A 0 A_Jumpifnoammo("Ready")
		GNTR GHI 3 //Reload start
		GNTR I 5 A_Playsound ("Gosimer/GrenadeL_open",Chan_Weapon)
		GNTR JKL 3 //FRAME L Break open
		GNTR M 4 A_Playsound ("Gosimer/GrenadeL_Close",Chan_6) //This Sound has "Insertshell" and "close" sfx baked in!
		GNTR NOP 4 //FRAME N Insert Shell
		GNTR QR 3
		GNTR ST 3 //FRAME T CLOSE Barrel
		GNTR U 5
		GNTR VX 3
		GNTR X 0 A_Refire
		Goto Ready
	Goldready:
		GNTG A 1 A_WeaponReady
		Loop
	GoldDeselect:
		GNTG A 1 A_Lower
		Loop
	GoldSelect:
		GNTG A 1 A_Raise
		Loop
	Goldfire:
		GNTG A 1
		GNTG A 0 A_Gunflash
		GNTG B 1 Bright A_Playsound ("Gosimer/GrenadeL_POOT",Chan_weapon)
		GNTR B 0 A_FireCustomMissile("CritBouncyNadez",0,1,0,0,0,7)
		GNTG CDE 1 Bright
		GNTG FA 2
		GNTR A 0 A_Jumpifnoammo("Ready")
		GNTG GHI 3 //Reload start
		GNTG I 5 A_Playsound ("Gosimer/GrenadeL_open",Chan_Weapon)
		GNTG JKL 3 //FRAME L Break open
		GNTG M 4 A_Playsound ("Gosimer/GrenadeL_Close",Chan_6)
		GNTG NOP 4 //FRAME N Insert Shell
		GNTG QR 3
		GNTG ST 3 //FRAME T CLOSE Barrel
		GNTG U 5
		GNTG VX 3
		GNTG X 0 A_Refire
		Goto Ready
	GoldAltfire:
		GNTG A 1
		GNTG A 0 A_Gunflash
		GNTG B 1 Bright A_Playsound ("Gosimer/GrenadeL_POOT",Chan_weapon)
		GNTR B 0 A_FireCustomMissile("CritImpactNadez",0,1,0,0,0,7)
		GNTG CDE 1 Bright
		GNTG FA 2
		GNTG A 0 A_Jumpifnoammo("Ready")
		GNTG GHI 3 //Reload start
		GNTG I 5 A_Playsound ("Gosimer/GrenadeL_open",Chan_Weapon)
		GNTG JKL 3 //FRAME L Break open
		GNTG M 4 A_Playsound ("Gosimer/GrenadeL_Close",Chan_6)
		GNTG NOP 4 //FRAME N Insert Shell
		GNTG QR 3
		GNTG ST 3 //FRAME T CLOSE Barrel
		GNTG U 5
		GNTG VX 3
		GNTG X 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_light2
		TNT1 A 2 A_light1
		Goto LightDone
	Spawn:
		GNTR Z -1
		Stop
	}
}

Actor DookiClip : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.MaxAmount 12
	ammo.Backpackmaxamount 12
}

ACTOR GosimersHandgun : SH_Weapon
{
	Tag "Pistol"
	Inventory.Forbiddento "Maximusprime"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "Gosimer"
	Inventory.Pickupmessage "Pistol."
	Obituary "%o killed %k"

	Weapon.PreferredSkin "Pistol-Marine"
	Weapon.AmmoType1 "DookiClip"
	Weapon.AmmoType2 "peastolClip"
	Weapon.AmmoGive1 32
	Weapon.AmmoGive2 48
	Weapon.AmmoUse1 1
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 4
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX .5
	Weapon.BobStyle "Smooth"
	Weapon.SwayStyle "HorizontalOnly"
	Weapon.ViewSwaySpeed 1.0

	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Ready:
			GDPI A 0 A_Jumpifinventory("Tier1upgradetoken",1,"GoldReady")
			GDPI A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop
		GoldReady:
			GDPG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop
		Deselect:
			GDPI A 0 A_Jumpifinventory("Tier1upgradetoken",1,"GoldDeselect")
			GDPI A 1 A_Lower
			Loop
		Golddeselect:
			GDPG A 1 A_Lower
			Loop
		Select:
			GDPI A 0 A_Jumpifinventory("Tier1upgradetoken",1,"GoldSelect")
			GDPI A 1 A_Raise
			Loop
		GoldSelect:
			GDPG A 1 A_Raise
			Loop

		Fire:
			GDPI A 0 A_Jumpifinventory("Tier1upgradetoken",1,"StartGoldFire")
			goto StartFire

		StartFire:
			GDPI B 0 A_JumpIfNoAmmo("ReloadOrDryfire")
			goto DoFire
		StartGoldFire:
			GDPG B 0 A_JumpIfNoAmmo("ReloadOrDryfire")
			goto DoGoldFire

		DoFire:
			"####" B 0 A_Playsound("GPistolFire", CHAN_WEAPON)
			"####" B 0 A_FireBullets(1.25, 0.75, 1, 6+random(0, 6), "BulletPuff", FBF_USEAMMO | FBF_NORANDOM)
			goto FireEnd
		DoGoldFire:
			"####" B 0 A_Playsound("GPistolFireGold", CHAN_WEAPON)
			"####" B 0 A_FireBullets(1.25, 0.75, 1, 6+random(0, 6), "GosimersExplosivePuff", FBF_USEAMMO | FBF_NORANDOM)
			goto FireEnd

		FireEnd:
			"####" B 0 A_GunFlash
			"####" B 1 Bright
			"####" C 1 Bright
			"####" DEF 1
			"####" A 1
			"####" A 0 A_JumpIfNoAmmo("ReloadOrDryfire")
			"####" A 0 A_Refire
			goto Ready

		ReloadOrDryfire:
			"####" A 0 A_JumpIfInventory("peastolClip", 1, "CheckReload")
			"####" A 0 A_Playsound("weapons/pistol/dry", CHAN_AUTO)
			"####" A 1 Offset(0, 33)
			"####" A 5 Offset(0, 32)
			"####" A 0 A_Refire
			goto Ready

		Reload:
			GDPI A 0 A_JumpIfInventory("peastolClip", 1, "CheckReload")
			goto Ready
		CheckReload:
			GDPI A 0 A_JumpIfInventory("DookiClip", 0, "Ready") // 0 == At Capacity
			goto ReloadStart
		ReloadStart:
			GPRE A 0 A_Playsound("GPistolMagOut", CHAN_AUTO)
			GPRE A 0 A_Jumpifinventory("Tier1upgradetoken",1,"ReloadEjectGold")
			goto ReloadEject

		ReloadEjectGold:
			GGRE A 0
		ReloadEject:
			"####" ABC 2
			"####" DEFGHIJK 1
			"####" C 6
			GPRI A 0 A_Jumpifinventory("Tier1upgradetoken",1,"ReloadInsertGold")
			goto ReloadInsert

		ReloadInsertGold:
			GGRI A 0
		ReloadInsert:
			"####" A 0 A_Playsound("GPistolMagIn", CHAN_AUTO)
			"####" ABCDEFGHIJKLMNOPQ 1
			goto ReloadLoop

		ReloadLoop:
			"####" A 0 A_TakeInventory("peastolClip", 1)
			"####" A 0 A_GiveInventory("DookiClip", 1)
			"####" A 0 A_JumpIfInventory("DookiClip", 0, "ReloadEnd1")
			"####" A 0 A_JumpIfInventory("peastolClip", 1, "ReloadLoop")
			goto ReloadEnd1

		ReloadEnd1:
			"####" RSTUVWX 1
			GPRE C 0 A_Jumpifinventory("Tier1upgradetoken",1,"ReloadEnd2Gold")
			goto ReloadEnd2

		ReloadEnd2Gold:
			GGRE A 0
		ReloadEnd2:
			"####" CBA 2
			"####" A 0 A_Refire
			goto Ready
		Flash:
			TNT1 A 1 A_Light2
			TNT1 A 1 A_Light1
			TNT1 A 1 A_Light0
			stop
		Spawn:
			PIST A -1
			Stop
	}
}

actor LightningPuff : BulletPuff
{
	+PUFFONACTORS
	+ALWAYSPUFF
	+FORCEPAIN
	+BLOODLESSIMPACT
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+NOTIMEFREEZE
	+NOBLOOD
	-BLOODSPLATTER
	RenderStyle Add
	Alpha 1.0
	Scale 0.25
	VSpeed 0
	States
	{
		Spawn:
			TBAL A 1 Bright A_FadeOut(0.05)
			loop
	}
}

actor LightningPuff_Painless : LightningPuff
{
	+PAINLESS
	-FORCEPAIN
}

ACTOR Lightning_Rifle: SH_Weapon
{
	Game Doom
	Scale 0.8
	Height 20
	SpawnID 31
	Weapon.SelectionOrder 7
	Weapon.SlotNumber 7
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0 // Handled in ACS
	Weapon.AmmoType "Battery"
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX .5
	Weapon.BobStyle "Smooth"
	Weapon.SwayStyle "HorizontalOnly"
	Weapon.ViewSwaySpeed 1.0
	Weapon.PreferredSkin "Plasma-Marine"
	Inventory.Respawntics"4200"
	Inventory.RestrictedTo "Gosimer"
	Inventory.PickupSound "Weapon/devistatorget"
	Inventory.PickupMessage "Lightning Gun"
	Tag "Lightning Gun"
	//+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+BLOODLESSIMPACT
	-BLOODSPLATTER
	States
	{
	Ready:
		THUN C 0 A_Stopsound(6)
	Ready2:
		THUN A 0 A_playsound("Weapon/litning_IDLE", 6, 1.0, 1)
		THUN A 1 A_WeaponReady
		THUN C 1 A_WeaponReady
		THUN B 1 A_WeaponReady
		THUN C 1 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_Stopsound(6)
		THUN C 1 A_Lower
		Goto Deselect+1
	Select:
		NULL A 0 A_Stopsound(6)
		THUN C 1 A_Raise
		Goto Select+1

	Fire:
		THUN D 0 A_PlaySound("Weapon/Litning_START",Chan_weapon)
		THUN D 1
		THUN E 1
		THUN F 0 A_playsound("Weapon/Litning_LOOP", 6, 1.0, 1)
		goto FireStart
		
	FireStart:
		THUN F 0 A_JumpIfInventory("Tier6upgradetoken",1,"GoldFireStart")
		THUN F 0 A_GunFlash
		THUN F 1 Offset(0, 32) A_RailAttack(6, 0, 1, "00 00 FF", "ff ff ff", RGF_FULLBRIGHT|RGF_SILENT, 10, "LightningPuff_Painless", 0, 0, 1028, 5, 1.0, 4.0, "Lightninggunblast")
		THUN F 1 Offset(0, 0) A_REFIRE ("JiggleRando")
		Goto Fireend
	
	GoldFireStart:
		THUN F 0 A_GunFlash
		THUN F 1 Offset(0, 32) A_RailAttack(12, 0, 1, "00 00 FF", "ff ff ff", RGF_FULLBRIGHT|RGF_SILENT, 10, "LightningPuff", 0, 0, 1028, 5, 1.0, 4.0, "REDLightninggunBlast")
		THUN F 1 Offset(0, 0) A_REFIRE ("JiggleRando")
		Goto Fireend
	
	FireHold:
		THUN F 0 Offset(0, 0) A_JumpIfInventory("Tier6upgradetoken",1,"GoldFirehold")
		THUN F 0 Offset(0, 0) A_GunFlash
		THUN F 0 Offset(0, 0) A_RailAttack(6, 0, 1, "00 00 FF", "ff ff ff", RGF_FULLBRIGHT|RGF_SILENT, 10, "LightningPuff", 0, 0, 1028, 35, 1.0, 4.0, "Lightninggunblast")
		THUN F 1 Offset(0, 0)
		THUN F 1 Offset(0, 0) A_REFIRE ("JiggleRando")
		goto FireEnd
		
	GoldFireHold:
		THUN F 0 Offset(0, 0) A_GunFlash
		THUN F 0 Offset(0, 0) A_RailAttack(12, 0, 1, "00 00 FF", "ff ff ff", RGF_FULLBRIGHT|RGF_SILENT, 10, "LightningPuff", 0, 0, 1028, 35, 1.0, 4.0, "REDLightninggunBlast")
		THUN F 1 Offset(0, 0)
		THUN F 1 Offset(0, 0) A_REFIRE ("JiggleRando")
		goto FireEnd
	
	JiggleRando:
		THUN F 0 Offset(0, 0) A_JUMP(250, "jiggle1", "jiggle2", "jiggle3", "jiggle4", "jiggle5")
		Goto FireHold
	Jiggle1:
		THUN F 0 Offset(1,33)
		goto FireHold
	Jiggle2:
		THUN F 0 Offset(-1,33)
		Goto FireHold
	Jiggle3:
		THUN F 0 Offset(-2,34)
		Goto FireHold
	Jiggle4:
		THUN F 0 Offset(-2,34)
		Goto FireHold
	Jiggle5:
		THUN F 0 Offset(0,33)
		Goto FireHold

	Fireend:
		NULL A 0 A_Stopsound(6)
		NULL A 0 A_PlaySound("Weapon/Litning_STOP", chan_weapon)
		THUN E 1
		THUN D 1
		Goto Ready
	 Flash:
		TNT1 A 1 Bright A_Light1
		Goto LightDone
	 Spawn:
		PLAS A -1
		Stop
	}
}

ACTOR Devistator_shell
{
	scale 0.25
	Radius 11
	Height 8
	Speed 20
	Damage 6
	Projectile
	+SEEKERMISSILE
	+SCREENSEEKER
	+FORCERADIUSDMG
	+NOTIMEFREEZE
	//SeeSound "weapon/D3DDEVSPAWN"
	DeathSound "weapon/D3DDEVBOOM"
	Obituary "%O caught %K's devistator shell" // "%o rode %k's rocket."
	States
	{
		Spawn:
			RPGX AAAAAA 1 bright
			goto Fly
		Fly:
			RPGX A 0 A_playsound("weapon/D3DDEVFLY",6, 1.0, 1)
			RPGX A 0 A_Weave(3, 3, frandom[speen](0.0, 1.0), frandom[speen](0.0, 1.0))
			RPGX A 1 Bright A_SeekerMissile(3,3,SMF_LOOK,50,5)
			Loop
		Death:
			DEXP A 0 Bright A_Explode(50, 128)
			DEXP A 0 Bright A_stopsound(6)
			DEXP A 0 Bright A_SetScale(0.2, 0.2)
			DEXP A 0 Bright A_SetTranslucent(1.0, 1)
			DEXP ABCDEFGHIJKLMNOPQRST 1 Bright
			Stop
	}
}

ACTOR micro_Devistator : SH_Weapon
{
	Game Doom
	Scale 0.8
	Height 20
	SpawnID 31
	Weapon.SelectionOrder 8
	Weapon.slotnumber 8
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0 // Handled in ACS
	Weapon.AmmoType "DevastatorAmmo"
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX .5
	Weapon.BobStyle "Smooth"
	Weapon.SwayStyle "HorizontalOnly"
	Weapon.ViewSwaySpeed 1.0
	+WEAPON.NOAUTOAIM
	Inventory.Forbiddento "Maximusprime"
	Inventory.Respawntics"4200"
	Inventory.ForbiddenTo "Kunai"
	Inventory.ForbiddenTo "Fiend"
	Inventory.RestrictedTo "Gosimer"
	Inventory.PickupSound "Weapon/devistatorget"
	Weapon.PreferredSkin "BFG-Marine"
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "Devastator"
	Tag "Devastator"
	States
	{
	Ready:
		DASS A 0 A_JumpIfInventory("Tier7upgradetoken",1,"GoldReady")
		DASS A 1 A_WeaponReady
		loop
	GoldReady:
		DAGS A 1 A_WeaponReady
		loop
	Deselect:
		DASS A 0 A_JumpIfInventory("Tier7upgradetoken",1,"GoldDeselect")
		DASS A 1 A_Lower
		Loop
	GoldDeselect:
		DAGS A 1 A_Lower
		loop
	Select:
		DASS A 0 A_JumpIfInventory("Tier7upgradetoken",1,"GoldSelect")
		DASS A 1 A_Raise
		Loop
	GoldSelect:
		DAGS A 1 A_Raise
		loop
	Fire:
		DASS A 0 A_JumpIfInventory("Tier7upgradetoken",1,"GoldFire")
		DASS A 0 A_Playsound("weapon/D3DDEVSPAWN",chan_weapon)
		DASS B 1 A_GunFlash
		DASS C 1
		DASS D 2 A_Firecustommissile("Devistator_shell",frandom(-3.0, 3.0),1,0,0,0,frandom(-2.0, 2.0))
		DASS E 2 A_Firecustommissile("Devistator_shell",frandom(-3.0, 3.0),1,0,0,0,frandom(-2.0, 2.0))
		DASS F 2 A_FireCustomMissile("Devistator_shell",frandom(-3.0, 3.0),1,0,0,0,frandom(-2.0, 2.0))
		DASS C 1
		DASS B 1
		DASS A 1 A_Refire
		Goto Ready
	GoldFire:
		DAGS A 0 A_Playsound("weapon/D3DDEVSPAWN",chan_weapon)
		DAGS B 1 A_GunFlash
		DAGS C 1
		DAGS D 1 A_Firecustommissile("Devistator_shell",frandom(-3.0, 3.0),1,0,0,0,frandom(-2.0, 2.0))
		DAGS E 1 A_Firecustommissile("Devistator_shell",frandom(-3.0, 3.0),1,0,0,0,frandom(-2.0, 2.0))
		DAGS F 1 A_FireCustomMissile("Devistator_shell",frandom(-3.0, 3.0),1,0,0,0,frandom(-2.0, 2.0))
		DAGS C 1
		DAGS B 1
		DAGS A 1 A_Refire("GoldFire")
		Goto GoldReady
	Flash:
		TNT1 A 2 Bright
		TNT1 A 2 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone
	Spawn:
		LAUN A -1
		Stop
	}
}

ACTOR HMachinegun : SH_Weapon
{
	Tag "Ripper"
	Weapon.PreferredSkin "Minigun-Marine"
	Weapon.SelectionOrder 3
	Weapon.SlotNumber 5
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0 // Handled in ACS
	Weapon.AmmoType "MGRounds"
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX .5
	Weapon.BobStyle "Smooth"
	Weapon.SwayStyle "HorizontalOnly"
	Weapon.ViewSwaySpeed 1.0
	Inventory.RestrictedTo "Gosimer"
	Inventory.PickupMessage "Heavy machinegun!"
	Inventory.PickupSound "weapon/akget"
	Obituary "%o killed %k"
	States
	{
	Ready:
		D3DG A 0 A_Jumpifinventory("Tier4upgradetoken",1,"GoldReady")
		D3DG A 1 A_WeaponReady
		D3DG A 15 A_WeaponReady
		Loop
	Deselect:
		D3DG A 0 A_Jumpifinventory("Tier4upgradetoken",1,"GoldDeselect")
		D3DG A 1 A_Lower
		Loop
	Select:
		D3DG A 0 A_Jumpifinventory("Tier4upgradetoken",1,"GoldSelect")
		D3DG A 1 A_Raise
		Loop
	Fire:
		D3DG A 0 A_Jumpifinventory("Tier4upgradetoken",1,"GoldFire")
		D3DF ABC 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(3, 2, -1, 9+Random(0,9), "Bulletpuff", FBF_USEAMMO | FBF_NORANDOM)
		D3DF DEF 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(3, 2, -1, 9+Random(0,9), "Bulletpuff", FBF_USEAMMO | FBF_NORANDOM)
		D3DF GHI 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(3, 2, -1, 9+Random(0,9), "Bulletpuff", FBF_USEAMMO | FBF_NORANDOM)
		D3DG A 0 A_ReFire("Hold")
		Goto Ready
	Hold:
		D3DG A 0 A_Jumpifinventory("Tier4upgradetoken",1,"GoldHold")
		D3DF ABC 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(6, 4, -1, 9+Random(0,9), "Bulletpuff", FBF_USEAMMO | FBF_NORANDOM)
		D3DF DEF 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(6, 4, -1, 9+Random(0,9), "Bulletpuff", FBF_USEAMMO | FBF_NORANDOM)
		D3DF GHI 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(6, 4, -1, 9+Random(0,9), "Bulletpuff", FBF_USEAMMO | FBF_NORANDOM)
		D3DG A 0 A_ReFire("Hold")
		Goto Ready
	GoldReady:
		D3GG A 1 A_WeaponReady
		D3GG A 15 A_WeaponReady
		Loop
	GoldDeselect:
		D3GG A 1 A_Lower
		Loop
	GoldSelect:
		TNT1 A 0 A_giveinventory("Tier4upgradetoken", 1)
		D3GG A 1 A_Raise
		Loop
	GoldFire:
		D3GF ABC 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon_Crit", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(2.5, 2, -1, 9+Random(0,9), "GosimersExplosivepuff", FBF_USEAMMO | FBF_NORANDOM)
		D3GF DEF 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon_Crit", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(2.5, 2, -1, 9+Random(0,9), "GosimersExplosivepuff", FBF_USEAMMO | FBF_NORANDOM)
		D3GF GHI 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon_Crit", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(2.5, 2, -1, 9+Random(0,9), "GosimersExplosivepuff", FBF_USEAMMO | FBF_NORANDOM)
		D3GG A 0 A_ReFire("GoldFire")
		Goto Ready
	GoldHold:
		D3GF ABC 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon_Crit", CHAN_WEAPON)
		MNGF A 0 Bright A_FireBullets(5, 3.5, -1, 9+Random(0,9), "GosimersExplosivepuff", FBF_USEAMMO | FBF_NORANDOM)
		D3GF DEF 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon_Crit", CHAN_AUTO)
		MNGF A 0 Bright A_FireBullets(5, 3.5, -1, 9+Random(0,9), "GosimersExplosivepuff", FBF_USEAMMO | FBF_NORANDOM)
		D3GF GHI 1 Bright
		NULL A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, 0, 1)
		MNGG A 0 A_PlaySound("Weapon/D3DAsscannon_Crit", CHAN_AUTO)
		MNGF A 0 Bright A_FireBullets(5, 3.5, -1, 9+Random(0,9), "GosimersExplosivepuff", FBF_USEAMMO | FBF_NORANDOM)
		D3GG A 0 A_ReFire("GoldHold")
		Goto Ready
	Flash:
		TNT1 A 1 Bright A_Light1
		TNT1 A 0 A_Light0
		Goto LightDone
		TNT1 A 1 Bright A_Light1
		TNT1 A 0 A_Light0
		Goto LightDone
	Spawn:
		DEGT A -1
		Stop
	}
}

//Combat Shotgun

ACTOR CombatShotgun : SH_Weapon
{
	Tag "Combat Shotgun"
	Weapon.PreferredSkin "Shotgun-Marine"
	Weapon.SelectionOrder 2
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0 // Handled in ACS
	Weapon.AmmoType "Shellz"
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX .5
	Weapon.BobStyle "Smooth"
	Inventory.Respawntics"4200"
	Weapon.SlotNumber 3
	Weapon.SwayStyle "HorizontalOnly"
	Weapon.ViewSwaySpeed 1.0
	Inventory.Forbiddento "Maximusprime"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "Gosimer"
	Inventory.PickupSound "weapon/singleget"
	Inventory.PickupMessage "CombatShotgun"
	Obituary "%o killed %k"

	States
	{
		Ready:
			GUN2 A 0 A_Jumpifinventory("Tier2upgradetoken",1,"Goldready")
			GUN2 A 1 A_WeaponReady
			GUN2 A 10 A_WeaponReady
			Loop
		Deselect:
			GUN2 A 0 A_Jumpifinventory("Tier2upgradetoken",1,"Golddeselect")
			GUN2 A 1 A_Lower
			Loop
		Select:
			GUN2 A 0 A_Jumpifinventory("Tier2upgradetoken",1,"Goldselect")
			GUN2 A 1 A_Raise
			Loop
		Fire:
			GUN2 A 0 A_Jumpifinventory("Tier2upgradetoken",1,"Goldfire")
			GUN2 A 0 A_FireBullets(5, 4, -1, 10+Random(0,10), "Bulletpuff", FBF_USEAMMO | FBF_NORANDOM)
			GUN2 AAAAAA 0 A_FireBullets(5, 4, -1, 10+Random(0,10), "Bulletpuff", FBF_NORANDOM)
			GUN2 A 0 A_GunFlash
			GUN2 A 1 Offset(0, 48) A_PlaySound("GShotgunFire", CHAN_WEAPON)
			SUN2 A 1 Offset(0, 47)
			GUN2 A 1 Offset(0, 36)
			GUN2 A 1 Offset(0, 34)
			GUN2 A 1 Offset(0, 33)
			GUN2 A 2 Offset(0, 32)
			GUN2 G 3
			GUN2 B 3
			GUN2 C 5 A_PlaySound("GShotgunCock1", CHAN_AUTO)
			GUN2 D 4
			GUN2 C 3 A_FireCustomMissile("SupershotgunCasingSpawner", 0, 0, 0, 2)
			GUN2 B 3 A_PlaySound("GShotgunCock2", CHAN_AUTO)
			GUN2 G 2
			GUN2 A 0
			GUN2 A 0 A_ReFire
			Goto Ready
		GoldReady:
			SUN2 A 1 A_WeaponReady
			SUN2 A 10 A_WeaponReady
			Loop
		GoldDeselect:
			TNT1 A 0 A_giveinventory("Tier2upgradetoken", 1)
			SUN2 A 1 A_Lower
			Loop
		GoldSelect:
			SUN2 A 1 A_Raise
			Loop
		GoldFire:
			SUN2 A 0 A_FireBullets(5, 4, -1, 10+Random(0,10), "GosimersExplosivepuff", FBF_USEAMMO | FBF_NORANDOM)
			SUN2 AAAAAA 0 A_FireBullets(5, 4, -1, 10+Random(0,10), "GosimersExplosivepuff", FBF_NORANDOM)
			SUN2 A 0 A_GunFlash
			SUN2 A 1 Offset(0, 48) A_PlaySound("GShotgunFireGold", CHAN_WEAPON)
			SUN2 A 1 Offset(0, 47)
			SUN2 A 1 Offset(0, 36)
			SUN2 A 1 Offset(0, 34)
			SUN2 A 1 Offset(0, 33)
			SUN2 A 2 Offset(0, 32)
			SUN2 G 3
			SUN2 B 3
			SUN2 C 5 A_PlaySound("GShotgunCock1", CHAN_AUTO)
			SUN2 D 4
			SUN2 C 3 A_FireCustomMissile("SupershotgunCasingSpawner", 0, 0, 0, 2)
			SUN2 B 3 A_PlaySound("GShotgunCock2", CHAN_AUTO)
			SUN2 G 2
			SUN2 A 0
			SUN2 A 0 A_ReFire
			Goto Ready
		AltFire:
			GUN1 A 0 A_jumpifinventory("Tier2upgradetoken",1,"Goldkick")
			GUNK A 2
			GUNK B 2 A_playsound("Weapon/Quickkick",Chan_weapon)
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
			GUNK C 3 A_CustomPunch(2 * random(1, 10), TRUE,0,"Shotgunmayray",96)
			GUNK D 3
			GUNK E 2
			GUNK A 2
			GUN2 A 5
			goto Ready
		Berserked:
			GUNK C 3 A_CustomPunch(20 * random(1, 10), TRUE,0,"Shotgunmayray",96)
			GUNK D 3
			GUNK E 2
			GUNK A 2
			GUN2 A 5
			goto Ready
		Goldkick:
			SUNK A 2
			SUNK B 2 A_playsound("Weapon/Quickkick",Chan_weapon)
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "GoldBerserked")
			SUNK C 3 A_CustomPunch(4 * random(1, 10), TRUE,0,"Shotgunmayray",96)
			SUNK D 3
			SUNK E 2
			SUNK A 2
			SUN2 A 5
			goto Ready
		GoldBerserked:
			SUNK C 3 A_CustomPunch(40 * random(1, 10), TRUE,0,"Shotgunmayray",96)
			SUNK D 3
			SUNK E 2
			SUNK A 2
			SUN2 A 5
			goto Ready
		Flash:
			GUN2 F 2 Bright A_Light2
			GUN2 E 1 Bright A_Light1
			TNT1 A 0 A_Light0
			Goto LightDone

		Spawn:
			SHOT A -1
			Stop
	}
}

// Burst shotgun
Actor BurstShotgun : SH_Weapon
{
	Weapon.PreferredSkin "SSG-Marine"
	radius 20
	height 16
	Weapon.SelectionOrder 3
	Weapon.slotnumber 4
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoType1 "SuperShellz"
	Weapon.AmmoType2 "SuperShellz"
	Weapon.AmmoGive1 0 // Handled in ACS
	Weapon.AmmoGive2 0
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX .5
	Weapon.BobStyle "Smooth"
	Weapon.SwayStyle "HorizontalOnly"
	Weapon.ViewSwaySpeed 1.0
	Inventory.RestrictedTo "Gosimer"
	inventory.pickupsound "weapon/GSSGGET"
	inventory.pickupmessage "You got the BurstShotgun"
	obituary "%o was Torn apart by %k's BurstShotgun."
	Tag "UAC Prototype Burst-Shotgun" 
	States
	{
		Ready:
			STBE A 0 A_Jumpifinventory("Tier3upgradetoken",1,"GoldReady")
			STBE A 1 A_WeaponReady
			STBE A 10 A_WeaponReady
			Loop
		Select:
			STBE A 0 A_Jumpifinventory("Tier3upgradetoken",1,"GoldSelect")
			STBE A 1 A_Raise
			Loop
		Deselect:
			STBE A 0 A_Jumpifinventory("Tier3upgradetoken",1,"GoldDeselect")
			STBE A 1 A_Lower
			Loop
		GoldReady:
			GTBE A 1 A_WeaponReady
			GTBE A 10 A_WeaponReady
			Loop
		GoldSelect:
			TNT1 A 0 A_giveinventory("Tier3upgradetoken", 1)
			GTBE A 1 A_Raise
			Loop
		GoldDeselect:
			GTBE A 1 A_Lower
			Loop
		Spawn:
			STBE X -1
			Stop
			
		ALTFire:
			TNT1 A 0 A_Jumpifinventory("Tier3upgradetoken",1,"GoldALTFire")
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_FireBullets(3, 3, 10, 5.5, "BulletPuff")
			TNT1 A 0 A_playsound("SSHTGN",Chan_6)
			STBE GF 2 BRIGHT
			STBE A 8
		ALTReloading:
			STBE A 5
			STBE BC 2
			TNT1 A 0 A_PlaySound("PulseShotgun/Pump", chan_weapon)
			STBE D 1 Offset(0,35)
			STBE D 1 Offset(0,37)
			STBE D 1 Offset(0,39)
			STBE D 1 Offset(0,41)
			STBE E 1 Offset(0,43)
			STBE E 1 Offset(0,45)
			STBE E 1 Offset(0,47)
			STBE E 1 Offset(0,49)
			STBE D 2 Offset(0,49)
			STBE D 0 A_FireCustomMissile("ShotgunCasingSpawner", 0, 0, 0, -1)
			STBE C 1 Offset(0,47)
			STBE C 1 Offset(0,45)
			STBE C 1 Offset(0,43)
			STBE C 1 Offset(0,41)
			STBE C 1 Offset(0,39)
			STBE C 1 Offset(0,37)
			STBE C 1 Offset(0,35)
			STBE BA 2
			Goto Ready
			
		Fire:
			TNT1 A 0 A_Jumpifinventory("Tier3upgradetoken",1,"GoldFire")
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_FireBullets(8, 8, 10, 6.5, "BulletPuff")
			TNT1 A 0 A_playsound("SSHTGN",Chan_weapon)
			STBE GF 2 BRIGHT
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_FireBullets(8, 8, 10, 6.5, "BulletPuff")
			TNT1 A 0 A_playsound("SSHTGN",Chan_weapon)
			STBE GF 2 BRIGHT
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_FireBullets(8, 8, 10, 6.5, "BulletPuff")
			TNT1 A 0 A_playsound("SSHTGN",Chan_weapon)
			STBE GF 2 BRIGHT
		Reloading:
			STBE A 5
			STBE BC 2
			TNT1 A 0 A_PlaySound("PulseShotgun/Pump", chan_7)
			STBE D 1 Offset(0,35)
			STBE D 1 Offset(0,37)
			STBE D 1 Offset(0,39)
			STBE D 1 Offset(0,41)
			STBE E 1 Offset(0,43)
			STBE E 1 Offset(0,45)
			STBE E 1 Offset(0,47)
			STBE E 1 Offset(0,49)
			STBE D 2 Offset(0,49)
			STBE D 0 A_FireCustomMissile("ShotgunCasingSpawner", 0, 0, 0, -1)
			STBE C 1 Offset(0,47)
			STBE C 1 Offset(0,45)
			STBE C 1 Offset(0,43)
			STBE C 1 Offset(0,41)
			STBE C 1 Offset(0,39)
			STBE C 1 Offset(0,37)
			STBE C 1 Offset(0,35)
			TNT1 A 0 A_PlaySound("PulseShotgun/Pump", chan_7)
			STBE D 1 Offset(0,35)
			STBE D 1 Offset(0,37)
			STBE D 1 Offset(0,39)
			STBE D 1 Offset(0,41)
			STBE E 1 Offset(0,43)
			STBE E 1 Offset(0,45)
			STBE E 1 Offset(0,47)
			STBE E 1 Offset(0,49)
			STBE D 2 Offset(0,49)
			STBE D 0 A_FireCustomMissile("ShotgunCasingSpawner", 0, 0, 0, -1)
			STBE C 1 Offset(0,47)
			STBE C 1 Offset(0,45)
			STBE C 1 Offset(0,43)
			STBE C 1 Offset(0,41)
			STBE C 1 Offset(0,39)
			STBE C 1 Offset(0,37)
			STBE C 1 Offset(0,35)
			TNT1 A 0 A_PlaySound("PulseShotgun/Pump", chan_7)
			STBE D 1 Offset(0,35)
			STBE D 1 Offset(0,37)
			STBE D 1 Offset(0,39)
			STBE D 1 Offset(0,41)
			STBE E 1 Offset(0,43)
			STBE E 1 Offset(0,45)
			STBE E 1 Offset(0,47)
			STBE E 1 Offset(0,49)
			STBE D 2 Offset(0,49)
			STBE D 0 A_FireCustomMissile("ShotgunCasingSpawner", 0, 0, 0, -1)
			STBE C 1 Offset(0,47)
			STBE C 1 Offset(0,45)
			STBE C 1 Offset(0,43)
			STBE C 1 Offset(0,41)
			STBE C 1 Offset(0,39)
			STBE C 1 Offset(0,37)
			STBE C 1 Offset(0,35)
			STBE BA 2		
			Goto Ready
			
			
		GoldFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_playsound("SSHTGN", CHAN_WEAPON)
			TNT1 A 0 A_FireBullets(6, 6, 10, 6.5, "GosimersExplosivePuff")
			TNT1 A 0 A_Jumpifnoammo("Ready")
			GTBE GF 2 BRIGHT
			TNT1 A 0 A_playsound("SSHTGN", CHAN_6)
			TNT1 A 0 A_FireBullets(6, 6, 10, 6.5, "GosimersExplosivePuff")
			TNT1 A 0 A_Jumpifnoammo("Ready")
			GTBE GF 2 BRIGHT
			TNT1 A 0 A_FireBullets(6, 6, 10, 6.5, "GosimersExplosivePuff")
			TNT1 A 0 A_playsound("SSHTGN", CHAN_WEAPON)
			GTBE GF 2 BRIGHT
			goto GoldReloading
		GoldReloading:
			GTBE A 5
			GTBE BC 2
			TNT1 A 0 A_PlaySound("PulseShotgun/Pump", chan_7)
			GTBE D 1 Offset(0,37)
			GTBE D 1 Offset(0,41)
			GTBE E 1 Offset(0,43)
			GTBE E 1 Offset(0,47)
			GTBE D 2 Offset(0,49)
			GTBE D 0 A_FireCustomMissile("ShotgunCasingSpawner", 0, 0, 0, -1)
			GTBE C 1 Offset(0,45)
			GTBE C 1 Offset(0,41)
			GTBE C 1 Offset(0,37)
			TNT1 A 0 A_PlaySound("PulseShotgun/Pump", chan_7)
			GTBE D 1 Offset(0,37)
			GTBE D 1 Offset(0,41)
			GTBE E 1 Offset(0,45)
			GTBE E 1 Offset(0,49)
			GTBE D 2 Offset(0,49)
			GTBE D 0 A_FireCustomMissile("ShotgunCasingSpawner", 0, 0, 0, -1)
			GTBE C 1 Offset(0,45)
			GTBE C 1 Offset(0,39)
			GTBE C 1 Offset(0,35)
			TNT1 A 0 A_PlaySound("PulseShotgun/Pump", chan_7)
			GTBE D 1 Offset(0,35)
			GTBE D 1 Offset(0,39)
			GTBE E 1 Offset(0,43)
			GTBE E 1 Offset(0,47)
			GTBE D 2 Offset(0,49)
			GTBE D 0 A_FireCustomMissile("ShotgunCasingSpawner", 0, 0, 0, -1)
			GTBE C 1 Offset(0,47)
			GTBE C 1 Offset(0,43)
			GTBE C 1 Offset(0,39)
			GTBE C 1 Offset(0,35)
			GTBE BA 2		
			Goto Ready
			
		GoldALTFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_FireBullets(3, 3, 10, 6, "GosimersExplosivePuff")
			TNT1 A 0 A_playsound("SSHTGN", CHAN_WEAPON)
			GTBE GF 2 BRIGHT
			GTBE A 4
			goto GoldALTReloading
		GoldALTReloading:
			GTBE A 4
			GTBE BC 1
			TNT1 A 0 A_PlaySound("PulseShotgun/Pump", chan_weapon)
			GTBE D 1 Offset(0,35)
			GTBE D 1 Offset(0,37)
			GTBE D 1 Offset(0,39)
			GTBE D 1 Offset(0,41)
			GTBE E 1 Offset(0,43)
			GTBE E 1 Offset(0,45)
			GTBE E 1 Offset(0,47)
			GTBE E 1 Offset(0,49)
			GTBE D 2 Offset(0,49)
			GTBE D 1 A_FireCustomMissile("ShotgunCasingSpawner", 0, 0, 0, -1)
			GTBE C 1 Offset(0,47)
			GTBE C 1 Offset(0,45)
			GTBE C 1 Offset(0,43)
			GTBE C 1 Offset(0,41)
			GTBE C 1 Offset(0,39)
			GTBE C 1 Offset(0,37)
			GTBE C 1 Offset(0,35)
			GTBE BA 1
			Goto Ready
	}
}

Actor Propulsion_Cannon : SH_Weapon
{
	Weapon.PreferredSkin "Carbine-Marine"
	Radius 20
	Height 16
	Weapon.Selectionorder 100
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "Railslugs"
	Weapon.SlotNumber 6 // This line isn't in skulltag.pk3, which instead defines the slot directly in DoomPlayer
	Inventory.Pickupmessage "$PICKUP_RAILGUN" // "You got the railgun!"
	Obituary "$OB_RAILGUN" // "%o was railed by %k."
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX .5
	Weapon.BobStyle "Smooth"
	Weapon.SlotNumber 7
	Weapon.SwayStyle "HorizontalOnly"
	Weapon.ViewSwaySpeed 1.0
	Inventory.Forbiddento "Maximusprime"
	Inventory.Respawntics"4200"
	Inventory.ForbiddenTo "Kunai"
	Inventory.ForbiddenTo "Fiend"
	Inventory.RestrictedTo "Gosimer"
	Inventory.PickupSound "Weapon/railget"
	Weapon.PreferredSkin "BFG-Marine"
	Inventory.PickupMessage "Gosimers propulsion cannon"
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	States
	{
		Ready:
			GSNP A 1 A_WeaponReady
			Loop
		Deselect:
			GSNP A 1 A_Lower
			Loop
		Select:
			GSNP A 1 A_Raise
			Loop
		Fire:
			GSNF A 1 BRIGHT A_RailAttack(100, 0, 1, "FF FF FF", "FF FF FF", RGF_FULLBRIGHT|RGF_SILENT, 0, "BulletPuff", 0, 0, 0, 35, 2.0, 4.0, "REDLightRailBlast")
			GSNF A 0 A_playsound("Weapon/Sniperrail_Fire",Chan_Weapon)
			GSNF B 1 BRIGHT
			//GSNP B 1 A_playsound("Weapon/SniperRecharge", Chan_7)
			GSNP B 9
			GSNP A 0 A_Refire
			Goto Ready
		//Reloaded:
			//GSNP B 6
			//GSNP C 2
			//GSNP A 6 A_ReFire
			//Goto Ready
		Flash:
			TNT1 A 5 bright A_Light1
			TNT1 A 5 bright A_Light2
			TNT1 A 0 bright A_Light0
			Goto LightDone
		Spawn:
			GSNP G -1
			Stop
	}
}