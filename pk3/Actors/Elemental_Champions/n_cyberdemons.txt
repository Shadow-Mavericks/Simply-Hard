ACTOR N_Cyberdemon_Base : SH_BaseMonster 
{
	Species "Cyberdemon"
	Health 4000
	Radius 40
	Height 110
	Mass 1000
	Speed 16
	MinMissileChance 160
	SeeSound "cyber/sight"
	PainSound "cyber/pain"
	DeathSound "cyber/death"
	ActiveSound "cyber/active"
	Obituary "$OB_CYBORG"
	MissileType "Rawket"
	+BOSS
	+MISSILEMORE
	+NORADIUSDMG
	+DONTMORPH
	+DONTRIP
	States
	{
		Spawn:
			CYBR A 0
		Spawn2:
			CYBR AB 10 A_Look
			Loop
		See:
			CYBR A 3 A_Hoof
			CYBR ABBCC 3 A_Chase
			CYBR D 3 A_Metal
			CYBR D 3 A_Chase
			Loop
		Missile:
			CYBR E 12 A_FaceTarget
			CYBR F 12 Bright A_MissileAttack
			CYBR E 12 A_FaceTarget
			CYBR F 12 Bright A_MissileAttack
			CYBR E 12 A_FaceTarget
			CYBR F 12 Bright A_MissileAttack
			Goto See
		Pain:
			CYBR G 10 A_Pain
			Goto See
		Death:
			CYBR H 10
			TNT1 AAA 0 A_CustomMissile ("XDeath1", 52, 0, random (0, 360), 2, random (0, 160))
			TNT1 AAA 0 A_CustomMissile ("XDeath2", 52, 0, random (0, 360), 2, random (0, 160))
			TNT1 AAA 0 A_CustomMissile ("XDeath3", 52, 0, random (0, 360), 2, random (0, 160))
			TNT1 AA 0 A_CustomMissile ("XDeath4", 52, 0, random (0, 360), 2, random (0, 160))
			TNT1 A 0 A_CustomMissile ("XDeathGuts", 52, 0, random (0, 360), 2, random (0, 160))
			TNT1 AAAAAAA 0 A_CustomMissile ("Instestin", 52, 0, random (0, 360), 2, random (0, 160))
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("Brutal_FlyingBlood", 55, 0, random (0, 360), 2, random (0, 160))
			TNT1 AAAAAAAAAA 0 bright A_CustomMissile ("SuperGoreSpawner", 52, 0, random (0, 360), 2, random (50, 130))
			CYBR I 10 A_Scream
			CYBR JKL 10
			CYBR M 10 A_NoBlocking
			CYBR NO 10
			TNT1 A 0 A_CustomMissile ("GiantBloodSpot", 4, 0, random (0, 360), 2, random (0, 160))
			CYBR P 30
			CYBR P -1 A_BossDeath
			Stop
		Crush:
			TNT1 AAAAAAAAAA 0 A_CustomMissile ("BloodTr255", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 A 0 A_PlaySound("misc/xdeath4")
			TNT1 A 0 A_CustomMissile ("GiantBloodSpot", 4, 0, random (0, 360), 2, random (0, 160))
			XBIG E -1
			Stop
	}
}


//Translation "48:79=[128, 255, 128]:[32, 64, 32]", 

actor Rocket_Acid : Rawket
{ 
	DamageType "Acid" 
}

actor Rocket_Void : Rawket
{ 
	DamageType "Void" 
}

actor Rocket_Electric : Rawket
{ 
	DamageType "Electric" 
	states
	{
		Spawn:
			MISL AAAAAAA 0 A_SpawnItemEx ("CryoSmoke3", 0, 0, 0,  (momx*0.5), (momy*0.5), (momz*0.5), 0)
			MISL AAAA 0 A_SpawnItemEx ("CryoSmoke4", 0, 0, 0,  (momx*0.5), (momy*0.5), (momz*0.5), 0)
			MISL A 1 A_SpawnItemEx("Particle_Lightning16")
			Loop
		Death:
			MISL B 0 Bright A_Explode
			PXPL AAAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			PXPL AAAA 0 Bright A_SpawnItemEx("CryoSmoke2", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
			PXPL ABCDEFGH 1 Bright
			PXPL IJKLLM 1 Bright A_FadeOut(0.1)
			TNT1 AAA 3 Bright A_SpawnItemEx("Particle_Lightning16")
			TNT1 AA 3 Bright A_SpawnItemEx("Particle_Lightning15")
			TNT1 A 3 Bright A_SpawnItemEx("Particle_Lightning14")
			TNT1 AAAAAA 3 A_SpawnItemEx("Particle_Lightning13")
			Stop
	}
}

actor Rocket_Ice : Rawket
{ 
	DamageType "Ice" 
}

actor Rocket_Fire : Rawket
{ 
	DamageType "Fire" 
}

actor Rocket_Heavymetal : Rawket
{ 
	DamageType "Heavymetal" 
}

ACTOR N_Cyberdemon_Acid : N_Cyberdemon_Base 
{
	PainChance 20
	MissileType "Rocket_Acid"
	Translation "48:79=[128, 255, 128]:[32, 64, 32]", "144:151=[128, 255, 128]:[32, 64, 32]"
}

ACTOR N_Cyberdemon_Void : N_Cyberdemon_Base 
{
	PainChance 30
	MissileType "Rocket_Void"
	Translation "48:79=[192, 128, 255]:[16, 0, 32]", "144:151=[192, 128, 255]:[16, 0, 32]"
}

ACTOR N_Cyberdemon_Fire : N_Cyberdemon_Base 
{
	PainChance 20
	MissileType "Rocket_Fire"
	Translation "48:79=[255, 128, 0]:[64, 32, 0]", "144:151=[255, 128, 0]:[64, 32, 0]"
}

ACTOR N_Cyberdemon_Electric : N_Cyberdemon_Base 
{
	PainChance 40
	MissileType "Rocket_Electric"
	Translation "48:79=[255, 255, 96]:[64, 64, 32]", "144:151=[255, 255, 96]:[64, 64, 32]"
}

ACTOR N_Cyberdemon_Ice : N_Cyberdemon_Base 
{
	PainChance 10
	MissileType "Rocket_Ice"
	Translation "48:79=[128, 192, 255]:[32, 48, 64]", "144:151=[128, 192, 255]:[32, 48, 64]"
}

ACTOR N_Cyberdemon_HeavyMetal : N_Cyberdemon_Base 
{
	Painchance 0
	MissileType "Rocket_HeavyMetal"
	Translation "48:79=[255, 255, 255]:[0, 0, 0]", "144:151=[255, 255, 255]:[0, 0, 0]"
}

ACTOR N_Cyberdemon_Magic : N_Cyberdemon_Base 
{
	PainChance 40
	Translation "48:79=[0, 128, 255]:[32, 32, 32]", "144:151=[0, 128, 255]:[32, 32, 32]", "1:1=[0, 16, 32]:[0, 16, 32]"
	States
	{
		Missile:
			CYBR E 6 A_FaceTarget
			CYBR F 1 Bright A_CustomMissile ("Baronball_acid",40,-27, random(-45, 45))
			CYBR E 3 A_FaceTarget
			CYBR F 1 Bright A_CustomMissile ("Baronball_fire",40,-27, random(-45, 45))
			CYBR E 3 A_FaceTarget
			CYBR F 1 Bright A_CustomMissile ("Baronball_Ice",40,-27, random(-45, 45))
			CYBR E 3 A_FaceTarget
			CYBR F 1 Bright A_CustomMissile ("Baronball_void",40,-27, random(-45, 45))
			CYBR E 3 A_FaceTarget
			CYBR F 1 Bright A_CustomMissile ("Baronball_metal",40,-27, random(-45, 45))
			CYBR E 3 A_FaceTarget
			CYBR F 1 Bright A_CustomMissile ("Baronball_electric",40,-27, random(-45, 45))
			CYBR F 0 A_SpidRefire
			Goto See
	}
}













