// New blood for Simply Hard
// Code By: StrikerTheHedgefox

actor SH_Blood : Blood Replaces Blood
{
	Radius 2
	Height 1
	Gravity 0.25
	Scale 0.75
	+FORCEXYBILLBOARD
	+THRUACTORS
	+NOTRIGGER
	+NOTELEPORT
	+DROPOFF
	+NOTIMEFREEZE
	+DECALUSESTRANSLATION
	+DECALUSESSPRITESCALE
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_ChangeVelocity(frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), CVF_REPLACE)
			TNT1 A 0 A_SetScale(scalex+frandom(-0.375, 0.0))
			TNT1 A 0 A_SpawnItemEx("SH_BloodSpurt", 0, 0, 0, frandom(1.25, 2.5), 0, frandom(1.5, 3.0), frandom(-180.0, 180.0), SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSCALE, 0)
			TNT1 A 0 A_SpawnItemEx("SH_BloodSpurt", 0, 0, 0, frandom(1.25, 2.5), 0, frandom(1.5, 3.0), frandom(-180.0, 180.0), SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSCALE, 128)
			TNT1 A 0 A_SpawnItemEx("SH_BloodDroplet", 0, 0, 0, frandom(0.125, 3.0), 0, frandom(1.0, 6.0), frandom(-180.0, 180.0), SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSCALE, 128)
			TNT1 A 0 A_SpawnItemEx("SH_BloodDrip", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSCALE, 0)
			BLUD CBA 3
			stop
	}
}

actor SH_BloodSpurt : SH_Blood
{
	Decal BloodDrip
	Scale 1.0
	
	+MISSILE
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			BLDR A 0 NoDelay A_SetScale(scalex+frandom(-0.125, 0.125))
		SpawnLoop:
			BLDR A 2 A_JumpIf(velz < 0, "Fall")
			loop
		Fall:
			BLDR BCDEFG 2
			stop
	}
}

actor SH_BloodDroplet : SH_Blood
{
	Decal BloodDrip
	BounceFactor 0.0
	Scale 1.0
	
	var int user_numdrips;
	var int user_driptimer;
	
	+MISSILE
	+CLIENTSIDEONLY
	+DOOMBOUNCE
	+USEBOUNCESTATE
	+BOUNCEONCEILINGS
	+BOUNCEONFLOORS
	+BOUNCEAUTOOFFFLOORONLY
	-BOUNCEONWALLS	
	States
	{
		Spawn:
			BLDD A 0 NoDelay A_SetScale(scalex+frandom(-0.125, 0.0))
			BLDD A 0 A_Jump(127, "Spawn2", "Spawn3", "Spawn4")
			goto SpawnLoop
		Spawn2:
			BLDD A 0 A_SpawnItemEx("SH_BloodDroplet2", 0, 0, 0, velx, vely, velz, 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION, 0)
			stop
		Spawn3:
			BLDD A 0 A_SpawnItemEx("SH_BloodDroplet3", 0, 0, 0, velx, vely, velz, 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION, 0)
			stop
		Spawn4:
			BLDD A 0 A_SpawnItemEx("SH_BloodDroplet4", 0, 0, 0, velx, vely, velz, 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION, 0)
			stop
			
		SpawnLoop:
			BLDD A 2 A_SpawnItemEx("SH_BloodDropletTrail", 0, 0, 0, frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 0)
			loop
			
		Crash:
		Bounce.Floor:
			BLDD A 0 A_SpawnItemEx("SH_BloodSplash", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSCALE, 0)
			BLDD A 0 A_ChangeFlag("MOVEWITHSECTOR", true)
			BLDD A 0 A_ChangeFlag("FORCEYBILLBOARD", true)
			BLDD A 0 A_ChangeFlag("FORCEXYBILLBOARD", false)
			BLDD A 0 A_ChangeFlag("NOGRAVITY", false)
			BLDD A 0 A_ChangeFlag("MISSILE", false)
			BLDD A 0 A_JumpTo("Splat")
			stop
			
		Bounce.Ceiling:
			CBLD A 0 A_SetUserVar("user_numdrips", random[drip](4, 10))
			CBLD A 0 A_ChangeFlag("NOGRAVITY", true)
			CBLD A 0 A_ChangeFlag("LOCKEDTOCEILING", true)
			CBLD A 0 A_ChangeFlag("FORCEYBILLBOARD", true)
			CBLD A 0 A_ChangeFlag("FORCEXYBILLBOARD", false)
			CBLD A 0 A_ChangeFlag("MISSILE", false)
			CBLD A 0 A_ChangeFlag("BOUNCEONCEILINGS", false)
			CBLD A 0 A_ChangeFlag("BOUNCEONFLOORS", false)
			CBLD A 0 A_ChangeFlag("DOOMBOUNCE", false)
			CBLD A 0 A_ChangeFlag("USEBOUNCESTATE", false)
			CBLD A 0 A_PlaySound("BloodDrip", CHAN_AUTO)
			CBLD A 0 A_Jump(256, "CeilingSplat1", "CeilingSplat2", "CeilingSplat3")
			stop
			
		CeilingSplat1:
			CBLD ABCD 2
			goto StartDripWait
		CeilingSplat2:
			CBL2 ABCD 2
			goto StartDripWait
		CeilingSplat3:
			CBL3 ABCD 2
			goto StartDripWait

		StartDripWait:
			"####" D 0 A_SetUserVar(user_driptimer, random(20, 40))
			goto DripWait
		DripWait:
			"####" D 1 A_SetUserVar("user_driptimer", user_driptimer-1)
			"####" D 0 A_JumpIf(user_driptimer <= 0, "Drip")
			loop
		Drip:
			"####" EFGHIJ 2
			"####" K 0 A_SetUserVar("user_numdrips", user_numdrips-1)
			"####" K 0 A_JumpIf(user_numdrips <= 0, "StartFall")
			"####" K 0 A_SpawnItemEx("SH_BloodDrip", 0, 0, -11.0*scaley, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSCALE, 0)
			"####" KL 2 
			goto StartDripWait
			
		StartFall:
			"####" A 0 A_ChangeFlag("FORCEYBILLBOARD", false)
			"####" A 0 A_ChangeFlag("FORCEXYBILLBOARD", true)
			"####" A 0 A_ChangeFlag("NOGRAVITY", false)
			"####" A 0 A_ChangeFlag("LOCKEDTOCEILING", false)
			"####" A 0 A_ChangeFlag("CORPSE", true)
			"####" A 0 A_SetGravity(0.25) // Just in case...
			"####" A 0 A_JumpTo("Fall")
			stop

		Fall:
			BLDD A 1
			loop
			
		Splat:
			BLDD B 0 A_PlaySound("BloodDrip", CHAN_AUTO)
			BLDD BCD 3
			BLDD E 2100
			stop
		Death:
			TNT1 A 0 A_PlaySound("BloodDrip", CHAN_AUTO)
			TNT1 A 1
			stop
	}
}

actor SH_BloodDroplet2 : SH_BloodDroplet
{
	States
	{
		Spawn:
			BLS2 ABCDEF 2 A_SpawnItemEx("SH_BloodDropletTrail", 0, 0, 0, frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 0)
			loop
		Fall:
			BLS2 ABCDEF 2
			loop
		Splat:
			BLS2 GHIJKLMN 3
			BLS2 O 2100
			stop
	}
}

actor SH_BloodDroplet3 : SH_BloodDroplet
{
	States
	{
		Spawn:
			BLS3 ABCDEFGH 2 A_SpawnItemEx("SH_BloodDropletTrail", 0, 0, 0, frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 0)
			loop
		Fall:
			BLS3 ABCDEFGH 2
			loop
		Splat:
			BLS3 IJKLM 2
			BLS3 N 2100
			stop
	}
}

actor SH_BloodDroplet4 : SH_BloodDroplet
{
	States
	{
		Spawn:
			BLS4 ABCDEFGH 2 A_SpawnItemEx("SH_BloodDropletTrail", 0, 0, 0, frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 0)
			loop
		Fall:
			BLS4 ABCDEFGH 2
			loop
		Splat:
			BLS4 IJKLM 2
			BLS4 N 2100
			stop
	}
}

actor SH_BloodSplash : SH_Blood
{
	+CLIENTSIDEONLY
	+MOVEWITHSECTOR
	States
	{
		Spawn:
			BSQ1 A 0 A_Jump(128, "Splat")
			BSQ1 A 0 A_SetScale(scalex*-1, scaley)
			goto Splat
		Splat:
			BSQ1 EFGHI 3
			stop
	}
}

actor SH_BloodDrip : SH_Blood
{
	+FORCEYBILLBOARD
	+CORPSE
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			BDRP A 1
			loop
		Crash:
		Death:
			BDRP A 0 A_ChangeFlag("MOVEWITHSECTOR", true)
			BDRP A 0 A_PlaySound("BloodDrip", CHAN_AUTO, frandom(0.375, 0.5))
			BDRP BCDE 2
			stop
	}
}

actor SH_BloodDropletTrail : SH_Blood
{
	+MISSILE
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			BLD1 A 0 NoDelay A_Jump(128, "DoJump")
			BLD1 A 0 A_SetScale(scalex*-1, scaley)
			goto DoJump
		DoJump:
			BLD1 A 0 A_Jump(256, "Spawn1", "Spawn2", "Spawn3")
			goto Spawn1
		Spawn1:
			BLD2 ABCDEFG 2
			stop
		Spawn2:
			BLD3 ABCDEF 2
			stop
		Spawn3:
			BLD4 ABCDEFG 2
			stop
		Death:
			TNT1 A 0
			stop
	}
}

actor SH_GasterDust : SH_Blood
{
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetScale(scalex+frandom(-0.5, 0.25))
			TNT1 AAA 0 A_SpawnItemEx("SH_GasterDustParticle", 0, 0, 0, frandom(-0.75, 0.75), frandom(-0.75, 0.75), frandom(0.0, 0.75), 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSCALE | SXF_NOCHECKPOSITION, 127)
			stop
	}
}

actor SH_GasterDustParticle : SH_Blood
{
	RenderStyle Add
	Alpha 1.0
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			BLD1 A 0 NoDelay A_Jump(128, "DoJump")
			BLD1 A 0 A_SetScale(scalex*-1, scaley)
			goto DoJump
		DoJump:
			BLD1 A 0 A_Jump(256, "Spawn1", "Spawn2")
			goto Spawn1
		Spawn1:
			BLD2 BCDEFG 3
			stop
		Spawn2:
			BLD4 BCDEFG 3
			stop
	}
}

actor SH_BloodMist : BloodSplatter replaces BloodSplatter
{
	Radius 2
	Height 2
	RenderStyle Translucent
	Alpha 0.25
	+NOINTERACTION
	+NOGRAVITY
	+NOCLIP
	+NOBLOCKMAP
	+FORCEXYBILLBOARD
	+NEVERADJUSTSPRITECLIPPING
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Stop
			TNT1 A 0 A_SetScale(frandom(0.5, 1.0))
			TNT1 A 0 A_Jump(128, "Animate")
			TNT1 A 0 A_SetScale(scalex*-1)
			TNT1 A 0 A_Jump(128, "Animate")
			TNT1 A 0 A_SetScale(scalex, scaley*-1)
			goto Animate
		Animate:
			PUF4 A 0 A_PlaySound("DeepFry2", CHAN_AUTO, 0.32)
			PUF4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
			PUF5 A 1
			stop
	}
}

ACTOR SH_GibSpawner : CustomInventory
{
	+NOTIMEFREEZE
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Inventory.Amount 1
	Inventory.MaxAmount 0
	Inventory.InterHubAmount 1
	States
	{
		Spawn:
			BGB1 A -1
			Stop
		Use:
			TNT1 A 0
			TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE", true)
			TNT1 A 0 A_XScream
			TNT1 A 0 A_NoBlocking
			TNT1 AAAAAAAA 0 A_SpawnItemEx("SH_BloodSpurt", 0, 0, frandom(1.0, height-1.0), frandom(1.25, 2.5)*(radius/20.0), 0, frandom(1.5, 3.0), frandom(-180.0, 180.0), SXF_CLIENTSIDE | SXF_USEBLOODCOLOR, 64)
			TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("SH_BloodDroplet", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 8.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 64)
			TNT1 A 0 A_SpawnItemEx("SH_BouncyGib1", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 0)
			TNT1 AAAA 0 A_SpawnItemEx("SH_BouncyGib2", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 64)
			TNT1 AA 0 A_SpawnItemEx("SH_BouncyGib3", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 32)
			TNT1 A 0 A_SpawnItemEx("SH_BouncyGib4", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 0)
			TNT1 A 0 A_SpawnItemEx("SH_BouncyGib5", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 0)
			TNT1 AAAA 0 A_SpawnItemEx("SH_BouncyGib6", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 0)
			TNT1 A 0 A_SpawnItemEx("SH_BouncyGib7", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 0)
			TNT1 AAA 0 A_SpawnItemEx("SH_BouncyGib8", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 64)
			TNT1 AAA 0 A_SpawnItemEx("SH_BouncyGib9", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 64)
			TNT1 A 0 A_SpawnItemEx("SH_GibSplash", 0, 0, 0, velx/4.0, vely/4.0, 0, 0, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_SETMASTER | SXF_ABSOLUTEVELOCITY, 0)	
			TNT1 A 0 A_SpawnItemEx("SH_GibMist", 0, 0, height/2.0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_SETMASTER, 0)		
			TNT1 A 0 A_JumpIf((radius >= 31.0) || (height >= 64.0), "ExtraChunky")
			goto DoChecks
			
		ExtraChunky:
			TNT1 AAAAA 0 A_SpawnItemEx("SH_BloodSpurt", 0, 0, frandom(1.0, height-1.0), frandom(1.25, 2.5)*(radius/20.0), 0, frandom(1.5, 3.0), frandom(-180.0, 180.0), SXF_CLIENTSIDE | SXF_USEBLOODCOLOR, 64)
			TNT1 AAAAA 0 A_SpawnItemEx("SH_BloodDroplet", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 8.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 128)
			TNT1 AA 0 A_SpawnItemEx("SH_BouncyGib1", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 0)
			TNT1 A 0 A_SpawnItemEx("SH_BouncyGib5", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 0)
			TNT1 A 0 A_SpawnItemEx("SH_BouncyGib7", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 0)
			TNT1 A 0 A_JumpIf((radius >= 64.0) || (height >= 80.0), "EvenChunkier")
			goto DoChecks
		
		EvenChunkier:
			TNT1 AAA 0 A_SpawnItemEx("SH_BloodSpurt", 0, 0, frandom(1.0, height-1.0), frandom(1.25, 2.5)*(radius/20.0), 0, frandom(1.5, 3.0), frandom(-180.0, 180.0), SXF_CLIENTSIDE | SXF_USEBLOODCOLOR, 64)
			TNT1 AAA 0 A_SpawnItemEx("SH_BloodDroplet", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-5.0, 5.0)+(velx/2.0), frandom(-5.0, 5.0)+(vely/2.0), frandom(2.0, 8.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 128)
			TNT1 AAA 0 A_SpawnItemEx("SH_BouncyGib1", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 128)
			TNT1 AAA 0 A_SpawnItemEx("SH_BouncyGib2", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 128)
			TNT1 AAA 0 A_SpawnItemEx("SH_BouncyGib5", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 128)
			TNT1 AAA 0 A_SpawnItemEx("SH_BouncyGib6", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 128)
			TNT1 AAA 0 A_SpawnItemEx("SH_BouncyGib7", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 128)
			TNT1 AAA 0 A_SpawnItemEx("SH_BouncyGib8", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 128)
			TNT1 AAA 0 A_SpawnItemEx("SH_BouncyGib9", frandom(-radius/2.0, radius/2.0), frandom(-radius/2.0, radius/2.0), frandom(1.0, height), frandom(-4.0, 4.0)+(velx/2.0), frandom(-4.0, 4.0)+(vely/2.0), frandom(2.0, 6.0)+(velz/2.0), angle, SXF_CLIENTSIDE | SXF_USEBLOODCOLOR | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 128)
			goto DoChecks

		DoChecks:
			TNT1 A 0 A_CheckFlag("BOSSDEATH", "DoBossDeath")
			goto End
			
		DoBossDeath:
			TNT1 A 0 A_BossDeath
			goto End

		End:
			TNT1 A 0 A_RailWait
			stop
	}
}

actor SH_BouncyGib1
{
	Decal BloodSplat
	Radius 4
	Height 4
	Mass 100
	Gravity 0.25
	BounceType "Hexen"
	BounceCount 5
	BounceFactor 0.8
	BounceSound "GibBounce"
	DeathSound "GibLand"
	Projectile
	-NOGRAVITY
	+NOTRIGGER
	+NOTELEPORT
	+NOBLOCKMONST
	+FORCEXYBILLBOARD
	//+CANBLAST
	+THRUACTORS
	+BOUNCEAUTOOFFFLOORONLY
	+SKYEXPLODE
	+NOTIMEFREEZE
	+USEBOUNCESTATE
	+NEVERADJUSTSPRITECLIPPING
	+DECALUSESTRANSLATION
	+DECALUSESSPRITESCALE
	
	var int user_driptimer;
	
	States
	{
		Spawn:
			GBG1 A 0 NoDelay A_Jump(128, "Animate")
			GBG1 A 0 A_SetScale(scalex*-1, scaley)
			GBG1 A 0 A_JumpTo("Animate")
			stop
		Animate:
			BGB1 ABCDEFG 2 A_SpawnItemEx("SH_BloodDropletTrail", frandom(-8.0, 8.0), frandom(-8.0, 8.0), frandom(0.0, 16.0), frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 64)
			loop
		Death:
			TNT1 A 0 A_ChangeFlag("MOVEWITHSECTOR", true)
			TNT1 A 0 A_ChangeFlag("CORPSE", true)
			TNT1 A 0 A_JumpTo("OnGround")
			stop
		Crash:
			TNT1 A 0 A_Scream
			TNT1 A 0 A_ChangeFlag("MOVEWITHSECTOR", true)
			TNT1 A 0 A_JumpTo("OnGround")
			stop
		OnGround:
			BGB1 H 2100
			stop
		OnCeiling:
			BGB1 H RANDOM(8, 15)
			goto StartDripping
			
		StartDripping:
			"####" "#" 0 A_SetUserVar(user_driptimer, random(16, 86))
		Dripping:
			"####" "#" 2 A_SpawnItemEx("SH_BloodDrip", frandom(-radius/2, radius/2), frandom(-radius/2, radius/2), 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION | SXF_TRANSFERSCALE, 150)
			"####" "#" 0 A_SetUserVar("user_driptimer", user_driptimer-1)
			"####" "#" 0 A_JumpIf(user_driptimer <= 0, "Fall")
			loop
		Fall:
			"####" "#" 0 A_ChangeFlag("LOCKEDTOCEILING", false)
			"####" "#" 0 A_ChangeFlag("NOGRAVITY", false)
			//"####" "#" 0 A_ChangeFlag("CANBLAST", true)
			"####" "#" 0 A_SetGravity(0.25)
			"####" "#" 0 A_SetScale(scalex, scaley*-1) // Revert scale again
			"####" "#" 0 A_JumpTo("Animate")
		
		Bounce.Ceiling:
			"####" "#" 0 A_JumpIf(CallACS("CHECK_SKY") != 0, "DontStick")
			"####" "#" 0 A_Jump(192, "DontStick")
			"####" "#" 0 A_Stop
			"####" "#" 0 A_SetScale(scalex, scaley*-1)
			"####" "#" 0 A_ChangeFlag("USEBOUNCESTATE", false)
			"####" "#" 0 A_ChangeFlag("BOUNCEONCEILINGS", false)
			"####" "#" 0 A_ChangeFlag("MISSILE", false)
			//"####" "#" 0 A_ChangeFlag("CANBLAST", false)
			"####" "#" 0 A_ChangeFlag("CORPSE", true)
			"####" "#" 0 A_ChangeFlag("LOCKEDTOCEILING", true)
			"####" "#" 0 A_JumpTo("OnCeiling")
			stop
		
		Bounce.Wall:
		Bounce.Floor:
		DontStick:
			"####" "#" 0 A_ChangeFlag("USEBOUNCESTATE", false) // Missed our chance to stick to the ceiling.
			"####" "#" 0 A_JumpTo("Animate")
			stop
	}
}

actor SH_BouncyGib2 : SH_BouncyGib1
{
	States
	{
		Animate:
			BGB2 ABCDEFG 2 A_SpawnItemEx("SH_BloodDropletTrail", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(0.0, 8.0), frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 64)
			loop
		OnGround:
			BGB2 H 2100
			stop
		OnCeiling:
			BGB2 H RANDOM(8, 15)
			goto StartDripping
	}
}

actor SH_BouncyGib3 : SH_BouncyGib1
{
	States
	{
		Animate:
			BGB3 ABCDEFG 2 A_SpawnItemEx("SH_BloodDropletTrail", frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(0.0, 12.0), frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 64)
			loop
		OnGround:
			BGB3 H 2100
			stop
		OnCeiling:
			BGB3 H RANDOM(8, 15)
			goto StartDripping
	}
}

actor SH_BouncyGib4 : SH_BouncyGib1
{
	-USEBOUNCESTATE
	States
	{
		Animate:
			BGB4 ABCDEFG 3 A_SpawnItemEx("SH_BloodDropletTrail", frandom(-9.0, 9.0), frandom(-9.0, 9.0), frandom(0.0, 18.0), frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 32)
			loop
		OnGround:
			BGB4 H 2100
			stop
	}
}

actor SH_BouncyGib5 : SH_BouncyGib1
{
	States
	{
		Animate:
			BGB5 ABCDEFG 2 A_SpawnItemEx("SH_BloodDropletTrail", frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(0.0, 20.0), frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 64)
			loop
		OnGround:
			BGB5 H 2100
			stop
		OnCeiling:
			BGB5 H RANDOM(4, 8)
			goto StartDripping
	}
}

actor SH_BouncyGib6 : SH_BouncyGib1
{
	States
	{
		Animate:
			BGB6 ABCDEFG 2 A_SpawnItemEx("SH_BloodDropletTrail", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(0.0, 8.0), frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 64)
			loop
		OnGround:
			BGB6 H 2100
			stop
		OnCeiling:
			BGB6 H RANDOM(8, 15)
			goto StartDripping
	}
}

actor SH_BouncyGib7 : SH_BouncyGib1
{
	States
	{
		Animate:
			BGB7 ABCDEFG 2 A_SpawnItemEx("SH_BloodDropletTrail", frandom(-8.0, 8.0), frandom(-8.0, 8.0), frandom(0.0, 16.0), frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 64)
			loop
		OnGround:
			BGB7 H 2100
			stop
		OnCeiling:
			BGB7 H RANDOM(4, 10)
			goto StartDripping
	}
}

actor SH_BouncyGib8 : SH_BouncyGib1
{
	-USEBOUNCESTATE
	States
	{
		Animate:
			BGB8 ABCDEFG 2 A_SpawnItemEx("SH_BloodDropletTrail", frandom(-8.0, 8.0), frandom(-8.0, 8.0), frandom(0.0, 16.0), frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 64)
			loop
		OnGround:
			BGB8 H 2100
			stop
	}
}

actor SH_BouncyGib9 : SH_BouncyGib1
{
	-USEBOUNCESTATE
	States
	{
		Animate:
			BGB9 ABCDEFG 3 A_SpawnItemEx("SH_BloodDropletTrail", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(0.0, 8.0), frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), 0, SXF_CLIENTSIDE | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION, 32)
			loop
		OnGround:
			BGB9 A 2100
			stop
	}
}

actor SH_GibSplash : SH_BaseParticle
{
	Scale 0.04
	Gravity 0.25
	-NOGRAVITY
	-NOINTERACTION
	-NOCLIP
	States
	{
		Spawn:
			12XS A 0 NoDelay A_ClientsideACSExecute("SET_SCALE_TO_OWNER_RADIUS")
			12XS ABCDEFGH 2
			stop
	}
}

actor SH_GibMist : SH_BaseParticle
{
	RenderStyle Translucent
	Alpha 0.42
	Scale 0.05
	States
	{
		Spawn:
			RMST A 0 NoDelay A_ClientsideACSExecute("SET_SCALE_TO_OWNER_RADIUS")
			RMST ABCDEFGH 4 A_FadeOut(0.05)
			stop
	}
}