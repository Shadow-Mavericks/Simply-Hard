// Based on the Tornado Demon by Maelstrom. Code cleaned up considerably by StrikerTheHedgefox.
ACTOR ArchSpeen 32725
{
	OBITUARY "%o was tossed around by an Archspeen."
	Health 700
	Radius 20
	Height 56
	Mass 500
	Speed 10
	RenderStyle Translucent
	Alpha 0.5
	PainChance 10
	SEESOUND "tornado/see"
	PAINSOUND "tornado/pain"
	DEATHSOUND "tornado/death"
	ACTIVESOUND "tornado/active"
	Bloodcolor "85 85 85"
	
	var int user_speen_count; // 45
	
	MONSTER
	+FloorClip
	+NoTarget
	+DontBlast
	+NoDamageThrust
	states
	{
		Spawn:
			SILE AB 10 A_Look
			Loop
		See:
			SILE AABBCC 2 A_Chase
			SILE D 0 A_Jump(32, "Speen")
			SILE DDEEFF 2 A_VileChase
			SILE F 0 A_Jump(32, "Speen", "SpeenSpeak")
			Loop
		SpeenSpeak:
			SPIN A 0 A_PlaySound("SPEEN", CHAN_VOICE)
			goto Speen
		Speen:
			SPIN A 0 A_SetInvulnerable
			SPIN A 0 A_UnsetShootable
			SPIN ABCDEFGHABCDEFGHABCDEFGHABCDEFGH 1
			WIND ABCD 1
			FX07 ABC 2
			FX07 A 0 A_SetUserVar("user_speen_count", 0)
			goto SpeenLoop
		SpeenLoop:
			FX07 A 0 A_PlaySound("tornado/form")
			FX07 AAAA 0 A_CustomMissile("Leaves3",Random(16,64),Random(-32,32),Random(0,360),0,0)
			FX07 A 0 A_SetUserVar("user_speen_count", user_speen_count+1)
			FX07 A 0 A_JumpIf(user_speen_count >= 45, "SpeenEnd")
			
			FX07 A 1 A_Wander
			FX07 A 0 A_RadiusGive("ArchspeenWind", 256, RGF_PLAYERS | RGF_MONSTERS | RGF_MISSILES | RGF_CORPSES | RGF_OBJECTS, 1)
			FX07 A 0 A_RadiusThrust(256,256,RTF_NOTMISSILE)
			FX07 A 1 A_Wander
			FX07 A 0 A_RadiusGive("ArchspeenWind", 256, RGF_PLAYERS | RGF_MONSTERS | RGF_MISSILES | RGF_CORPSES | RGF_OBJECTS, 1)
			FX07 A 0 A_RadiusThrust(256,256,RTF_NOTMISSILE)
			
			FX07 B 1 A_Wander
			FX07 B 0 A_RadiusGive("ArchspeenWind", 256, RGF_PLAYERS | RGF_MONSTERS | RGF_MISSILES | RGF_CORPSES | RGF_OBJECTS, 1)
			FX07 B 0 A_RadiusThrust(256,256,RTF_NOTMISSILE)
			FX07 B 1 A_Wander
			FX07 B 0 A_RadiusGive("ArchspeenWind", 256, RGF_PLAYERS | RGF_MONSTERS | RGF_MISSILES | RGF_CORPSES | RGF_OBJECTS, 1)
			FX07 B 0 A_RadiusThrust(256,256,RTF_NOTMISSILE)
			
			FX07 C 1 A_Wander
			FX07 C 0 A_RadiusGive("ArchspeenWind", 256, RGF_PLAYERS | RGF_MONSTERS | RGF_MISSILES | RGF_CORPSES | RGF_OBJECTS, 1)
			FX07 C 0 A_RadiusThrust(256,256,RTF_NOTMISSILE)
			FX07 C 1 A_Wander
			FX07 C 0 A_RadiusGive("ArchspeenWind", 256, RGF_PLAYERS | RGF_MONSTERS | RGF_MISSILES | RGF_CORPSES | RGF_OBJECTS, 1)
			FX07 C 0 A_RadiusThrust(256,256,RTF_NOTMISSILE)
			loop
		SpeenEnd:
			WIND DCBA 1
			SPIN A 0 A_SetShootable
			SPIN A 0 A_UnsetInvulnerable
			SPIN ABCDEFGHABCDEFGHABCDEFGHABCDEFGH 1
			Goto See
		Missile:
			SILE G 0 A_Jump(64, "Teleport")
			SILE G 0 BRIGHT A_PlaySound("tornado/form")
			SILE G 8 BRIGHT A_FaceTarget
			SILE H 6 BRIGHT
			SILE IJKLMN 6 BRIGHT A_FaceTarget  
			SILE N 0 A_PlaySound("wind/away")
			SILE NNNNNNNNNN 0 A_CustomMissile("Leaves1",Random(16,64),Random(-64,64),Random(45,-45),0,0) 
			SILE O 6 BRIGHT A_RadiusThrust(4096, 255, 0) 
			SILE P 10 BRIGHT
			Goto See
		Teleport: 
			SILE A 1 A_PlaySound("wind/away")
			SILE AAAAAAAA 0 A_SpawnItemEx("Leaves2",Random(-32,32),0,Random(16,64),3,0,0,Random(0,360),128)
			SILE A 0 A_UnSetShootable
			WIND A 1
			WIND B 1 A_SetTranslucent(0.32)
			WIND C 1 A_SetTranslucent(0.25)
			WIND D 1 A_SetTranslucent(0.15)
			WIND E 1 A_SetTranslucent(0.10)
			NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
			NULL A 0 A_Jump(128,25)
			NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(0,0,1,1)
			WIND E 1 A_SetTranslucent(0.10)
			WIND D 1 A_SetTranslucent(0.15)
			WIND C 1 A_SetTranslucent(0.25)
			WIND B 1 A_SetTranslucent(0.32)
			WIND A 1 A_SetTranslucent(0.50)
			SILE A 1
			SILE A 0 A_SetShootable
			SILE A 1 A_PlaySound("wind/away")
			Goto See
		Heal:
			WIND A 0 A_SetUserVar("user_speen_count", 0)
			WIND ABCD 1 Bright
			WIND A 0 A_Jump(32, "SpeenLoop")
			FX07 ABCABC 2
			WIND DCBA 1 Bright
			Goto See
		Pain:
			SILE Q 5
			SILE Q 5 A_Pain
			Goto See
		Death:
			SILE Q 7
			SILE R 7 A_Scream
			SILE S 7 A_NoBlocking
			SILE TUVW 7
			SILE XY 5
			SILE Z -1
			Stop
	}
}

Actor SpinnyDoomsauce : ArchSpeen
{
	Tag "Spinny Doomsauce, the friendly Archspeen!"
	+FRIENDLY
}

ACTOR ArchspeenWind : CustomInventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 0
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Use:
			TNT1 A 0 A_CheckFlag("DONTBLAST", "Nope")
			TNT1 A 0 A_CheckFlag("NODAMAGETHRUST", "Nope")
			TNT1 A 0 A_ChangeVelocity(velx, vely, velz+1.0, CVF_REPLACE)
			TNT1 A 0 A_CheckFlag("ISMONSTER", "Speen")
			TNT1 A 0 A_RailWait
			stop
		Speen:
			TNT1 A 0 A_SetAngle(angle+10.0, SPF_INTERPOLATE)
			stop
		Nope:
			TNT1 A 0 A_RailWait
			stop
	}
}

ACTOR Leaves1
{
	Radius 8
	Height 16
	Speed 15
	Damage 5
	PROJECTILE
	+RANDOMIZE
	+NOGRAVITY
	+FLOATBOB
	+FLOAT
	states
	{
		Spawn:
			LEF1 ABCDEFGHI 4 BRIGHT
			LEF1 ABCDEFGHI 4 BRIGHT
			LEF1 ABCDEFGHI 4 BRIGHT
			LEF1 ABCDEFGHI 4 BRIGHT
			LEF1 ABCDEFGHI 4 BRIGHT
			Goto death
		Death:
			LEF1 A 1 BRIGHT
			Stop
	}
}

ACTOR Leaves2
{
	Radius 6
	Height 16
	Speed 3
	Damage 0
	PROJECTILE
	+CLIENTSIDEONLY
	+RANDOMIZE
	+FLOATBOB
	+FLOAT
	-NOGRAVITY
	states
	{
		Spawn:
			LEF2 ABCDEFGHI 4 BRIGHT
			LEF2 ABCDEFGHI 4 BRIGHT
			LEF2 ABCDEFGHI 4 BRIGHT
			LEF2 ABCDEFGHI 4 BRIGHT
			LEF2 ABCDEFGHI 4 BRIGHT
			Goto death
		Death:
			LEF2 A 1 BRIGHT
			Stop
	}
}

ACTOR Leaves3
{
	Radius 8
	Height 16
	Speed 15
	Damage (2)
	PROJECTILE
	+RANDOMIZE
	+NOGRAVITY
	+FLOATBOB
	+FLOAT
	states
	{
		Spawn:
			LEF1 ABCDEFGHI 1 BRIGHT
			LEF1 ABCDEFGHI 1 BRIGHT
			LEF1 ABCDEFGHI 1 BRIGHT
			LEF1 ABCDEFGHI 1 BRIGHT
			LEF1 ABCDEFGHI 1 BRIGHT
			Goto death
		Death:
			LEF1 A 1 BRIGHT
			Stop
	}
}

