// Damage Falloff Calc = fix16(basedamage) - (fix16(falloff) * tics/rate) >> 16

const int DARKFORCES_DMG_MULT = 2;
const int DARKFORCES_RAD_MULT = 4;
const int DARKFORCES_TIC_SHIFT = 2;

actor BlastBolt : FastProjectile
{
	RenderStyle Add
	Alpha 1.0
	Radius 2
	Height 2
	Speed 62
	Damage (10 * DARKFORCES_DMG_MULT) // Multiply all DF damages by 2 to scale for Doom Enemies.
	DeathSound "Max/LZHIT"
	Scale 0.75
	
	PROJECTILE
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+FORCEPAIN
	
	var int user_falloff;
	var int user_mindmg;
	var int user_fallofftics;
	var int user_falloffrate;

	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 65536 * DARKFORCES_DMG_MULT)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 14 >> DARKFORCES_TIC_SHIFT)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 1 * DARKFORCES_DMG_MULT)
			goto SpawnLoop
		SpawnLoop:
			BLAS A 1 BRIGHT A_SetUserVar(user_fallofftics, user_fallofftics+1)
			loop
		Death:
			SPAR A 0 A_SetTranslucent(1.0, 0)
			SPAR ABCDEF 2 BRIGHT
			Stop
	}
}

actor DL44Bolt : BlastBolt
{
	Damage (12 * DARKFORCES_DMG_MULT)
	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 65536 * DARKFORCES_DMG_MULT)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 14 >> DARKFORCES_TIC_SHIFT)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 3 * DARKFORCES_DMG_MULT)
			goto SpawnLoop
	}
}

actor RifBlast : BlastBolt
{
	Damage (10 * DARKFORCES_DMG_MULT)
	-FORCEPAIN
	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 52428 * DARKFORCES_DMG_MULT)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 14 >> DARKFORCES_TIC_SHIFT)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 1 * DARKFORCES_DMG_MULT)
			goto SpawnLoop
	}
}

actor GoldShot : RifBlast
{
	Damage (12 * DARKFORCES_DMG_MULT)
	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 52428 * DARKFORCES_DMG_MULT)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 14 >> DARKFORCES_TIC_SHIFT)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 3 * DARKFORCES_DMG_MULT)
			goto SpawnLoop
	}
}

actor GoldShotZoom : RifBlast
{
	Damage (20 * DARKFORCES_DMG_MULT)
	Speed 300
	+FORCEPAIN
	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 0)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 0)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 0)
			goto SpawnLoop
	}
}

actor CutBlast : BlastBolt
{
	Radius 2
	Height 2
	Speed 25 // 100 >> 2
	Damage (15 * DARKFORCES_DMG_MULT)
	Obituary "%o was chopped up by %k"
	DeathSound "Max/LZHIT"
	scale 0.8
	
	PROJECTILE
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	-FORCEPAIN
	
	States
	{
		Spawn:
			CULT A 0 BRIGHT // Buffer
			CULT A 0 BRIGHT A_SetUserVar(user_falloff, 39321 * DARKFORCES_DMG_MULT)
			CULT A 0 BRIGHT A_SetUserVar(user_falloffrate, 29 >> DARKFORCES_TIC_SHIFT)
			CULT A 0 BRIGHT A_SetUserVar(user_mindmg, 1 * DARKFORCES_DMG_MULT)
			goto SpawnLoop
		SpawnLoop:
			CULT AAAABBBBCCCCDDDD 1 BRIGHT A_SetUserVar(user_fallofftics, user_fallofftics+1)
			loop
		Death:
			CUEX A  0 A_SetTranslucent(1.0, 0)
			CUEX ABCDEFGH 4 BRIGHT
			Stop
	}
}

actor GoobBlast : CutBlast
{
	Speed 30
	Damage (20 * DARKFORCES_DMG_MULT)
	States
	{
		Spawn:
			GOOB A 0 BRIGHT // Buffer
			GOOB A 0 BRIGHT A_SetUserVar(user_falloff, 39321 * DARKFORCES_DMG_MULT)
			GOOB A 0 BRIGHT A_SetUserVar(user_falloffrate, 29 >> DARKFORCES_TIC_SHIFT)
			GOOB A 0 BRIGHT A_SetUserVar(user_mindmg, 1 * DARKFORCES_DMG_MULT)
			goto SpawnLoop
		SpawnLoop:
			GOOB AAAABBBBCCCCDDDD 1 BRIGHT A_SetUserVar(user_fallofftics, user_fallofftics+1)
			loop
		Death:
			GOSP A  0 A_SetTranslucent(1.0, 0)
			GOSP ABCDEFGH 4 BRIGHT
			Stop
	}
}

actor Mortarnade
{
	DeathSound "Max/MedExplo"
	Obituary "%k's Mortar blew %o away "
	Radius 8
	Height 8
	Speed 27
	Damage (0)
	Gravity 0.7
	Scale 0.7
	
	PROJECTILE
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+FORCERADIUSDMG
	-NOGRAVITY
	
	States
	{
		Spawn:
			MORS A 0 NoDelay A_ChangeVelocity(0.0, 0.0, 7.5)
			goto SpawnLoop
		SpawnLoop:
			MORS ABCDEF 4 BRIGHT
			Loop
		Death:
			TNT1 A 0 A_SetScale(2.0, 2.0)
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", TRUE)
			MOEX A 3 bright A_Explode(50 * DARKFORCES_DMG_MULT, 40 * DARKFORCES_RAD_MULT, XF_HURTSOURCE)
			MOEX BCDEFGHIJKLMN 2 bright
			stop
	}
}

actor ConcussionBlast : FastProjectile
{
	DeathSound "Max/LrgExplo"
	obituary "%o was unlucky to meet %k's Concussion Rifle"
	Radius 6
	Height 6
	Damage (0)
	speed 80
	
	RenderStyle Add
	Alpha 1.0
	
	PROJECTILE
	+NOBLOCKMAP
	+ACTIVATEPCROSS
	+ACTIVATEIMPACT
	+NOGRAVITY
	+FORCERADIUSDMG
	+NOTIMEFREEZE
	
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Death:
			CBLA A 3 bright A_Explode(30 * DARKFORCES_DMG_MULT, 25 * DARKFORCES_RAD_MULT, XF_HURTSOURCE)
			CBLA B 3 bright A_BFGSpray ("ConExtra", 8, 0)
			CBLA CDEFGH 3 bright
			stop
	}
}

actor ConExtra
{
	DamageType "Marines"
	DeathSound "exp/larg"
	RenderStyle Add
	Alpha 1.0
	
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTIMEFREEZE
	+FORCERADIUSDMG
	
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Explode(30 * DARKFORCES_DMG_MULT, 25 * DARKFORCES_RAD_MULT, XF_HURTSOURCE)
			CBLA ABCDEFGH 3 bright
			stop
	}
}

actor AssaultCannonPlasma
{
	Radius 6
	Height 6
	Speed 25
	Damage (30 * DARKFORCES_DMG_MULT)
	obituary "%o became another victim to %k's Assault Cannon"
	DeathSound "Max/MedExplo"
	scale 0.8
	
	PROJECTILE
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+FORCEPAIN
	
	States
	{
		Spawn:
			ACSH AB 3 BRIGHT
			ACSH C 0 BRIGHT A_ChangeFlag("FORCEPAIN", false)
			ACSH CD 3 BRIGHT
			goto SpawnLoop
		SpawnLoop:
			ACSH ABCD 3 BRIGHT
			loop
		Death:
			ACBL A 0 A_SetTranslucent(1.0, 0)
			ACBL ABCDEF 3 BRIGHT
			Stop
	}
}

actor GoldAssaultCannonPlasma : AssaultCannonPlasma
{
	Damage (40 * DARKFORCES_DMG_MULT)
	Speed 30
	Scale 0.9
	States
	{
		Spawn:
			WPLS AB 3 BRIGHT
			WPLS B 0 BRIGHT A_ChangeFlag("FORCEPAIN", false)
			goto SpawnLoop
		SpawnLoop:
			WPLS AB 3 BRIGHT
			Loop
		Death:
			WEXP A 0 A_SetTranslucent(1.0, 0)
			WEXP ABCDEF 3 Bright
			stop
	}
}

actor AssaultCannonMissile
{
	Radius 6
	Height 6
	Speed 18
	Damage (0)
	obituary "%o became another victim to %k's Assault Cannon"
	SeeSound "Max/Trail"
	ActiveSound "Max/Trail"
	scale 0.8
	
	PROJECTILE
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+FORCERADIUSDMG
	+FORCEPAIN
	
	States
	{
		Spawn:
			ACMI AB 4 BRIGHT A_LoopActiveSound
			loop
		Death:
			ACMB A 0 A_StopSound(CHAN_VOICE)
			ACMB A 0 A_PlaySound("Max/MedExplo", CHAN_AUTO)
			ACMB A 0 A_SetScale(2.0, 2.0)
			ACMB A 0 A_Blast(BF_AFFECTBOSSES, 512, 20 * DARKFORCES_RAD_MULT, 10, "BlastEffect", "") // BaseEffect is temporary...
			ACMB A 0 A_Explode (70 * DARKFORCES_DMG_MULT, 40 * DARKFORCES_RAD_MULT, XF_HURTSOURCE)
			ACMB ABCDEFHIJKL 3 BRIGHT
			Stop
	}
}

actor GoldAssaultCannonMissile : AssaultCannonMissile
{
	Speed 36
	States
	{
		Spawn:
			WMSL AB 4 BRIGHT A_LoopActiveSound
			Loop
		Death:
			ACMB A 0 A_StopSound(CHAN_VOICE)
			ACMB A 0 A_PlaySound("Max/MedExplo", CHAN_AUTO)
			ACMB A 0 A_SetScale(2.0, 2.0)
			ACMB A 0 A_Blast(BF_AFFECTBOSSES, 512, 20 * DARKFORCES_RAD_MULT, 10, "BlastEffect", "") // BaseEffect is temporary...
			ACMB A 0 A_Explode (90 * DARKFORCES_DMG_MULT, 40 * DARKFORCES_RAD_MULT, XF_HURTSOURCE)
			ACMB ABCDEFHIJKL 3 BRIGHT
			Stop
	}
}

actor Cutshot : Inventory
{
	inventory.amount 1
	inventory.maxamount 5
}

actor Bullet : FastProjectile
{
	RenderStyle Add
	Alpha 1.0
	Radius 2
	Height 2
	Speed 67 // 270 >> 2
	Damage (10 * DARKFORCES_DMG_MULT)
	Decal "BulletChip"
	Obituary "%o got blasted by %k, like the rebel scum he is!"
	DeathSound "Max/LZHIT"
	Scale 0.8
	
	PROJECTILE
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	
	States
	{
	Spawn:
		BULL A 1 BRIGHT
		loop
	Death:
		BUEX A 0 A_SetTranslucent(1.0, 0)
		BUEX ABCDEFG 2 BRIGHT
		Stop
	}
}

actor GoldBullet : Bullet
{
	Damage (12 * DARKFORCES_DMG_MULT)
	Scale 0.8
	States
	{
	Spawn:
		GBUL A 1 BRIGHT
		loop
	Death:
		BUEX A 0 A_SetTranslucent(1.0, 0)
		BUEX ABCDEFG 2 BRIGHT
		Stop
	}
}

actor ScopeZoom : Inventory
{
	inventory.maxamount 1
}

actor MortarGun : Weapon
{
	weapon.selectionorder 7
	weapon.AmmoUse 1
	weapon.ammotype "RoketAmmo"
	weapon.ammogive 3
	Weapon.UpSound "Max/WepRaise"
	weapon.kickback 300
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 5
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	Inventory.Pickupmessage "Mortar Gun"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	+NOAUTOAIM
	States
	{
		Ready:
			MORT A 1 A_WeaponReady
			loop
		Deselect:
			MORT A 1 A_Lower
			NULL AA 0 A_Lower
			loop
		Select:
			MORT A 1 A_Raise
			NULL AA 0 A_Raise
			loop
		Fire:
			MORT B 0 A_PlaySound ("Max/mortarfire", CHAN_WEAPON)
			MORT B 0 A_FireCustomMissile("Mortarnade", 0, true, 5)
			MORT B 12 bright A_GunFlash
			MORT C 4 A_PlaySound("Max/mortarload", CHAN_AUTO)
			MORT D 4
			MORT A 4
			MORT A 1 A_ReFire
			Goto Ready
	//	AltFire:
	//		MORT B 0 A_PlaySound ("Max/mortarfire")
	//		TNT1 A 0 A_FireCustomMissile("Mortarbouncenade",1,1,0)
	//		MORT B 18 bright A_GunFlash
	//		MORT C 6 A_PlaySound ("Max/mortarload")
	//		MORT D 6
	//		MORT A 12
	//		MORT A 2 A_ReFire
	//		Goto Ready
		Flash:
			TNT1 A 14 BRIGHT A_Light1
			stop
		Spawn:
			MOGU A -1
			Stop
	}
}

actor ConcRifle : Weapon
{
	obituary "%o was unlucky to meet %k's Concussion Rifle"
	weapon.selectionorder 4
	weapon.ammouse 4
	Weapon.AmmoGive 20
	Weapon.AmmoType "Battery"
	Weapon.kickback 100
	Weapon.UpSound "Max/WepRaise"
	Inventory.PickupMessage "Concussion Rifle"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 6
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	states
	{
		Select:
			CONC A 1 A_Raise
			NULL AA 0 A_Raise
			loop
		Deselect:
			CONC A 1 A_Lower
			NULL AA 0 A_Lower
			loop
		Ready:
			CONC A 1 A_WeaponReady
			Loop
		Fire:
			CONC B 0 A_GunFlash
			CONC B 0 A_PlaySound ("Max/ConcRifle")
			CONC B 14 bright A_FireCustomMissile ("ConcussionBlast", 0, 1, 0, 0, 0)
			CONC C 4 bright
			CONC A 11
			CONC A 1 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 7 bright A_Light1
			TNT1 A 7 bright A_Light0
			stop
		Spawn:
			CRIF A -1
			Stop
	}
}

actor AssaultCannon : Weapon
{
	obituary "%o became another victim to %k's Assault Cannon"
	weapon.selectionorder 1
	weapon.ammouse 5
	weapon.ammouse2 1
	Weapon.AmmoGive 32
	Weapon.AmmoGive2 1
	Weapon.AmmoType "Battery"
	Weapon.AmmoType2 "BFGCHARGE"
	Weapon.kickback 100
	Weapon.UpSound "Max/WepRaise"
	Inventory.PickupMessage "Assault Cannon"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 8
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	states
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldReady")
			ASAU A 1 A_WeaponReady
			loop
		Select:
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldSelect")
			ASAU A 1 A_Raise
			NULL AA 0 A_Raise
			loop
		Deselect:
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldDeselect")
			ASAU A 1 A_Lower
			NULL AA 0 A_Lower
			loop
		Fire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldFire")
			ASAU B 0 A_GunFlash
			ASAU B 0 A_PlaySound ("Max/Assault")
			ASAU B 6 bright A_FireCustomMissile ("AssaultCannonPlasma", 0, 1, 10, 0, 0)
			ASAU B 1
			ASAU A 0 A_Refire
			Goto Ready
		AltFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldAltFire")
			ASAU C 9 A_GunFlash
			ASAU C 0 A_PlaySound ("Max/Missile")
			ASAU D 20 bright A_FireCustomMissile ("AssaultCannonMissile", 0, 1, 10, 0, 0)
			ASAU A 13
			ASAU A 1 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 8 bright A_Light1
			TNT1 A 8 bright A_Light0
			stop
		GoldReady:
			BLWH A 1 A_WeaponReady
			loop
		GoldSelect:
			BLWH A 1 A_Raise
			NULL AA 0 A_Raise
			loop
		GoldDeselect:
			BLWH A 1 A_Lower
			NULL AA 0 A_Lower
			loop
		GoldFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			BLWH B 0 A_GunFlash
			BLWH B 0 A_PlaySound ("Max/BWPlasma")
			BLWH B 2 Bright A_FireCustomMissile ("GoldAssaultCannonPlasma", 0, 1, 10, -0.5, 0)
			BLWH C 2 Bright
			BLWH D 2 Bright
			BLWH A 1
			BLWH A 1 A_Refire
			Goto Ready
		GoldAltFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			BLWH E 2 Bright
			BLWH F 2 Bright
			TNT1 C 0 A_PlaySound ("Max/BWMissile", 1)
			TNT1 C 0 A_PlaySound ("Max/BWRock", 2)
			BLWH G 2 Bright A_FireCustomMissile ("GoldAssaultCannonMissile", 0, 1, 10, 0, 0)
			BLWH H 2 Bright
			BLWH R 29
			BLWH I 2
			BLWH J 2
			BLWH A 1 A_ReFire
			Goto Ready
		Spawn:
			ACAN A -1
			Stop
	}
}

actor DisruptorRifle : DoomWeapon
{
	obituary "%o was disintegrated by %k"
	weapon.selectionorder 4
	weapon.ammouse 2
	weapon.ammogive 8
	weapon.ammotype "RailSlugs"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	Inventory.pickupmessage "Disruptor Rifle"
	Weapon.UpSound "Max/WepRaise"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 7
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	States
	{
		Select:
			NULL AA 0 A_Raise
			DRIF A 1 A_Raise
			Goto Select+1
		Deselect:
			NULL AA 0 A_Lower
			DRIF A 1 A_Lower
			Goto Deselect+1
		Ready:
			DRIF A 1 A_WeaponReady
			Loop
		Fire:
			DRIF A 1 A_RailAttack(300, 0, 1, "FF FF FF", "FF FF FF", RGF_FULLBRIGHT|RGF_SILENT, 0, "BulletPuff", 0, 0, 0, 35, 2.0, 4.0, "REDLightRailBlast")
			TNT1 A 0 A_PlaySound("Max/Disrupt", CHAN_WEAPON)
			DRIF B 1 bright A_GunFlash
			DRIF C 1
			TNT1 A 0 A_PlaySound("Max/DisrCh", 7)
			DRIF D 1
			DRIF E 1
			DRIF F 1
			DRIF E 5
			DRIF D 1
			DRIF A 93
			DRIF A 1 A_Refire
			Goto Ready
		Flash:
			TNT1 A 3 bright A_Light1
			TNT1 A 3 bright A_light0
			stop
		Spawn:
			DIRF A -1
			Stop
	}
}

actor FusionCutter : Weapon
{
	obituary "%k demonstrated the power of the fusion cutter tool to %o"
	Weapon.selectionorder 4
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 2
	Weapon.AmmoGive 8
	Weapon.AmmoGive2 8
	Weapon.AmmoType "ShellZ"
	Weapon.AmmoType2 "SuperShellZ"
	Weapon.UpSound "Max/WepRaise"
	Weapon.kickback 150
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 3
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	weapon.kickback 1536
	Inventory.PickupMessage "Fusion Cutter"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	states
	{
		Select:
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldSelect")
			FUSI A 1 A_Raise
			NULL AA 0 A_Raise
			loop
		Deselect:
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldDeselect")
			FUSI A 1 A_Lower
			NULL AA 0 A_Lower
			loop
		Ready:
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldReady")
			FUSI A 1 A_WeaponReady
			Loop
		Fire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldFire")
			FUSI A 0 A_JumpIfInventory("Cutshot", 1, "Fire2")
			FUSI A 0 A_GunFlash
			FUSI A 0 A_PlaySound ("Max/fusion")
			FUSI B 4 bright A_FireCustomMissile ("CutBlast", 0, 1, -12, 0, 0)
			FUSI A 4 A_GiveInventory ("Cutshot", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire2:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			FUSI A 0 A_JumpIfInventory("Cutshot", 2, "Fire3")
			FUSI C 0 A_GunFlash
			FUSI C 0 A_PlaySound ("Max/fusion")
			FUSI C 4 bright A_FireCustomMissile ("CutBlast", 0, 1, -5, -1, 0)
			FUSI A 4 A_GiveInventory ("Cutshot", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire3:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			FUSI A 0 A_JumpIfInventory("Cutshot", 3, "Fire4")
			FUSI D 0 A_GunFlash
			FUSI D 0 A_PlaySound ("Max/fusion")
			FUSI D 4 bright A_FireCustomMissile ("CutBlast", 0, 1, 4, -2, 0)
			FUSI A 4 A_GiveInventory ("Cutshot", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire4:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			FUSI A 0 A_JumpIfInventory("Cutshot", 4, "Fire5")
			FUSI E 0 A_GunFlash
			FUSI E 0 A_PlaySound ("Max/fusion")
			FUSI E 4 bright A_FireCustomMissile ("CutBlast", 0, 1, 12, -3, 0)
			FUSI A 4 A_GiveInventory ("Cutshot", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire5:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			FUSI A 0 A_JumpIfInventory("Cutshot", 5, "Fire6")
			FUSI D 0 A_GunFlash
			FUSI D 0 A_PlaySound ("Max/fusion")
			FUSI D 4 bright A_FireCustomMissile ("CutBlast", 0, 1, 4, -2, 0)
			FUSI A 4 A_GiveInventory ("Cutshot", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire6:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			FUSI C 0 A_GunFlash
			FUSI C 0 A_PlaySound ("Max/fusion")
			FUSI C 4 bright A_FireCustomMissile ("CutBlast", 0, 1, -5, -1, 0)
			FUSI A 4 A_TakeInventory ("Cutshot", 5)
			FUSI A 1 A_ReFire
			Goto Ready
		AltFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldAltFire")
			FUSI F 0 A_GunFlash
			FUSI F 0 A_PlaySound ("Max/fusion", 1)
			TNT1 A 0 A_PlaySound ("Max/fusion", 2)
			TNT1 A 0 A_PlaySound ("Max/fusion", 3)
			TNT1 A 0 A_PlaySound ("Max/fusion", 4)
			FUSI F 0 A_FireCustomMissile ("CutBlast", 4, true, -12, 0, 0)
			FUSI F 0 A_FireCustomMissile ("CutBlast", 1, false, -5, -1, 0)
			FUSI F 0 A_FireCustomMissile ("CutBlast", -1, false, 4, -2, 0)
			FUSI F 0 A_FireCustomMissile ("CutBlast", -4, false, 12, -3, 0)
			FUSI F 8 bright
			FUSI A 8
			FUSI A 1 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 3 bright A_Light1
			TNT1 A 4 bright A_Light0
			stop
		GoldSelect:
			FUS2 A 1 A_Raise
			NULL AA 0 A_Raise
			loop
		GoldDeselect:
			FUS2 A 1 A_Lower
			NULL AA 0 A_Lower
			loop
		GoldReady:
			FUS2 A 1 A_WeaponReady
			Loop
		GoldFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			FUS2 A 0 A_JumpIfInventory("Cutshot", 1, "GoldFire2")
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound ("Max/Goob")
			FUS2 B 4 bright A_FireCustomMissile ("GoobBlast", 0, 1, -12, 0, 0)
			FUS2 A 4 A_GiveInventory ("Cutshot", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire2:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory("Cutshot", 2, "GoldFire3")
			FUS2 C 0 A_GunFlash
			FUS2 C 0 A_PlaySound ("Max/Goob")
			FUS2 C 4 bright A_FireCustomMissile ("GoobBlast", 0, 1, -5, -1, 0)
			FUS2 A 4 A_GiveInventory ("Cutshot", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire3:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory("Cutshot", 3, "GoldFire4")
			FUS2 D 0 A_GunFlash
			FUS2 D 0 A_PlaySound ("Max/Goob")
			FUS2 D 4 bright A_FireCustomMissile ("GoobBlast", 0, 1, 4, -2, 0)
			FUS2 A 4 A_GiveInventory ("Cutshot", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire4:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory("Cutshot", 4, "GoldFire5")
			FUS2 E 0 A_GunFlash
			FUS2 E 0 A_PlaySound ("Max/Goob")
			FUS2 E 4 bright A_FireCustomMissile ("GoobBlast", 0, 1, 12, -3, 0)
			FUS2 A 4 A_GiveInventory ("Cutshot", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire5:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory("Cutshot", 5, "GoldFire6")
			FUS2 D 0 A_GunFlash
			FUS2 D 0 A_PlaySound ("Max/Goob", CHAN_WEAPON)
			FUS2 D 4 bright A_FireCustomMissile ("GoobBlast", 0, 1, 4, -2, 0)
			FUS2 A 4 A_GiveInventory ("Cutshot", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire6:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			FUS2 C 0 A_GunFlash
			FUS2 C 0 A_PlaySound ("Max/Goob")
			FUS2 C 4 bright A_FireCustomMissile ("GoobBlast", 0, 1, -5, -1, 0)
			FUS2 A 4 A_TakeInventory ("Cutshot", 5)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldAltFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			FUS2 F 0 A_GunFlash
			FUS2 F 0 A_PlaySound ("Max/Goob", 1)
			TNT1 A 0 A_PlaySound ("Max/Goob", 2)
			TNT1 A 0 A_PlaySound ("Max/Goob", 3)
			TNT1 A 0 A_PlaySound ("Max/Goob", 4)
			FUS2 F 0 A_FireCustomMissile ("GoobBlast", 4, true, -12, 0, 0)
			FUS2 F 0 A_FireCustomMissile ("GoobBlast", 1, false, -5, -1, 0)
			FUS2 F 0 A_FireCustomMissile ("GoobBlast", -1, false, 4, -2, 0)
			FUS2 F 0 A_FireCustomMissile ("GoobBlast", -4, false, 12, -3, 0)
			FUS2 F 8 bright
			FUS2 A 8
			FUS2 A 1 A_ReFire
			Goto Ready
		Spawn:
			FCUT A -1
			Stop
	}
}

actor Autogun : Weapon
{
	obituary "%o got in the way of %k's repeater rifle"
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 5
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 15
	Weapon.AmmoGive2 3
	Weapon.AmmoType "AK_Mag"
	Weapon.AmmoType2 "Shellz"
	Weapon.UpSound "Max/WepRaise"
	Weapon.kickback 100
	weapon.BobStyle "InverseSmooth"
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	Inventory.PickupMessage "Autogun"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	states
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldReady")
			AUTO A 1 A_WeaponReady
			Loop
		Select:
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldSelect")
			AUTO A 1 A_Raise
			NULL AA 0 A_Raise
			Loop
		Deselect:
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldDeselect")
			NULL A 0 A_StopSound(5)
			AUTO A 1 A_Lower
			NULL AA 0 A_Lower
			Loop
		Fire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldFire")
			AUTO B 0 A_GunFlash
			AUTO B 0 A_PlaySound ("Max/autogun", 5)
			AUTO B 3 A_FireCustomMissile ("Bullet",0.25,1,9,0,0)
			AUTO C 1
			AUTO C 0 A_ReFire
			Goto Ready
		Hold:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldHold")
			NULL A 0 A_GunFlash
			NULL A 0 A_PlaySound ("Max/autoloop", 5, 1, 1)
			AUTO C 3
			AUTO B 3 Bright A_FireCustomMissile ("Bullet",0.25,1,9,0,0)
			AUTO C 1
			NULL C 0 A_ReFire
			NULL A 0 A_StopSound(5)
			NULL A 0 A_ClearReFire
			Goto Ready
		AltFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldAltFire")
			AUTO B 0 A_PlaySound ("Max/autogun", 5)
			AUTO B 0 A_FireCustomMissile ("Bullet",0.25,1,6,-9,0)
			AUTO B 0 A_FireCustomMissile ("Bullet",0.25,0,15,-9,0)
			AUTO B 9 BRIGHT A_FireCustomMissile ("Bullet",0.25,0,9,0,0)
			AUTO C 6
			AUTO C 0 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 2 bright A_Light1
			TNT1 A 2 bright A_Light0
			stop
		GoldReady:
			GYRO A 1 A_WeaponReady
			Loop
		GoldSelect:
			GYRO A 1 A_Raise
			NULL AA 0 A_Raise
			Loop
		GoldDeselect:
			GYRO A 1 A_Lower
			NULL AA 0 A_Lower
			Loop
		GoldFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			GYRO B 0 A_GunFlash
			GYRO B 0 A_PlaySound ("Max/GAutogun", 5)
			GYRO B 3 bright A_FireCustomMissile ("GoldBullet",0.25,1,9,0,0)
			GYRO C 1
			GYRO C 0 A_ReFire
			Goto Ready
		GoldHold:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			NULL A 0 A_GunFlash
			NULL A 0 A_PlaySound ("Max/GAutoloop", 5, 1, 1)
			GYRO C 3
			GYRO B 3 Bright A_FireCustomMissile ("GoldBullet",0.25,1,9,0,0)
			GYRO C 1 Bright
			GYRO C 0 A_ReFire
			GYRO A 0 A_StopSound(5)
			GYRO A 0 A_ClearReFire
			Goto Ready
		GoldAltFire:
			TNT1 A 0 A_Jumpifnoammo("Ready")
			GYRO B 0 A_PlaySound ("Max/GAutogun", 5)
			GYRO B 0 A_FireCustomMissile ("GoldBullet",0.25,1,4,-9,0)
			GYRO B 0 A_FireCustomMissile ("GoldBullet",0.25,0,15,-9,0)
			GYRO B 9 BRIGHT A_FireCustomMissile ("GoldBullet",0.25,0,9,0,0)
			GYRO C 6
			GYRO C 0 A_ReFire
			Goto Ready
		Spawn:
			AGUN A -1
			Stop
	}
}

actor DFPistol : Weapon
{
	obituary "%o couldn't handle %k's blaster pistol"
	Weapon.SelectionOrder 9
	Weapon.AmmoUse 1
	Weapon.AmmoGive 15
	Weapon.AmmoType "peastolClip"
	Weapon.UpSound "Max/WepRaise"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	Decal ""
	Inventory.PickupMessage "Bryar Pistol"
	weapon.BobStyle "InverseSmooth"
	+AMMO_OPTIONAL
	Weapon.SlotNumber 2
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	states
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("Tier1upgradetoken", 1, "GoldReady")
			BRYP A 1 A_WeaponReady
			Loop
		Deselect:
			TNT1 A 0 A_JumpIfInventory("Tier1upgradetoken", 1, "GoldDeselect")
			NULL AA 0 A_Lower
			BRYP A 1 A_Lower
			Goto Deselect+2
		Select:
			TNT1 A 0 A_JumpIfInventory("Tier1upgradetoken", 1, "GoldSelect")
			NULL AA 0 A_Raise
			BRYP A 1 A_Raise
			Goto Select+2
		Fire:
			TNT1 A 0 A_JumpIfInventory("Tier1upgradetoken", 1, "GoldFire")
			BRYP A 0 A_GunFlash
			BRYP A 0 A_FireCustomMissile("BlastBolt", 0.25, 0, 5, 0, 0, 0)
			BRYP A 0 A_PlaySound ("Max/Bryar")
			BRYP B 4 bright
			BRYP C 4 bright
			BRYP A 9
			BRYP A 0 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 8 bright A_Light1
			TNT1 A 8 bright A_Light0
			Goto LightDone
		GoldSelect:
			TNT1 A 0 A_JumpIfInventory("peastolClip", 1, 1)
			Goto Select+2
			NULL AA 0 A_Raise
			DL44 A 1 A_Raise
			Loop
		GoldDeselect:
			TNT1 A 0 A_JumpIfInventory("peastolClip", 1, 1)
			Goto Deselect+2
			NULL AA 0 A_Lower
			DL44 A 1 A_Lower
			Loop
		GoldReady:
			TNT1 A 0 A_JumpIfInventory("peastolClip", 1, 1)
			Goto Ready+1
			DL44 A 1 A_WeaponReady
			Wait
		GoldFire:
			TNT1 A 0 A_JumpIfInventory("peastolClip", 1, 1)
			Goto Fire+1
			DL44 A 0 A_GunFlash
			TNT1 A 0 A_FireCustomMissile("DL44Bolt", 0.25, 1, 5, 0, 0, 0)
			TNT1 A 0 A_PlaySound ("Max/DL44")
			DL44 B 4 bright
			DL44 C 3 bright
			DL44 A 2
			DL44 A 0 A_ReFire
			Goto Ready
		Spawn:
			BPIS A -1
			Stop
	}
}

actor DFBlaster : Weapon
{
	obituary "%o was blasted away by %k's E-11 rifle"
	Weapon.SelectionOrder 8
	Weapon.AmmoUse 1
	Weapon.AmmoGive 15
	Weapon.AmmoType "peastolClip"
	Weapon.UpSound "Max/WepRaise"
	Inventory.PickupMessage "Blaster Rifle"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 2
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	States
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("Tier2upgradetoken", 1, "GoldReady")
			BLRF A 1 A_WeaponReady
			Loop
		Deselect:
			TNT1 A 0 A_JumpIfInventory("Tier2upgradetoken", 1, "GoldDeselect")
			BLRF A 1 A_Lower
			NULL AA 0 A_Lower
			Loop
		Select:
			TNT1 A 0 A_JumpIfInventory("Tier2upgradetoken", 1, "GoldSelect")
			BLRF A 1 A_Raise
			NULL AA 0 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("Tier2upgradetoken", 1, "GoldFire")
			BLRF A 0 A_GunFlash
			BLRF A 0 A_PlaySound ("Max/E-11")
			BLRF B 2 bright A_FireCustomMissile("RifBlast", frandom(-2.0,2.0),1,4,0,0,frandom(-2.0,2.0))
			BLRF A 3
			BLRF A 2 A_ReFire
			Goto Ready
		Altfire:
			TNT1 A 0 A_JumpIfInventory("Tier2upgradetoken", 1, "GoldAltFire")
			Goto Ready
		Flash:
			TNT1 A 3 Bright A_Light1
			TNT1 A 3 Bright A_Light0
			Goto LightDone
		GoldDeselect:
			NULL AA 0 A_Lower
			EE11 A 1 A_Lower
			Loop
		GoldSelect:
			NULL AA 0 A_Raise
			EE11 A 1 A_Raise
			Loop
		GoldReady:
			TNT1 A 0 A_JumpIfInventory("ScopeZoom", 1, "ZoomReady")
			EE11 A 1 A_WeaponReady
			Wait
		GoldFire:
			TNT1 A 0 A_JumpIfInventory("ScopeZoom", 1, "GoldFireZoom")
			EE11 A 0 A_GunFlash
			EE11 A 0 A_PlaySound ("Max/GE-11N")
			EE11 B 2 bright A_FireCustomMissile("GoldShot", frandom(-1.25,1.25),1,6,0,0,frandom(-1.25,1.25))
			EE11 A 2
			EE11 A 2 A_ReFire
			Goto Ready
		GoldAltfire:
			TNT1 A 0 A_JumpifInventory ("ScopeZoom", 1, "ZoomOut")
			EESC A 1 A_PlaySound("Max/Zoom", CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("ScopeZoom", 1)
			EESC A 8 A_ZoomFactor(3)
			Goto Ready
		ZoomReady:
			EESC A 1 A_WeaponReady (WRF_NOBOB|WRF_NOSWITCH|WRF_ALLOWZOOM)
			Loop
		GoldFireZoom:
			TNT1 A 0 A_JumpIfInventory ("peastolClip", 8, "GoldFireZoomDoFire")
			Goto ZoomOut
		GoldFireZoomDoFire:
			EESC A 3
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound ("Max/GE-11Z", CHAN_WEAPON)
			TNT1 A 0 A_FireCustomMissile("GoldShotZoom", 0, 1, 0, 4, 0)
			TNT1 A 0 A_TakeInventory("peastolClip", 6)
			EESC A 10
			TNT1 A 0 A_ReFire
			Goto Ready
		ZoomOut:
			TNT1 A 0 A_TakeInventory("ScopeZoom", 1)
			TNT1 A 0 A_PlaySound("Max/Zoom", CHAN_WEAPON)
			EESC A 8 A_ZoomFactor(1)
			EE11 A 1
			Goto Ready
		Spawn:
			BRIF A -1
			Stop
	}
}

ACTOR FistPuff
{
	RenderStyle add
	Alpha 0.5
	Speed 0
	Mass 10
	Attacksound "Max/fisthit"
	Seesound "Max/fisthit"
	Mass 5

	+PUFFONACTORS
	+NOEXTREMEDEATH
	+NOBLOCKMAP
	+NOGRAVITY
	+RANDOMIZE
	-ALLOWPARTICLES

	States
	{
		Spawn:
			TNT1 AAAA 4
			Stop
	}
}

actor DFFist : Weapon
{
	weapon.selectionorder 110
	weapon.Kickback 200
	
	Inventory.Forbiddento "Maximusprime"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	Obituary "o% got knocked the fuck out by %k"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 1
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	States
	{
		Ready:
			FIST A 1 A_WeaponReady
			loop
		Deselect:
			FIST A 1 A_Lower
			NULL AA 0 A_Lower
			loop
		Select:
			FIST A 1 A_Raise
			NULL AA 0 A_Raise
			loop
		Fire:
			FIST B 6
			TNT1 B 0 A_PlaySound ("Max/fist")
			TNT1 B 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
			FIST C 0 A_FireBullets(-15.0, 0.0, 1, 6 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 0 A_FireBullets(0.0, 0.0, 1, 6 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 0 A_FireBullets(15.0, 0.0, 1, 6 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 6
			Goto FireEnd
		Berserked:
			FIST C 0 A_FireBullets(-15.0, 0.0, 1, 18 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 0 A_FireBullets(0.0, 0.0, 1, 18 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 0 A_FireBullets(15.0, 0.0, 1, 18 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 6
		FireEnd:
			FIST D 4
			FIST A 5
			FIST A 0 A_ReFire
			goto Ready
	}
}