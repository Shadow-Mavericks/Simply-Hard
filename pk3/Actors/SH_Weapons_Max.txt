// Damage Falloff Calc = fix16(basedamage) - (fix16(falloff) * tics/rate) >> 16

const int DARKFORCES_DMG_MULT = 2;
const int DARKFORCES_RAD_MULT = 6;
const int DARKFORCES_TIC_SHIFT = 2;

actor BlastBolt : SH_BasePlayerFastProjectile
{
	RenderStyle Add
	Alpha 1.0
	Radius 2
	Height 2
	Speed 62
	Damage (10 * DARKFORCES_DMG_MULT) // Multiply all DF damages by 2 to scale for Doom Enemies.
	DeathSound "Max/LZHIT"
	Scale 0.75
	
	DamageType "MaxBlasterBolt"
	
	PROJECTILE
	+FORCEPAIN
	-ACTIVATEIMPACT // Hack below covers this.
	
	var int user_falloff;
	var int user_mindmg;
	var int user_fallofftics;
	var int user_falloffrate;

	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 65536 * DARKFORCES_DMG_MULT)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 14 >> DARKFORCES_TIC_SHIFT)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 1 * DARKFORCES_DMG_MULT)
			goto SpawnStart
		SpawnStart:
			TNT1 A 1 BRIGHT A_SetUserVar(user_fallofftics, user_fallofftics+1)
			goto SpawnLoop
		SpawnLoop:
			BLAS A 1 BRIGHT Light("RedBolt") A_SetUserVar(user_fallofftics, user_fallofftics+1)
			loop
		Death:
			SPAR A 0 A_CustomBulletAttack(0, 0, 1, 0, "SigilHitscanHax", 32, CBAF_AIMFACING | CBAF_EXPLICITANGLE | CBAF_NORANDOM)
			SPAR A 0 A_SetTranslucent(1.0, 0)
			SPAR ABCDEF 2 BRIGHT
			Stop
	}
}

actor DL44Bolt : BlastBolt
{
	Damage (12 * DARKFORCES_DMG_MULT)
	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 65536 * DARKFORCES_DMG_MULT)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 14 >> DARKFORCES_TIC_SHIFT)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 3 * DARKFORCES_DMG_MULT)
			goto SpawnStart
	}
}

actor RifBlast : BlastBolt
{
	Damage (10 * DARKFORCES_DMG_MULT)
	-FORCEPAIN
	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 52428 * DARKFORCES_DMG_MULT)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 14 >> DARKFORCES_TIC_SHIFT)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 1 * DARKFORCES_DMG_MULT)
			goto SpawnStart
	}
}

actor GoldShot : RifBlast
{
	Damage (12 * DARKFORCES_DMG_MULT)
	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 52428 * DARKFORCES_DMG_MULT)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 14 >> DARKFORCES_TIC_SHIFT)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 3 * DARKFORCES_DMG_MULT)
			goto SpawnStart
	}
}

actor GoldShotZoom : RifBlast
{
	Damage (36 * DARKFORCES_DMG_MULT)
	Speed 300
	+FORCEPAIN
	States
	{
		Spawn:
			BLAS A 0 BRIGHT // Buffer
			BLAS A 0 BRIGHT A_SetUserVar(user_falloff, 0)
			BLAS A 0 BRIGHT A_SetUserVar(user_falloffrate, 0)
			BLAS A 0 BRIGHT A_SetUserVar(user_mindmg, 0)
			goto SpawnStart
	}
}

actor CutBlast : BlastBolt
{
	Radius 2
	Height 2
	Speed 25 // 100 >> 2
	Damage (15 * DARKFORCES_DMG_MULT)
	Obituary "%o was chopped up by %k"
	DeathSound "Max/LZHIT"
	scale 0.8
	
	RenderStyle Add
	Alpha 1.0
	
	+FORCEXYBILLBOARD
	-FORCEPAIN
	
	States
	{
		Spawn:
			CULT A 0 BRIGHT // Buffer
			CULT A 0 BRIGHT A_SetUserVar(user_falloff, 39321 * DARKFORCES_DMG_MULT)
			CULT A 0 BRIGHT A_SetUserVar(user_falloffrate, 29 >> DARKFORCES_TIC_SHIFT)
			CULT A 0 BRIGHT A_SetUserVar(user_mindmg, 1 * DARKFORCES_DMG_MULT)
			goto SpawnLoop
		SpawnLoop:
			CULT AAAABBBBCCCCDDDD 1 BRIGHT Light("GreenBolt") A_SetUserVar(user_fallofftics, user_fallofftics+1)
			loop
		Death:
			CUEX A 0 A_CustomBulletAttack(0, 0, 1, 0, "SigilHitscanHax", 32, CBAF_AIMFACING | CBAF_EXPLICITANGLE | CBAF_NORANDOM)
			CUEX A 0 A_SetTranslucent(1.0, 0)
			CUEX ABCDEFGH 4 BRIGHT
			Stop
	}
}

actor GoobBlast : CutBlast
{
	Speed 30
	Damage (20 * DARKFORCES_DMG_MULT)
	States
	{
		Spawn:
			GOOB A 0 BRIGHT // Buffer
			GOOB A 0 BRIGHT A_SetUserVar(user_falloff, 39321 * DARKFORCES_DMG_MULT)
			GOOB A 0 BRIGHT A_SetUserVar(user_falloffrate, 29 >> DARKFORCES_TIC_SHIFT)
			GOOB A 0 BRIGHT A_SetUserVar(user_mindmg, 1 * DARKFORCES_DMG_MULT)
			goto SpawnLoop
		SpawnLoop:
			GOOB AAAABBBBCCCCDDDD 1 BRIGHT Light("BlueBolt") A_SetUserVar(user_fallofftics, user_fallofftics+1)
			loop
		Death:
			GOSP A 0 A_CustomBulletAttack(0, 0, 1, 0, "SigilHitscanHax", 32, CBAF_AIMFACING | CBAF_EXPLICITANGLE | CBAF_NORANDOM)
			GOSP A  0 A_SetTranslucent(1.0, 0)
			GOSP ABCDEFGH 4 BRIGHT
			Stop
	}
}

actor Mortarnade : SH_BasePlayerProjectile
{
	DeathSound "Max/MedExplo"
	Obituary "%k's Mortar blew %o away "
	Radius 8
	Height 8
	Speed 27
	Damage (20) // In DF there's no direct damage, but making an exception.
	Gravity 0.7
	
	DamageType "MaxExplosion"

	Scale 0.8

	PROJECTILE
	+NEVERADJUSTSPRITECLIPPING
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+FORCERADIUSDMG
	-NOGRAVITY
	-NOTELEPORT
	
	States
	{
		Spawn:
			MORS A 0 NoDelay A_ChangeVelocity(0.0, 0.0, 7.5)
			goto SpawnLoop
		SpawnLoop:
			MORS ABCDEF 4 BRIGHT
			Loop
		Death:
			TNT1 A 0 A_SetScale(2.0, 2.0)
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", TRUE)
			TNT1 A 0 Bright A_Quake (1, 6, 0, 80 * DARKFORCES_RAD_MULT, "") 
			TNT1 A 0 Bright A_Quake (2, 6, 0, 60 * DARKFORCES_RAD_MULT, "") 
			TNT1 A 0 Bright A_Quake (4, 6, 0, 40 * DARKFORCES_RAD_MULT, "") 
			MOEX A 3 bright A_Explode(50 * DARKFORCES_DMG_MULT, 40 * DARKFORCES_RAD_MULT, XF_HURTSOURCE)
			MOEX BCDEFGHIJKLMN 2 bright
			stop
	}
}

actor FlakParticle : SH_BaseParticle
{
	RenderStyle Add
	Alpha 0.25
	Scale 0.375
	States
	{
		Spawn:
			AATK A 1 Bright
			goto SpawnLoop
		SpawnLoop:
			AATK BCD 1 Bright A_Fadeout(0.015)
			loop
	}
}

actor FlakBall : MortarNade
{
	Obituary "%o was torn apart by %k's flechette ball."
	Scale 0.5
	
	DeathSound "Max/FlakBallExplosion"
	BounceSound "Max/FlakBallBounce"
	BounceFactor 0.7
	WallBounceSound "Max/FlakBallBounce"
	WallBounceFactor 0.33

	+NEVERADJUSTSPRITECLIPPING
	+SKYEXPLODE
	+EXPLODEONWATER
	+BOUNCEONWALLS
	+BOUNCEONCEILINGS
	-BOUNCEONFLOORS
	-NOWALLBOUNCESND
	-NOBOUNCESOUND
	States
	{
		Spawn:
			FCGN A 0 NoDelay A_ChangeVelocity(0.0, 0.0, 7.5)
			goto SpawnLoop
		SpawnLoop:
			FCGN AB 1 BRIGHT A_SpawnItemEx("FlakParticle", 0, 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), 0, SXF_CLIENTSIDE, 0)
			Loop
		Death:
			TNT1 A 0 A_SetTranslucent(1.0, 1)
			TNT1 A 0 A_SetScale(2.0, 2.0)
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", TRUE)
			TNT1 A 0 Bright A_Quake (1, 6, 0, 75 * DARKFORCES_RAD_MULT, "") 
			TNT1 A 0 Bright A_Quake (2, 6, 0, 50 * DARKFORCES_RAD_MULT, "") 
			TNT1 A 0 Bright A_Quake (4, 6, 0, 25 * DARKFORCES_RAD_MULT, "") 
			PEXP A 3 bright A_Explode(64 * DARKFORCES_DMG_MULT, 25 * DARKFORCES_RAD_MULT, XF_HURTSOURCE)
			PEXP BCDEFGHI 2 bright
			stop
	}
}

actor ConcussionBlast : SH_BasePlayerFastProjectile
{
	SeeSound "Max/ConcProj"
	Obituary "%o was unlucky to meet %k's Concussion Rifle"
	Radius 2
	Height 2
	Damage (20) // In DF there's no direct damage, but making an exception.
	Speed 47
	
	RenderStyle Add
	Alpha 1.0
	
	DamageType "MaxExplosion"
	
	PROJECTILE
	+FORCERADIUSDMG
	+FORCEPAIN
	-ACTIVATEIMPACT
	
	States
	{
		Spawn:
			BULL A 1 Bright Light("BlueBolt")
			Loop
		Death:
			CBLA A 0 A_CustomBulletAttack(0, 0, 1, 0, "SigilHitscanHax", 32, CBAF_AIMFACING | CBAF_EXPLICITANGLE | CBAF_NORANDOM)
			CBLA A 0 Bright A_Quake (1, 8, 0, 100 * DARKFORCES_RAD_MULT, "") 
			CBLA A 0 Bright A_Quake (2, 8, 0, 75 * DARKFORCES_RAD_MULT, "") 
			CBLA A 0 Bright A_Quake (4, 8, 0, 50 * DARKFORCES_RAD_MULT, "") 
			CBLA A 0 Bright A_PlaySound("Max/LrgExplo", CHAN_AUTO)
			CBLA A 3 Bright A_Explode(30 * DARKFORCES_DMG_MULT, 25 * DARKFORCES_RAD_MULT, XF_HURTSOURCE, false, 25 * DARKFORCES_RAD_MULT)
			CBLA B 3 Bright
			CBLA CDEFGH 3 Bright
			stop
	}
}

actor ConcussionExtra
{
	RenderStyle Add
	Alpha 1.0

	+NEVERADJUSTSPRITECLIPPING
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTIMEFREEZE
	+FORCERADIUSDMG
	
	States
	{
		Spawn:
			CBLA A 0 Bright NoDelay A_Quake (1, 8, 0, 100 * DARKFORCES_RAD_MULT, "") 
			CBLA A 0 Bright A_Quake (2, 8, 0, 75 * DARKFORCES_RAD_MULT, "") 
			CBLA A 0 Bright A_Quake (4, 8, 0, 50 * DARKFORCES_RAD_MULT, "") 
			CBLA A 0 Bright A_PlaySound("Max/LrgExplo", CHAN_AUTO, 0.5)
			CBLA ABCDEFGH 3 bright
			stop
	}
}

actor AssaultCannonPlasma : SH_BasePlayerProjectile
{
	Radius 6
	Height 6
	Speed 25
	Damage (25 * DARKFORCES_DMG_MULT)
	obituary "%o became another victim to %k's Assault Cannon"
	DeathSound "Max/MedExplo"

	DamageType "MaxBlasterBolt"

	RenderStyle Add
	Alpha 1.0
	Scale 0.8

	PROJECTILE
	+FORCEPAIN
	+FORCERADIUSDMG
	-ACTIVATEIMPACT
	
	States
	{
		Spawn:
			ACSH AB 3 BRIGHT Light("BigBlueBolt")
			ACSH C 0 BRIGHT Light("BigBlueBolt") A_ChangeFlag("FORCEPAIN", false)
			ACSH CD 3 BRIGHT Light("BigBlueBolt")
			goto SpawnLoop
		SpawnLoop:
			ACSH ABCD 3 BRIGHT Light("BigBlueBolt")
			loop
		Death:
			ACBL A 0 A_CustomBulletAttack(0, 0, 1, 0, "SigilHitscanHax", 32, CBAF_AIMFACING | CBAF_EXPLICITANGLE | CBAF_NORANDOM)
			ACBL A 0 A_Explode(5 * DARKFORCES_DMG_MULT, 48, 0, false, 48)
			ACBL A 3 BRIGHT Light("BlueExplosion1")
			ACBL B 3 BRIGHT Light("BlueExplosion2")
			ACBL C 3 BRIGHT Light("BlueExplosion3")
			ACBL D 3 BRIGHT Light("BlueExplosion4")
			ACBL E 3 BRIGHT Light("BlueExplosion5")
			ACBL F 3 BRIGHT Light("BlueExplosion6")
			Stop
	}
}

actor GoldAssaultCannonPlasma : AssaultCannonPlasma
{
	Damage (30 * DARKFORCES_DMG_MULT)
	Speed 30
	Scale 0.9
	States
	{
		Spawn:
			WPLS ABCD 3 BRIGHT Light("BigYellowBolt")
			WPLS B 0 BRIGHT Light("BigYellowBolt") A_ChangeFlag("FORCEPAIN", false)
			goto SpawnLoop
		SpawnLoop:
			WPLS ABCD 3 BRIGHT Light("BigYellowBolt")
			Loop
		Death:
			WEXP A 0 A_CustomBulletAttack(0, 0, 1, 0, "SigilHitscanHax", 32, CBAF_AIMFACING | CBAF_EXPLICITANGLE | CBAF_NORANDOM)
			WEXP A 0 A_Explode(10 * DARKFORCES_DMG_MULT, 48, 0, false, 48)
			WEXP A 3 Bright Light("YellowExplosion1")
			WEXP B 3 Bright Light("YellowExplosion2")
			WEXP C 3 Bright Light("YellowExplosion3")
			WEXP D 3 Bright Light("YellowExplosion4")
			WEXP E 3 Bright Light("YellowExplosion5")
			WEXP F 3 Bright Light("YellowExplosion6")
			stop
	}
}

actor AssaultCannonMissile : SH_BasePlayerProjectile
{
	Radius 6
	Height 6
	Speed 18
	Damage (20)
	obituary "%o became another victim to %k's Assault Cannon"
	SeeSound "Max/Trail"
	ActiveSound "Max/Trail"

	DamageType "MaxExplosion"
	Scale 0.8

	PROJECTILE
	+NEVERADJUSTSPRITECLIPPING
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+FORCERADIUSDMG
	+FORCEPAIN
	-NOTELEPORT
	
	States
	{
		Spawn:
			ACMI AB 4 BRIGHT A_LoopActiveSound
			loop
		Death:
			ACMB A 0 A_StopSound(CHAN_VOICE)
			ACMB A 0 Bright A_Quake (1, 8, 0, 100 * DARKFORCES_RAD_MULT, "") 
			ACMB A 0 Bright A_Quake (2, 8, 0, 75 * DARKFORCES_RAD_MULT, "") 
			ACMB A 0 Bright A_Quake (4, 8, 0, 50 * DARKFORCES_RAD_MULT, "") 
			ACMB A 0 A_PlaySound("Max/MedExplo", CHAN_AUTO)
			ACMB A 0 A_SetScale(2.0, 2.0)
			ACMB A 0 A_Blast(BF_AFFECTBOSSES, 512, 20 * DARKFORCES_RAD_MULT, 10, "BlastEffect", "") // BaseEffect is temporary...
			ACMB A 0 A_Explode (70 * DARKFORCES_DMG_MULT, 40 * DARKFORCES_RAD_MULT, XF_HURTSOURCE)
			ACMB ABCDEFHIJKL 3 BRIGHT
			Stop
	}
}

actor GoldAssaultCannonMissile : AssaultCannonMissile
{
	Speed 36
	States
	{
		Spawn:
			WMSL AB 4 BRIGHT A_LoopActiveSound
			Loop
		Death:
			ACMB A 0 A_StopSound(CHAN_VOICE)
			ACMB A 0 Bright A_Quake (1, 12, 0, 100 * DARKFORCES_RAD_MULT, "") 
			ACMB A 0 Bright A_Quake (2, 8, 0, 75 * DARKFORCES_RAD_MULT, "") 
			ACMB A 0 Bright A_Quake (4, 8, 0, 50 * DARKFORCES_RAD_MULT, "") 
			ACMB A 0 A_PlaySound("Max/MedExplo", CHAN_AUTO)
			ACMB A 0 A_SetScale(2.0, 2.0)
			ACMB A 0 A_Blast(BF_AFFECTBOSSES, 512, 20 * DARKFORCES_RAD_MULT, 10, "BlastEffect", "") // BaseEffect is temporary...
			ACMB A 0 A_Explode (90 * DARKFORCES_DMG_MULT, 40 * DARKFORCES_RAD_MULT, XF_HURTSOURCE)
			ACMB ABCDEFHIJKL 3 BRIGHT A_Explode (1, 25 * DARKFORCES_RAD_MULT, 0, false, 25 * DARKFORCES_RAD_MULT)
			Stop
	}
}

actor CutterBarrelTracker : Inventory
{
	inventory.amount 1
	inventory.maxamount 5
}

actor QuadConcBarrelTracker : Inventory
{
	inventory.amount 1
	inventory.maxamount 1
}

actor Bullet : SH_BasePlayerFastProjectile
{
	RenderStyle Add
	Alpha 1.0
	Radius 2
	Height 2
	Speed 67 // 270 >> 2
	Damage (10 * DARKFORCES_DMG_MULT)
	Decal "BulletChip"
	Obituary "%o got blasted by %k, like the rebel scum he is!"
	DeathSound "Max/LZHIT"
	Scale 0.8

	DamageType "MaxBlasterBolt"

	PROJECTILE
	-ACTIVATEIMPACT
	
	States
	{
		Spawn:
			BULL A 1 BRIGHT Light("SmallBlueBolt")
			loop
		Death:
			BUEX A 0 A_CustomBulletAttack(0, 0, 1, 0, "SigilHitscanHax", 32, CBAF_AIMFACING | CBAF_EXPLICITANGLE | CBAF_NORANDOM)
			BUEX A 0 A_SetTranslucent(1.0, 0)
			BUEX ABCDEFG 2 BRIGHT
			Stop
	}
}

actor GoldBullet : Bullet
{
	Damage (12 * DARKFORCES_DMG_MULT)
	Scale 0.8
	States
	{
	Spawn:
		GBUL A 1 BRIGHT Light("OrangeBolt")
		loop
	Death:
		BUEX A 0 A_SetTranslucent(1.0, 0)
		BUEX ABCDEFG 2 BRIGHT
		Stop
	}
}

actor ScopeZoom : Inventory
{
	inventory.maxamount 1
}

actor MortarGun : SH_Weapon
{
	Tag "Packered Mortar Gun"

	Weapon.SelectionOrder 7
	Weapon.AmmoUse1 1
	Weapon.AmmoType "RoketAmmo"
	Weapon.AmmoGive 0 // Handled in ACS
	Weapon.UpSound "Max/WepRaise"
	Weapon.Kickback 300
	Weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 5
	Weapon.BobRangeX 1
	Weapon.BobRangeY 1
	Weapon.BobSpeed 1
	
	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset 24
	
	Inventory.Pickupmessage "Mortar Gun"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.ALT_AMMO_OPTIONAL
	States
	{
		Ready:
			MORT A 0 A_JumpIfInventory("Tier5upgradetoken", 1, "GoldReady")
			MORT A 1 A_WeaponReady(WRF_NOSECONDARY)
			loop
		Deselect:
			MORT A 0 A_JumpIfInventory("Tier5upgradetoken", 1, "GoldDeselect")
			MORT A 1 A_Lower
			TNT1 AA 0 A_Lower
			loop
		Select:
			MORT A 0 A_JumpIfInventory("Tier5upgradetoken", 1, "GoldSelect")
			MORT A 1 A_Raise
			TNT1 AA 0 A_Raise
			loop
		Fire:
			MORT B 0 A_JumpIfNoAmmo("DryFire")
			MORT B 0 A_JumpIfInventory("Tier5upgradetoken", 1, "GoldFire")
			MORT B 0 A_PlaySound ("Max/MortarFire", CHAN_WEAPON)
			MORT B 0 A_FireCustomMissile("Mortarnade", 0, CallACS("SUPERCHARGE_CHECK"), 5)
			MORT B 12 bright A_GunFlash
			MORT C 0 A_PlaySound("Max/MortarLoad", CHAN_AUTO)
			MORT CDA 4 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
			MORT A 0 A_ReFire
			Goto Ready
		DryFire:
			MORT A 0 A_JumpIfInventory("Tier5upgradetoken", 1, "GoldDryFire")
			MORT A 0 A_PlaySound("Max/MortarDryFire", CHAN_WEAPON)
			MORT A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			MORT A 0 A_Refire
			goto Ready
	//	AltFire:
	//		MORT B 0 A_PlaySound ("Max/mortarfire")
	//		TNT1 A 0 A_FireCustomMissile("Mortarbouncenade",1,1,0)
	//		MORT B 18 bright A_GunFlash
	//		MORT C 6 A_PlaySound ("Max/mortarload")
	//		MORT D 6
	//		MORT A 12
	//		MORT A 2 A_ReFire
	//		Goto Ready
		Flash:
			TNT1 A 0 BRIGHT A_Light2
			TNT1 A 6 A_SetPitch(pitch-2.0, 0)	
			TNT1 A 0 BRIGHT A_Light1
			TNT1 AAAAAAAA 1 bright A_SetPitch(pitch+0.25, 0)
			goto LightDone
		
		GoldReady:
			FC1G A 1 A_WeaponReady(WRF_NOSECONDARY)
			loop
		GoldDeselect:
			FC1G A 1 A_Lower
			FC1G AA 0 A_Lower
			loop
		GoldSelect:
			FC1G A 1 A_Raise
			FC1G AA 0 A_Raise
			loop
		GoldFire:
			FC1G A 0 A_PlaySound ("Max/FlakAlt", CHAN_WEAPON)
			FC1G A 0 A_FireCustomMissile("FlakBall", frandom(1.0, 3.5), CallACS("SUPERCHARGE_CHECK"), 2)
			FC1G A 0 A_FireCustomMissile("FlakBall", frandom(-1.0, -3.5), CallACS("SUPERCHARGE_CHECK"), 8)
			FC1G A 0 A_Quake (1.0, 6, 0, 64, "")
			FC1G A 3 bright	A_GunFlash("GoldFlash")
			FC1G BCDE 3
			FC1G A 9 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
			FC1G A 0 A_ReFire
			Goto GoldReady
		GoldDryFire:
			FC1G A 0 A_PlaySound("Max/MortarDryFire", CHAN_WEAPON)
			FC1G A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			FC1G A 0 A_Refire
			goto GoldReady
		GoldFlash:
			FC1F A 0 Bright A_SetPitch(pitch-3.0)
			FC1F A 1 Bright A_Light1
			FC1F B 1 Bright A_Light2
			FC1F C 1 Bright A_Light1
			FC1F C 0 Bright A_Light0
			TNT1 AAAAAAAAAAAA 1 A_SetPitch(pitch+0.25, 0)//SPF_INTERPOLATE)
			goto LightDone
			
		Spawn:
			MOGU A -1
			Stop
	}
}

actor ConcRifle : SH_Weapon
{
	Tag "Stouker Concussion Rifle"
	obituary "%o was unlucky to meet %k's Concussion Rifle"
	Weapon.SelectionOrder 4
	Weapon.AmmoUse 4
	Weapon.AmmoGive 0 // Handled in ACS
	Weapon.AmmoType "DF_PowerCell"
	Weapon.Kickback 384
	Weapon.UpSound "Max/WepRaise"
	Inventory.PickupMessage "Concussion Rifle"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	Weapon.SlotNumber 6
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 1
	Weapon.BobRangeY 1
	Weapon.BobSpeed 1
	
	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset 24
	
	+WEAPON.NOAUTOFIRE
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.ALT_AMMO_OPTIONAL
	states
	{
		Select:
			CONC A 0 A_JumpIfInventory ("Tier6upgradetoken", 1, "GoldSelect")
			CONC A 1 A_Raise
			CONC AA 0 A_Raise
			loop
		
		Deselect:
			CONC A 0 A_JumpIfInventory ("Tier6upgradetoken", 1, "GoldDeselect")
			CONC A 1 A_Lower
			CONC AA 0 A_Lower
			loop
		
		Ready:
			CONC A 0 A_JumpIfInventory ("Tier6upgradetoken", 1, "GoldReady")
			CONC A 1 A_WeaponReady
			Loop
		
		Fire:
			CONC B 0 A_JumpIfNoAmmo("DryFire")
			CONC B 0 A_JumpIfInventory ("Tier6upgradetoken", 1, "GoldFire")
			CONC B 0 A_PlaySound ("Max/ConcRifle", CHAN_WEAPON)
			CONC B 0 A_FireCustomMissile ("ConcussionBlast", 0, CallACS("SUPERCHARGE_CHECK"), 5, 0, 0)
			CONC B 14 bright A_GunFlash
			CONC C 4 bright
			CONC A 10
			CONC A 0 A_ReFire
			Goto Ready
			
		DryFire:
			CONC A 0 A_PlaySound("Max/ConcDryFire", CHAN_WEAPON)
			CONC A 0 A_JumpIfInventory ("Tier6upgradetoken", 1, "GoldDryFire")
			CONC A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			COCC A 0 A_Refire
			goto Ready

		Flash:
			TNT1 A 0 bright A_Light2
			TNT1 A 7 A_SetPitch(pitch-2.0)
			TNT1 A 0 bright A_Light1
			TNT1 AAAAAAAA 1 A_SetPitch(pitch+0.25)
			goto LightDone

		GoldSelect:
			QUAD A 1 A_Raise
			QUAD AA 0 A_Raise
			loop
		GoldDeselect:
			QUAD A 1 A_Lower
			QUAD AA 0 A_Lower
			loop
		GoldReady:
			QUAD A 1 A_WeaponReady
			loop
		GoldFire:
			QUAD B 0 A_JumpIfInventory("QuadConcBarrelTracker", 1, "GoldFire2")
			QUAD B 0 A_PlaySound ("Max/ConcRifle", CHAN_WEAPON)
			QUAD B 0 A_FireCustomMissile ("ConcussionBlast", frandom(0.0, 1.5), CallACS("SUPERCHARGE_CHECK"), 3, -1, 0, frandom(-0.75, 0.75))
			QUAD B 0 A_FireCustomMissile ("ConcussionBlast", frandom(-1.5, 0.0), CallACS("SUPERCHARGE_CHECK"), 9, -1, 0, frandom(-0.75, 0.75))
			QUAD B 0 A_GunFlash
			QUAD B 0 A_GiveInventory("QuadConcBarrelTracker", 1)
			QUAD B 2 bright A_Quake (1.0, 6, 0, 64, "")
			QUAD C 1 Offset(2, 34)
			QUAD C 1 Offset(4, 36)
			QUAD D 1 Offset(6, 38)
			QUAD D 1 Offset(8, 40)
			QUAD A 1 Offset(9, 41)
			QUAD A 1 Offset(6, 38)
			QUAD A 1 Offset(2, 34)
			QUAD A 1 Offset(1, 33)
			QUAD A 18 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH | WRF_NOBOB) // Clear Offsets
			QUAD A 0 A_ReFire
			Goto Ready
		GoldFire2:
			QUAD E 0 A_PlaySound ("Max/ConcRifle", CHAN_WEAPON)
			QUAD E 0 A_FireCustomMissile ("ConcussionBlast", frandom(0.0, 1.5), CallACS("SUPERCHARGE_CHECK"), 3, 3, 0, frandom(-0.75, 0.75))
			QUAD E 0 A_FireCustomMissile ("ConcussionBlast", frandom(-1.5, 0.0), CallACS("SUPERCHARGE_CHECK"), 9, 3, 0, frandom(-0.75, 0.75))
			QUAD E 0 A_GunFlash
			QUAD E 0 A_TakeInventory("QuadConcBarrelTracker", 1)
			QUAD E 2 bright A_Quake (1.0, 6, 0, 64, "")
			QUAD F 1 Offset(3, 34)
			QUAD F 1 Offset(6, 36)
			QUAD G 1 Offset(9, 38)
			QUAD G 1 Offset(12, 40)
			QUAD A 1 Offset(13, 41)
			QUAD A 1 Offset(6, 38)
			QUAD A 1 Offset(2, 34)
			QUAD A 1 Offset(1, 33)
			QUAD A 18 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH | WRF_NOBOB) // Clear Offsets
			QUAD A 0 A_ReFire
			Goto Ready
		GoldDryFire:
			QUAD A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			QUAD A 0 A_Refire
			goto GoldReady
	
		Spawn:
			CRIF A -1
			Stop
	}
}

actor AssaultCannon : SH_Weapon
{
	obituary "%o became another victim to %k's Assault Cannon"
	weapon.selectionorder 1
	weapon.ammouse 1
	weapon.ammouse2 1
	Weapon.AmmoGive 0 // Handled in ACS
	Weapon.AmmoGive2 0 // Ditto
	Weapon.AmmoType "DF_PlasmaCartridge"
	Weapon.AmmoType2 "DF_Missile"
	Weapon.kickback 100
	Weapon.UpSound "Max/WepRaise"
	Inventory.PickupMessage "Assault Cannon"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 8
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	
	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset 16
	
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.ALT_AMMO_OPTIONAL
	states
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldReady")
			ASAU A 1 A_WeaponReady
			loop
		Select:
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldSelect")
			ASAU A 1 A_Raise
			TNT1 AA 0 A_Raise
			loop
		Deselect:
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldDeselect")
			ASAU A 1 A_Lower
			TNT1 AA 0 A_Lower
			loop
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("DryFire")
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldFire")
			ASAU B 0 A_PlaySound ("Max/Assault", CHAN_WEAPON)
			ASAU B 0 A_FireCustomMissile ("AssaultCannonPlasma", frandom(-0.75, 0.75), CallACS("SUPERCHARGE_CHECK"), 10, 0, 0, frandom(-0.75, 0.75))
			ASAU B 6 bright A_GunFlash
			ASAU B 1
			ASAU A 0 A_Refire
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfNoAmmo("DryFire")
			TNT1 A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldAltFire")
			ASAU C 6
			ASAU C 0 A_PlaySound ("Max/Missile", CHAN_WEAPON)
			ASAU C 0 A_FireCustomMissile ("AssaultCannonMissile", 0, CallACS("SUPERCHARGE_CHECK"), 10, 0, 0)
			ASAU D 20 bright A_GunFlash("AltFlash")
			ASAU A 16
			ASAU A 1 A_ReFire
			Goto Ready
		DryFire:
			ASAU A 0 A_PlaySound("Max/AssaultDryFire", CHAN_WEAPON)
			ASAU A 0 A_JumpIfInventory("Tier7upgradetoken", 1, "GoldDryFire")
			ASAU A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			ASAU A 0 A_Refire
			goto Ready
		Flash:
			TNT1 A 0 bright A_Light2
			TNT1 A 2 bright A_SetPitch(pitch-1.125)
			TNT1 A 0 bright A_Light1
			TNT1 AAA 1 A_SetPitch(pitch+0.375)	
			goto LightDone
		AltFlash:
			TNT1 A 0 bright A_Light2
			TNT1 A 8 bright A_SetPitch(pitch-3.0)
			TNT1 A 0 bright A_Light1
			TNT1 AAAAAAAAAA 1 A_SetPitch(pitch+0.3)	
			goto LightDone
		GoldReady:
			BLWH A 1 A_WeaponReady
			loop
		GoldSelect:
			BLWH A 1 A_Raise
			TNT1 AA 0 A_Raise
			loop
		GoldDeselect:
			BLWH A 1 A_Lower
			TNT1 AA 0 A_Lower
			loop
		GoldFire:		
			BLWH B 0 A_PlaySound ("Max/BWPlasma")
			BLWH B 0 A_FireCustomMissile ("GoldAssaultCannonPlasma", frandom(-0.75, 0.75), CallACS("SUPERCHARGE_CHECK"), 10, -0.5, 0, frandom(-0.75, 0.75))
			BLWH B 2 Bright A_GunFlash
			BLWH C 2 Bright
			BLWH D 2 Bright
			BLWH A 1
			BLWH A 1 A_Refire
			Goto Ready
		GoldAltFire:
			BLWH E 2 Bright
			BLWH F 2 Bright
			BLWH F 0 A_PlaySound ("Max/BWMissile", 1)
			BLWH F 0 A_PlaySound ("Max/BWRock", 2)
			BLWH F 0 A_FireCustomMissile ("GoldAssaultCannonMissile", 0, CallACS("SUPERCHARGE_CHECK"), 10, 0, 0)
			BLWH G 2 Bright A_GunFlash("AltFlash")
			BLWH H 2 Bright
			BLWH R 29
			BLWH I 2
			BLWH J 2
			BLWH A 1 A_ReFire
			Goto Ready
		GoldDryFire:
			BLWH A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			BLWH A 0 A_ReFire
			goto Ready
		Spawn:
			ACAN A -1
			Stop
	}
}

actor DisruptorRifle : SH_Weapon
{
	Tag "Tenloss DXR-6 Disruptor Rifle"
	obituary "%o was disintegrated by %k"
	weapon.selectionorder 4
	weapon.ammouse 2
	weapon.ammogive 0 // Handled in ACS
	weapon.ammotype "RailSlugs"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	Inventory.pickupmessage "Disruptor Rifle"
	Weapon.UpSound "Max/WepRaise"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 7
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	
	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset 24
	
	States
	{
		Select:
			TNT1 AA 0 A_Raise
			DRIF A 1 A_Raise
			Goto Select+1
		Deselect:
			TNT1 AA 0 A_Lower
			DRIF A 1 A_Lower
			Goto Deselect+1
		Ready:
			DRIF A 1 A_WeaponReady
			Loop
		Fire:
			DRIF A 1 A_RailAttack(400, 0, CallACS("SUPERCHARGE_CHECK"), "FF FF FF", "FF FF FF", RGF_FULLBRIGHT|RGF_SILENT, 0, "SH_PlayerBulletPuff", 0, 0, 0, 35, 2.0, 4.0, "REDLightRailBlast")
			TNT1 A 0 A_PlaySound("Max/Disrupt", CHAN_WEAPON)
			DRIF B 1 bright A_GunFlash
			DRIF C 1
			TNT1 A 0 A_JumpIfNoAmmo("Ready")
			TNT1 A 0 A_PlaySound("Max/DisrCh", 7)
			DRIF D 1
			DRIF E 1
			DRIF F 1
			DRIF E 5
			DRIF D 1
			DRIF A 77 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
			DRIF A 16 A_WeaponReady(WRF_NOFIRE)
			DRIF A 0 A_Refire
			Goto Ready
		Flash:
			TNT1 A 0 bright A_Light2
			TNT1 A 4 A_SetPitch(pitch-2.0)
			TNT1 A 0 bright A_Light1
			TNT1 AAAAAAAA 1 A_SetPitch(pitch+0.25)
			goto LightDone
		Spawn:
			DIRF A -1
			Stop
	}
}

actor FusionCutter : SH_Weapon
{
	Tag "Jeron Fusion Cutter"
	obituary "%k demonstrated the power of the fusion cutter tool to %o"
	Weapon.selectionorder 4
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 4
	Weapon.AmmoGive1 0 // Handled in ACS
	Weapon.AmmoType1 "DF_PowerCell"
	Weapon.AmmoType2 "DF_PowerCell"
	Weapon.UpSound "Max/WepRaise"
	Weapon.kickback 100
	Weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 3
	Weapon.BobRangeX 1
	Weapon.BobRangeY 1
	Weapon.bobspeed 1
	
	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset 24
	
	Weapon.kickback 1024
	Inventory.PickupMessage "Fusion Cutter"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.ALT_AMMO_OPTIONAL
	states
	{
		Select:
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldSelect")
			FUSI A 1 A_Raise
			TNT1 AA 0 A_Raise
			loop
		Deselect:
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldDeselect")
			FUSI A 1 A_Lower
			TNT1 AA 0 A_Lower
			loop
		Ready:
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldReady")
			FUSI A 1 A_WeaponReady
			Loop
		DryFire:
			FUSI A 0 A_PlaySound("Max/FusionDryFire", CHAN_WEAPON)
			FUSI A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldDryFire")
			FUSI A 18
			FUSI A 0 A_Refire
			goto Ready
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("DryFire")
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldFire")
			FUSI A 0 A_JumpIfInventory("CutterBarrelTracker", 1, "Fire2")
			FUSI A 0 A_PlaySound("Max/fusion", CHAN_WEAPON)
			FUSI B 0 bright A_FireCustomMissile ("CutBlast", 0, CallACS("SUPERCHARGE_CHECK"), -12, 0, 0)
			FUSI B 4 bright A_GunFlash
			FUSI A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire2:
			FUSI A 0 A_JumpIfInventory("CutterBarrelTracker", 2, "Fire3")
			FUSI C 0 A_PlaySound("Max/fusion", CHAN_WEAPON)
			FUSI C 0 bright A_FireCustomMissile ("CutBlast", 0, CallACS("SUPERCHARGE_CHECK"), -5, -1, 0)
			FUSI C 4 bright A_GunFlash
			FUSI A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire3:
			FUSI A 0 A_JumpIfInventory("CutterBarrelTracker", 3, "Fire4")
			FUSI D 0 A_PlaySound ("Max/fusion", CHAN_WEAPON)
			FUSI D 0 bright A_FireCustomMissile ("CutBlast", 0, CallACS("SUPERCHARGE_CHECK"), 4, -2, 0)
			FUSI D 4 bright A_GunFlash
			FUSI A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire4:
			FUSI A 0 A_JumpIfInventory("CutterBarrelTracker", 4, "Fire5")
			FUSI E 0 A_PlaySound ("Max/fusion", CHAN_WEAPON)
			FUSI E 0 bright A_FireCustomMissile ("CutBlast", 0, CallACS("SUPERCHARGE_CHECK"), 12, -3, 0)
			FUSI E 4 bright A_GunFlash
			FUSI A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire5:
			FUSI A 0 A_JumpIfInventory("CutterBarrelTracker", 5, "Fire6")
			FUSI D 0 A_PlaySound ("Max/fusion", CHAN_WEAPON)
			FUSI D 0 bright A_FireCustomMissile ("CutBlast", 0, CallACS("SUPERCHARGE_CHECK"), 4, -2, 0)
			FUSI D 4 bright A_GunFlash
			FUSI A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUSI A 1 A_ReFire
			Goto Ready
		Fire6:
			FUSI C 0 A_PlaySound ("Max/fusion", CHAN_WEAPON)
			FUSI C 0 bright A_FireCustomMissile ("CutBlast", 0, CallACS("SUPERCHARGE_CHECK"), -5, -1, 0)
			FUSI C 4 bright A_GunFlash
			FUSI A 4 A_TakeInventory ("CutterBarrelTracker", 5)
			FUSI A 1 A_ReFire
			Goto Ready
		AltFire:
			TNT1 A 0 A_Jumpifnoammo("DryFire")
			TNT1 A 0 A_JumpIfInventory ("Tier3upgradetoken", 1, "GoldAltFire")
			FUSI F 0 A_PlaySound ("Max/fusion", CHAN_AUTO)
			TNT1 A 0 A_PlaySound ("Max/fusion", CHAN_AUTO)
			TNT1 A 0 A_PlaySound ("Max/fusion", CHAN_AUTO)
			TNT1 A 0 A_PlaySound ("Max/fusion", CHAN_AUTO)
			FUSI F 0 A_FireCustomMissile ("CutBlast", 4, CallACS("SUPERCHARGE_CHECK"), -12, 0, 0)
			FUSI F 0 A_FireCustomMissile ("CutBlast", 1, false, -5, -1, 0)
			FUSI F 0 A_FireCustomMissile ("CutBlast", -1, false, 4, -2, 0)
			FUSI F 0 A_FireCustomMissile ("CutBlast", -4, false, 12, -3, 0)
			FUSI F 8 bright A_GunFlash("AltFlash")
			FUSI A 8
			FUSI A 1 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 0 bright A_Light2
			TNT1 A 2 A_SetPitch(pitch-0.75)
			TNT1 A 0 bright A_Light1
			TNT1 AAA 1 A_SetPitch(pitch+0.25)
			goto LightDone
		AltFlash:
			TNT1 A 0 bright A_Light2
			TNT1 A 4 A_SetPitch(pitch-2.0)
			TNT1 A 0 bright A_Light1
			TNT1 AAAA 1 A_SetPitch(pitch+0.5)
			goto LightDone
		GoldSelect:
			FUS2 A 1 A_Raise
			TNT1 AA 0 A_Raise
			loop
		GoldDeselect:
			FUS2 A 1 A_Lower
			TNT1 AA 0 A_Lower
			loop
		GoldReady:
			FUS2 A 1 A_WeaponReady
			Loop
		GoldDryFire:
			FUS2 A 18
			FUS2 A 0 A_Refire
			goto GoldReady
		GoldFire:
			FUS2 A 0 A_JumpIfInventory("CutterBarrelTracker", 1, "GoldFire2")
			TNT1 A 0 A_PlaySound ("Max/Goob")
			FUS2 B 0 bright A_FireCustomMissile ("GoobBlast", 0, CallACS("SUPERCHARGE_CHECK"), -12, 0, 0)
			FUS2 B 4 bright A_GunFlash
			FUS2 A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire2:
			TNT1 A 0 A_JumpIfInventory("CutterBarrelTracker", 2, "GoldFire3")
			FUS2 C 0 A_PlaySound ("Max/Goob")
			FUS2 C 0 bright A_FireCustomMissile ("GoobBlast", 0, CallACS("SUPERCHARGE_CHECK"), -5, -1, 0)
			FUS2 C 4 bright A_GunFlash
			FUS2 A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire3:
			TNT1 A 0 A_JumpIfInventory("CutterBarrelTracker", 3, "GoldFire4")
			FUS2 D 0 A_PlaySound ("Max/Goob")
			FUS2 D 0 bright A_FireCustomMissile ("GoobBlast", 0, CallACS("SUPERCHARGE_CHECK"), 4, -2, 0)
			FUS2 D 4 bright A_GunFlash
			FUS2 A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire4:
			TNT1 A 0 A_JumpIfInventory("CutterBarrelTracker", 4, "GoldFire5")
			FUS2 E 0 A_PlaySound ("Max/Goob")
			FUS2 E 0 bright A_FireCustomMissile ("GoobBlast", 0, CallACS("SUPERCHARGE_CHECK"), 12, -3, 0)
			FUS2 E 4 bright A_GunFlash
			FUS2 A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire5:
			TNT1 A 0 A_JumpIfInventory("CutterBarrelTracker", 5, "GoldFire6")
			FUS2 D 0 A_PlaySound ("Max/Goob", CHAN_WEAPON)
			FUS2 D 4 bright A_FireCustomMissile ("GoobBlast", 0, CallACS("SUPERCHARGE_CHECK"), 4, -2, 0)
			FUS2 D 0 bright A_GunFlash
			FUS2 A 4 A_GiveInventory ("CutterBarrelTracker", 1)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldFire6:
			FUS2 C 0 A_PlaySound ("Max/Goob")
			FUS2 C 4 bright A_FireCustomMissile ("GoobBlast", 0, CallACS("SUPERCHARGE_CHECK"), -5, -1, 0)
			FUS2 C 4 bright A_GunFlash
			FUS2 A 4 A_TakeInventory ("CutterBarrelTracker", 5)
			FUS2 A 1 A_ReFire
			Goto Ready
		GoldAltFire:
			FUS2 F 0 A_PlaySound ("Max/Goob", 1)
			TNT1 A 0 A_PlaySound ("Max/Goob", 2)
			TNT1 A 0 A_PlaySound ("Max/Goob", 3)
			TNT1 A 0 A_PlaySound ("Max/Goob", 4)
			FUS2 F 0 A_FireCustomMissile ("GoobBlast", 4, CallACS("SUPERCHARGE_CHECK"), -12, 0, 0)
			FUS2 F 0 A_FireCustomMissile ("GoobBlast", 1, false, -5, -1, 0)
			FUS2 F 0 A_FireCustomMissile ("GoobBlast", -1, false, 4, -2, 0)
			FUS2 F 0 A_FireCustomMissile ("GoobBlast", -4, false, 12, -3, 0)
			FUS2 F 8 bright A_GunFlash("AltFlash")
			FUS2 A 8
			FUS2 A 1 A_ReFire
			Goto Ready
		Spawn:
			FCUT A -1
			Stop
	}
}

actor Autogun : SH_Weapon
{
	Tag "Imperial Repeater Rifle"
	obituary "%o got in the way of %k's repeater rifle"
	Weapon.SlotNumber 4
	Weapon.SelectionOrder 5
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 3
	Weapon.AmmoGive1 0 // Handled in ACS
	Weapon.AmmoType1 "DF_PowerCell"
	Weapon.AmmoType2 "DF_PowerCell"
	Weapon.UpSound "Max/WepRaise"
	Weapon.kickback 100
	weapon.BobStyle "InverseSmooth"
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	
	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset 24
	
	Inventory.PickupMessage "Autogun"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.ALT_AMMO_OPTIONAL
	states
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldReady")
			AUTO A 1 A_WeaponReady
			Loop
		Select:
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldSelect")
			AUTO A 1 A_Raise
			TNT1 AA 0 A_Raise
			Loop
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_WEAPON)
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldDeselect")
			AUTO A 1 A_Lower
			TNT1 AA 0 A_Lower
			Loop
		DryFire:
			AUTO A 0 A_PlaySound("Max/RepeaterDryFire", CHAN_WEAPON)
			AUTO A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldDryFire")
			AUTO A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			AUTO A 0 A_Refire
			goto Ready
		Fire:
			TNT1 A 0 A_Jumpifnoammo("DryFire")
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldFire")
			AUTO B 0 A_PlaySound ("Max/autogun", CHAN_WEAPON)
			AUTO B 0 Bright A_FireCustomMissile ("Bullet", 0, CallACS("SUPERCHARGE_CHECK"), 10, 0, 0)
			AUTO B 2 Bright A_GunFlash
			AUTO C 2
			AUTO C 0 A_ReFire
			Goto Ready
		Hold:
			TNT1 A 0 A_Jumpifnoammo("DryFire")
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldHold")
			TNT1 A 0 A_PlaySound ("Max/autoloop", CHAN_WEAPON, 1, 1)
			AUTO C 2	
			AUTO B 0 Bright A_FireCustomMissile ("Bullet", frandom(-0.5, 0.5), CallACS("SUPERCHARGE_CHECK"), 10, 0, 0, frandom(-0.5, 0.5))
			AUTO B 2 Bright A_GunFlash
			AUTO C 2
			TNT1 C 0 A_ReFire
			TNT1 A 0 A_StopSound(CHAN_WEAPON)
			Goto Ready
		AltFire:
			TNT1 A 0 A_Jumpifnoammo("DryFire")
			TNT1 A 0 A_JumpIfInventory ("Tier4upgradetoken", 1, "GoldAltFire")
			AUTO B 0 A_PlaySound ("Max/autogun", CHAN_WEAPON)
			AUTO B 0 A_FireCustomMissile ("Bullet", -2.0 + frandom(-0.5, 0.5), CallACS("SUPERCHARGE_CHECK"), 5,  -6, 0, -1.0 + frandom(-0.5, 0.5))
			AUTO B 0 A_FireCustomMissile ("Bullet", 2.0 + frandom(-0.5, 0.5), false, 15, -6, 0, -1.0 + frandom(-0.5, 0.5))
			AUTO B 0 A_FireCustomMissile ("Bullet", frandom(-0.5, 0.5), false, 10, 0, 0, 2.25 + frandom(-0.5, 0.5))
			AUTO B 2 BRIGHT A_GunFlash("AltFlash")
			AUTO C 2
			AUTO A 8
			AUTO A 0 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 0 bright A_Light2
			TNT1 A 2 bright A_SetPitch(pitch-0.5)
			TNT1 A 0 bright A_Light1
			TNT1 AA 1 bright A_SetPitch(pitch+0.25)	
			goto LightDone
		AltFlash:
			TNT1 A 0 bright A_Light2
			TNT1 A 4 bright A_SetPitch(pitch-1.5)
			TNT1 A 0 bright A_Light1
			TNT1 AAA 1 bright A_SetPitch(pitch+0.5)	
			goto LightDone
		GoldReady:
			GYRO A 1 A_WeaponReady
			Loop
		GoldSelect:
			GYRO A 1 A_Raise
			TNT1 AA 0 A_Raise
			Loop
		GoldDeselect:
			GYRO A 1 A_Lower
			TNT1 AA 0 A_Lower
			Loop
		GoldDryFire:
			GYRO A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			GYRO A 0 A_Refire
			goto Ready
		GoldFire:
			GYRO B 0 A_PlaySound ("Max/GAutogun", CHAN_WEAPON)
			GYRO B 0 bright A_FireCustomMissile ("GoldBullet", 0, CallACS("SUPERCHARGE_CHECK"), 10, 0, 0)
			GYRO B 2 bright A_GunFlash
			GYRO C 2
			GYRO C 0 A_ReFire
			Goto Ready
		GoldHold:		
			TNT1 A 0 A_PlaySound ("Max/GAutoloop", CHAN_WEAPON, 1, 1)
			GYRO B 0 Bright A_FireCustomMissile ("GoldBullet", frandom(-0.5, 0.5), CallACS("SUPERCHARGE_CHECK"), 10, 0, 0, frandom(-0.5, 0.5))
			GYRO B 2 Bright A_GunFlash
			GYRO C 3
			GYRO C 0 A_ReFire
			GYRO A 0 A_StopSound(CHAN_WEAPON)
			Goto Ready
		GoldAltFire:
			GYRO B 0 A_PlaySound ("Max/GAutogun", CHAN_WEAPON)
			GYRO B 0 A_FireCustomMissile ("GoldBullet", -2.0 + frandom(-0.5, 0.5), CallACS("SUPERCHARGE_CHECK"), 5,  -6, 0, -1.0 + frandom(-0.5, 0.5))
			GYRO B 0 A_FireCustomMissile ("GoldBullet", 2.0 + frandom(-0.5, 0.5), false, 15, -6, 0, -1.0 + frandom(-0.5, 0.5))
			GYRO B 0 A_FireCustomMissile ("GoldBullet", frandom(-0.5, 0.5), false, 10, 0, 0, 2.25 + frandom(-0.5, 0.5))
			GYRO B 2 BRIGHT A_GunFlash("AltFlash")
			GYRO C 2
			GYRO A 8
			GYRO A 0 A_ReFire
			Goto Ready
		Spawn:
			AGUN A -1
			Stop
	}
}

actor DFPistol : SH_Weapon
{
	Tag "K-16 Bryar Pistol"
	obituary "%o couldn't handle %k's blaster pistol"
	Weapon.SelectionOrder 9
	Weapon.AmmoUse 1
	Weapon.AmmoGive 15
	Weapon.AmmoType "DF_EnergyUnit"
	Weapon.UpSound "Max/WepRaise"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	Decal ""
	Inventory.PickupMessage "Bryar Pistol"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 2
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	
	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset 24
	
	Weapon.PowerupItem "Tier1upgradetoken"
	Weapon.SisterWeapon "DL44"
	
	+WEAPON.AMMO_OPTIONAL
	states
	{
		Ready:
			BRYP A 1 A_WeaponReady
			Loop
		Deselect:
			NULL AA 0 A_Lower
			BRYP A 1 A_Lower
			loop
		Select:
			NULL AA 0 A_Raise
			BRYP A 1 A_Raise
			loop
		DryFire:
			BRYP A 0 A_PlaySound("Max/BryarDryFire", CHAN_WEAPON)
			BRYP A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			BRYP A 0 A_Refire
			goto Ready
		Fire:
			BRYP A 0 A_JumpIfNoAmmo("DryFire")
			BRYP A 0 A_FireCustomMissile("BlastBolt", 0.25, CallACS("SUPERCHARGE_CHECK"), 5, 0, 0, 0)
			BRYP A 0 A_PlaySound ("Max/Bryar")
			BRYP B 4 bright A_GunFlash
			BRYP C 4 bright
			BRYP A 9
			BRYP A 0 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 0 bright A_Light2
			TNT1 A 2 bright A_SetPitch(pitch-0.5)
			TNT1 A 0 bright A_Light1
			TNT1 AA 1 bright A_SetPitch(pitch+0.25)	
			Goto LightDone
		Spawn:
			BPIS A -1
			Stop
	}
}

actor DL44 : DFPistol
{
	Tag "BlasTech DL-44 (Tier 1 Upgrade)"
	obituary "%o was Han Solo'd by %k."
	
	Weapon.SisterWeapon "DFPistol"
	
	+WEAPON.POWERED_UP
	
	States
	{
		Select:
			DL44 AA 0 A_Raise
			DL44 A 1 A_Raise
			Loop
		Deselect:
			DL44 AA 0 A_Lower
			DL44 A 1 A_Lower
			Loop
		Ready:
			DL44 A 1 A_WeaponReady
			loop
		DryFire:
			DL44 A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			DL44 A 0 A_Refire
			goto Ready
		Fire:
			DL44 A 0 A_JumpIfNoAmmo("DryFire")
			DL44 A 0 A_FireCustomMissile("DL44Bolt", 0.25, CallACS("SUPERCHARGE_CHECK"), 5, 0, 0, 0)
			DL44 A 0 A_PlaySound ("Max/DL44")
			DL44 B 4 bright A_GunFlash
			DL44 C 3 bright
			DL44 A 2
			DL44 A 0 A_ReFire
			Goto Ready
	}
}

actor DFBlaster : SH_Weapon
{
	Tag "BlasTech E-11"
	obituary "%o was blasted away by %k's E-11 rifle"
	Weapon.SelectionOrder 8
	Weapon.AmmoUse 2
	Weapon.AmmoGive 0 // Handled in ACS
	Weapon.AmmoType "DF_EnergyUnit"
	Weapon.UpSound "Max/WepRaise"
	Inventory.PickupMessage "Blaster Rifle"
	Inventory.ForbiddenTo "Fiend"
	Inventory.ForbiddenTo "Gosimer"
	Inventory.ForbiddenTo "Kunai"
	Inventory.RestrictedTo "MaximusPrime"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 2
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1

	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset 24
	
	Weapon.PowerupItem "Tier2upgradetoken"
	Weapon.SisterWeapon "E-11DCarbine"

	+WEAPON.AMMO_OPTIONAL
	States
	{
		Ready:
			BLRF A 1 A_WeaponReady
			Loop
		Deselect:
			BLRF A 1 A_Lower
			TNT1 AA 0 A_Lower
			Loop
		Select:
			BLRF A 1 A_Raise
			TNT1 AA 0 A_Raise
			Loop
		DryFire:
			BLRF A 0 A_PlaySound("Max/E-11DryFire", CHAN_WEAPON)
			BLRF A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			BLRF A 0 A_Refire
			goto Ready
		Fire:
			BLRF A 0 A_JumpIfNoAmmo("DryFire")
			BLRF A 0 A_PlaySound ("Max/E-11")
			BLRF B 0 bright A_FireCustomMissile("RifBlast", frandom(-2.0,2.0), CallACS("SUPERCHARGE_CHECK"), 4, 0, 0,frandom(-2.0,2.0))
			BLRF B 2 bright A_GunFlash
			BLRF A 3
			BLRF A 2 A_ReFire
			Goto Ready
		Flash:
			TNT1 A 0 Bright A_Light1
			TNT1 A 2 Bright A_SetPitch(pitch-0.5)
			TNT1 A 0 Bright A_Light0
			TTN1 AA 1 Bright A_SetPitch(pitch+0.25)
			Goto LightDone
		Spawn:
			BRIF A -1
			Stop
	}
}

actor E-11DCarbine : DFBlaster
{
	Tag "BlasTech E-11D Carbine (Tier 2 Upgrade)"

	Weapon.SisterWeapon "DFBlaster"
	
	+WEAPON.POWERED_UP
	States
	{
		Deselect:
			TNT1 AA 0 A_Lower
			EE11 A 1 A_Lower
			Loop
		Select:
			TNT1 AA 0 A_Raise
			EE11 A 1 A_Raise
			Loop
		Ready:
			TNT1 A 0 A_JumpIfInventory("ScopeZoom", 1, "ZoomReady")
			EE11 A 1 A_WeaponReady
			Wait
		DryFire:
			EE11 A 18 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
			EE11 A 0 A_Refire
			goto Ready
		Fire:
			EE11 A 0 A_JumpIfNoAmmo("DryFire")
			EE11 A 0 A_JumpIfInventory("ScopeZoom", 1, "FireZoom")
			EE11 A 0 A_PlaySound ("Max/GE-11N")
			EE11 B 0 bright A_FireCustomMissile("GoldShot", frandom(-1.25,1.25), CallACS("SUPERCHARGE_CHECK"), 6, 0, 0,frandom(-1.25,1.25))
			EE11 B 2 bright A_GunFlash("Flash")
			EE11 A 2
			EE11 A 2 A_ReFire
			Goto Ready
		Altfire:
			TNT1 A 0 A_JumpifInventory ("ScopeZoom", 1, "ZoomOut")
			EESC A 1 A_PlaySound("Max/Zoom", CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("ScopeZoom", 1)
			EESC A 8 A_ZoomFactor(3)
			Goto Ready
		Flash:
			TNT1 A 0 Bright A_Light1
			TNT1 A 1 Bright A_SetPitch(pitch-0.25)
			TNT1 A 0 Bright A_Light0
			TTN1 A 1 Bright A_SetPitch(pitch+0.25)
			Goto LightDone
		ZoomReady:
			EESC A 1 A_WeaponReady (WRF_NOBOB|WRF_NOSWITCH|WRF_ALLOWZOOM)
			Loop
		FireZoom:
			TNT1 A 0 A_JumpIfInventory("DF_EnergyUnit", 8, "FireZoomDoFire")
			Goto ZoomOut
		FireZoomDoFire:
			EESC A 3
			TNT1 A 0 A_PlaySound ("Max/GE-11Z", CHAN_WEAPON)
			TNT1 A 0 A_FireCustomMissile("GoldShotZoom", 0, CallACS("SUPERCHARGE_CHECK"), 0, 4, 0)
			TNT1 A 0 A_TakeInventory("DF_EnergyUnit", 6)
			EESC A 10 A_GunFlash
			TNT1 A 0 A_ReFire
			Goto Ready
		ZoomOut:
			TNT1 A 0 A_TakeInventory("ScopeZoom", 1)
			TNT1 A 0 A_PlaySound("Max/Zoom", CHAN_WEAPON)
			EESC A 8 A_ZoomFactor(1)
			EE11 A 1
			Goto Ready
	}
}

ACTOR FistPuff
{
	RenderStyle add
	Alpha 0.5
	Speed 0
	Mass 10
	Attacksound "Max/fisthit"
	Seesound "Max/fisthit"
	Mass 5

	+PUFFONACTORS
	+NOEXTREMEDEATH
	+NOBLOCKMAP
	+NOGRAVITY
	+RANDOMIZE
	-ALLOWPARTICLES

	States
	{
		Spawn:
			TNT1 AAAA 4
			Stop
	}
}

actor DFFist : SH_Weapon
{
	Tag "Rebel's Fist"

	weapon.selectionorder 110
	weapon.Kickback 200
	
	Inventory.RestrictedTo "MaximusPrime"

	Obituary "o% got knocked the fuck out by %k"
	weapon.BobStyle "InverseSmooth"
	Weapon.SlotNumber 1
	weapon.BobRangeX 1
	weapon.BobRangeY 1
	weapon.bobspeed 1
	
	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset 24
	
	+WEAPON.NOALERT
	+WEAPON.CHEATNOTWEAPON
	States
	{
		Ready:
			FIST A 1 A_WeaponReady
			loop
		Deselect:
			FIST A 1 A_Lower
			TNT1 AA 0 A_Lower
			loop
		Select:
			FIST A 1 A_Raise
			TNT1 AA 0 A_Raise
			loop
		Fire:
			FIST B 4
			TNT1 B 0 A_PlaySound ("Max/fist")
			TNT1 B 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
			FIST C 0 A_FireBullets(-15.0, 0.0, -1, 6 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 0 A_FireBullets(0.0, 0.0, -1, 6 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 0 A_FireBullets(15.0, 0.0, -1, 6 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 6
			Goto FireEnd
		Berserked:
			FIST C 0 A_FireBullets(-15.0, 0.0, -1, 18 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 0 A_FireBullets(0.0, 0.0, -1, 18 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 0 A_FireBullets(15.0, 0.0, -1, 18 * DARKFORCES_DMG_MULT, "FistPuff", FBF_EXPLICITANGLE | FBF_NORANDOM, 64.0)
			FIST C 6
		FireEnd:
			FIST D 4
			FIST A 5
			FIST A 0 A_ReFire
			goto Ready
	}
}

actor SaberPuff : FistPuff
{
	Seesound "Max/lshit"
	Attacksound "Max/lshitwall"
	Deathsound "Max/lshit"
	-NOEXTREMEDEATH
	+ALLOWPARTICLES
	+BLOODLESSIMPACT
	States
	{
		Spawn:
			PUFF ABCD 1 BRIGHT
			Stop
	}
}

actor SaberPuff_SweetSpot : SaberPuff
{
	+FORCEPAIN
}

const int SABER_DMG = 40;

actor Lightsaber : SH_Weapon
{
	weapon.selectionorder 1000
	weapon.kickback 250
	Inventory.RestrictedTo "MaximusPrime"
	Obituary "o% met %k's lightsaber"
	weapon.BobStyle "InverseSmooth"
	Inventory.PickupSound "Max/HealthBonus"
	Weapon.UpSound "Max/LSSelect"
	Weapon.SlotNumber 1
	weapon.BobRangeX 0.5
	weapon.BobRangeY 0.5
	weapon.bobspeed 1.0
	
	Weapon.ViewPitchStyle "Centered"
	Weapon.ViewPitchOffset -8
	
	States
	{
		Spawn:
			LGSB R -1
			loop
		Select:
			SABR AABB 1 Bright A_Raise
			Loop
		Deselect:
			SABR A 0 A_PlaySound("Max/LSDeselect", CHAN_WEAPON)
			goto DeselectLoop
		DeselectLoop:
			SABR A 0 A_StopSound(CHAN_7)
			SABR AABB 1 Bright A_Lower
			Loop
		Ready:
			SABR A 0 A_Playsound("Max/LSHum", CHAN_7, 1.0, true)
			goto ReadyLoop // Only want to call the looping sound once due to randomization.
		ReadyLoop:
			SABR AAAABBBB 1 Bright A_WeaponReady
			Loop
		Fire:
			//SABR A -1 Offset(0, 32) A_Jump(256, "SwingLeftReverse") // Used for testing
			SABR A -1 Offset(0, 32) A_Jump(256, "SwingLeft", "SwingLeftDown", "SwingLeftSweep", "SwingLeftReverse", "SwingRight", "SwingRightDown", "SwingRightSweep")
			goto Ready
		SwingLeft:
			SABA A 2 Bright
			SABA B 2 Bright A_Playsound ("Max/lsattack", CHAN_WEAPON)
			SABA C 2 Bright A_FireBullets(-16.0, 0.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA D 1 Bright A_FireBullets(0.0, 2.0, -1, SABER_DMG, "SaberPuff_SweetSpot", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA E 1 Bright A_FireBullets(8.0, 4.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA F 1 Bright A_FireBullets(16.0, 6.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA G 1 Bright A_FireBullets(32.0, 8.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA H 1 Bright A_FireBullets(40.0, 10.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			TNT1 A 7 Bright
			Goto Recover
		SwingLeftDown:
			SABA A 2 Bright
			SABA B 2 Bright A_Playsound ("Max/lsattack", CHAN_WEAPON)
			SABA C 1 Bright A_FireBullets(-8.0, 0.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA C 1 Bright Offset(-16, 48)
			SABA D 1 Bright Offset(16, 32) A_FireBullets(0.0, 2.0, -1, SABER_DMG, "SaberPuff_SweetSpot", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA E 1 Bright Offset(5, 62) A_FireBullets(8.0, 8.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA F 1 Bright Offset(6, 72) A_FireBullets(16.0, 16.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA G 1 Bright Offset(7, 82) A_FireBullets(32.0, 22.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA H 1 Bright Offset(8, 92) A_FireBullets(40.0, 26.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			TNT1 A 7 Bright
			Goto Recover
		SwingLeftSweep:
			SABA B 1 Bright Offset(16, 64)
			SABA B 1 Bright Offset(32, 72)
			SABA B 1 Bright Offset(48, 80)
			SABA B 1 Bright Offset(64, 96)
			SABA C 1 Bright Offset(80, 96) A_Playsound ("Max/lsattack", CHAN_WEAPON)
			SABA C 1 Bright Offset(32, 104)
			SABA C 1 Bright Offset(1, 128)
			SABA C 1 Bright Offset(-40, 160) A_FireBullets(-24.0, 0.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA D 1 Bright Offset(64, 32) A_FireBullets(-16.0, 0.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA D 1 Bright Offset(1, 32) A_FireBullets(0.0, 0.0, -1, SABER_DMG, "SaberPuff_SweetSpot", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA E 1 Bright Offset(-1, 30) A_FireBullets(16.0, 1.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA E 1 Bright Offset(-64, 36) A_FireBullets(32.0, 2.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA E 1 Bright Offset(-128, 44) A_FireBullets(48.0, 3.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA E 1 Bright Offset(-168, 56) A_FireBullets(56.0, 4.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			TNT1 A 7 Bright
			Goto Recover
		SwingLeftReverse:
			SABA B 1 Bright Offset(-16, 64)
			SABA B 1 Bright Offset(-24, 72)
			SABA B 1 Bright Offset(-32, 80) A_Playsound ("Max/lsattack", CHAN_WEAPON)
			SABA B 1 Bright Offset(-64, 96)
			SABA C 1 Bright Offset(-48, 128)
			SABA C 1 Bright Offset(-50, 132)
			SABA D 1 Bright Offset(-1, 48) A_FireBullets(0.0, 2.0, -1, SABER_DMG, "SaberPuff_SweetSpot", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			TNT1 A 4 Bright
			TNT1 A 0 Bright A_Playsound ("Max/lsattack", CHAN_WEAPON)
			SABA H 1 Bright Offset(8, 92) A_FireBullets(40.0, 26.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA G 1 Bright Offset(7, 82) A_FireBullets(32.0, 22.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA F 1 Bright Offset(6, 72) A_FireBullets(16.0, 16.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA E 1 Bright Offset(5, 62) A_FireBullets(8.0, 8.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA D 1 Bright Offset(16, 32) A_FireBullets(0.0, 2.0, -1, SABER_DMG, "SaberPuff_SweetSpot", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA C 1 Bright A_FireBullets(-8.0, 0.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA B 1 Bright Offset(16, 48)
			SABA B 1 Bright Offset(32, 64)
			SABF E 0 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
			SABF EDCBA 1 Bright Offset(0, 32)
			goto Ready
		SwingRight:
			SABA I 2 Bright
			SABA J 2 Bright A_Playsound ("Max/lsattack", CHAN_WEAPON)
			SABA K 2 Bright A_FireBullets(16.0, 0.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA L 1 Bright A_FireBullets(0.0, 2.0, -1, SABER_DMG, "SaberPuff_SweetSpot", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA M 1 Bright A_FireBullets(-8.0, 4.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA N 1 Bright A_FireBullets(-16.0, 6.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA O 1 Bright A_FireBullets(-32.0, 8.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA P 1 Bright A_FireBullets(-40.0, 10.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			TNT1 A 7 Bright
			Goto Recover
		SwingRightDown:
			SABA I 2 Bright
			SABA J 2 Bright A_Playsound ("Max/lsattack", CHAN_WEAPON)
			SABA K 1 Bright A_FireBullets(8.0, 0.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA K 1 Bright Offset(16, 48)
			SABA L 1 Bright Offset(-16, 32) A_FireBullets(0.0, 2.0, -1, SABER_DMG, "SaberPuff_SweetSpot", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA M 1 Bright Offset(-5, 62) A_FireBullets(-8.0, 8.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA N 1 Bright Offset(-6, 72) A_FireBullets(-16.0, 16.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA O 1 Bright Offset(-7, 82) A_FireBullets(-32.0, 22.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA P 1 Bright Offset(-8, 92) A_FireBullets(-40.0, 26.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			TNT1 A 7 Bright
			Goto Recover
		SwingRightSweep:
			SABA J 1 Bright Offset(-16, 64)
			SABA J 1 Bright Offset(-32, 72)
			SABA J 1 Bright Offset(-48, 80)
			SABA J 1 Bright Offset(-64, 96)
			SABA K 1 Bright Offset(-80, 96) A_Playsound ("Max/lsattack", CHAN_WEAPON)
			SABA K 1 Bright Offset(-32, 104)
			SABA K 1 Bright Offset(-1, 128)
			SABA K 1 Bright Offset(40, 160) A_FireBullets(24.0, 0.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA L 1 Bright Offset(-64, 32) A_FireBullets(16.0, 0.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA L 1 Bright Offset(-1, 32) A_FireBullets(0.0, 0.0, -1, SABER_DMG, "SaberPuff_SweetSpot", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA M 1 Bright Offset(1, 30) A_FireBullets(-16.0, 1.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA M 1 Bright Offset(64, 36) A_FireBullets(-32.0, 2.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA M 1 Bright Offset(128, 44) A_FireBullets(-48.0, 3.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			SABA M 1 Bright Offset(168, 56) A_FireBullets(-56.0, 4.0, -1, SABER_DMG, "SaberPuff", FBF_EXPLICITANGLE | FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80.0)
			TNT1 A 7 Bright
			Goto Recover
		
		Recover:
			SABF E 0 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH) // Used to reset offsets
			SABF EDCBA 1 Bright A_Refire
			goto Ready
	}
}

actor PowerSupercharge : PowerDoubleFiringSpeed { }
actor DF_SuperCharge : PowerupGiver
{
	Game Doom
	Game Heretic
	Game Hexen
	Scale 0.6
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	Powerup.Type Supercharge
	Inventory.PickupMessage "Weapon Super Charge"
	powerup.duration -45
	States
	{
	Spawn:
		WEPS A -1
		stop
	}
}