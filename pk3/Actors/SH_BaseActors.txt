// This file defines base actor classes to inherit from for the mod at large.

// Not meant to do anything on their own, they are for keeping behavior consistent
// among groups of actors that are meant to behave similarly, and to reduce the amount
// of duplicated code.

// Code by StrikerTheHedgefox.

//////////////////////////////////////////////////////////////////
// Particles
//////////////////////////////////////////////////////////////////
actor SH_BaseParticle
{
	+NEVERADJUSTSPRITECLIPPING
	+ALLOWCLIENTSPAWN
	+NOSPAWNEVENTSCRIPT
	+NODAMAGEEVENTSCRIPT
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+NOCLIP
	+NOGRAVITY
	+NOBLOCKMAP
	+DROPOFF
	+NOTELEPORT
	+NOTIMEFREEZE
	+DONTSPLASH
	+NOTRIGGER
	+THRUACTORS
}

//////////////////////////////////////////////////////////////////
// Projectiles
//////////////////////////////////////////////////////////////////

actor SH_BasePlayerProjectile
{
	Obituary "%o was killed by a player projectile without an obituary. FIX THIS MOTHERFUCKER!"
	ThruBits (THRU_SHIELDS)
	Projectile
	+NEVERADJUSTSPRITECLIPPING
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+RANDOMIZE
	+ALLOWTHRUFLAGS
	+ALLOWTHRUBITS
}

actor SH_BasePlayerFastProjectile : FastProjectile
{
	Obituary "%o was killed by a player projectile without an obituary. FIX THIS MOTHERFUCKER!"
	ThruBits (THRU_SHIELDS)
	+NEVERADJUSTSPRITECLIPPING
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+RANDOMIZE
	+ALLOWTHRUFLAGS
	+ALLOWTHRUBITS
}

actor SH_BaseEnemyProjectile
{
	Obituary "%o was killed by an enemy projectile without an obituary. FIX THIS MOTHERFUCKER!"
	Projectile
	+NEVERADJUSTSPRITECLIPPING
	+FORCEXYBILLBOARD
	+DONTSEEKINVISIBLE
	+RANDOMIZE
}

actor SH_BaseEnemyFastProjectile : FastProjectile
{
	Obituary "%o was killed by an enemy projectile without an obituary. FIX THIS MOTHERFUCKER!"
	+NEVERADJUSTSPRITECLIPPING
	+FORCEXYBILLBOARD
	+DONTSEEKINVISIBLE
	+RANDOMIZE
}

//////////////////////////////////////////////////////////////////
// Player Classes
//////////////////////////////////////////////////////////////////

actor SH_BasePlayer : DoomPlayer
{
	Species "Player"
	DamageFactor "Marines", 0.0 // Keep! Used for some things you don't want to have team damage for.
	DamageFactor "PlayerDynamite", 0.25
	DamageFactor "PlayerSucc", 0.25
	DamageFactor "PlayerExplosivePuff", 0.0
	
	Player.DamageScreenColor "FF 00 00", 1.0
	Player.DamageScreenColor "00 FF 00", 1.0, "Acid"
	Player.DamageScreenColor "FF 64 00", 1.0, "Fire"
	Player.DamageScreenColor "00 88 FF", 1.0, "Ice"
	Player.DamageScreenColor "FF FF 00", 1.0, "Electric"
	Player.DamageScreenColor "88 00 FF", 1.0, "Void"

	ThruBits (THRU_PLAYERS) // Allows actors with this bit to move through a player.
	-ALLOWTHRUBITS // Don't want players to move through one another unless the sv_unblockplayers/allies flags are on.

	var int user_supercharge_useammo;
	var int user_recoil_x;
	var int user_recoil_y;
}

//////////////////////////////////////////////////////////////////
// Monsters
//////////////////////////////////////////////////////////////////
actor SH_BaseMonster
{
	//$Category Monsters
	
	MONSTER
	
	var int user_extralife_chance;
	var int user_manncobox_chance;
	var int user_hpbar_offset;
}

actor SH_BaseMonsterRandomSpawner : RandomSpawner
{
	+SOLID
	+ISMONSTER
	+USESPAWNEVENTSCRIPT
	-NOBLOCKMAP
	-NOSECTOR
	-THRUACTORS
	States
	{
		Spawn:
			TNT1 A 2 Bright
			TNT1 A 0 A_ChangeFlag("SOLID", 0)
			//TNT1 A 0 A_ChangeFlag("NOBLOCKMAP", 1)
			//TNT1 A 0 A_ChangeFlag("NOSECTOR", 1)
			TNT1 A -1
			stop
	}
}

//////////////////////////////////////////////////////////////////
// Portals
//////////////////////////////////////////////////////////////////
actor PortalUnit_Base
{
	Scale 0.5
	Radius 8
	Height 8
	Speed 10
	Damage 0
	Gravity 1
	Projectile
	+DONTGIB
	+THRUACTORS
	+NOTIMEFREEZE
	-NOGRAVITY
}

actor PortalPayout_Base
{
	Speed 0
	Radius 1
	Height 1
	Scale 0.5
	var int user_timer;
	+THRUACTORS
	+NOBLOOD
	+INVULNERABLE
	+DONTGIB
	+NOTIMEFREEZE
	+NOCLIP
}

//////////////////////////////////////////////////////////////////
// Misc Tokens & Flags
//////////////////////////////////////////////////////////////////
actor SH_BaseToken : Inventory
{
	Radius 12
	Height 56
	Inventory.Amount 1
	Inventory.MaxAmount 1
	+INVENTORY.UNDROPPABLE
	+INVENTORY.UNTOSSABLE
	+INVENTORY.HUBPOWER
	+INVENTORY.PERSISTENTPOWER
	-INVENTORY.INTERHUBSTRIP
	+DONTGIB
	States
	{
		Spawn:
			SOUL A -1
			Stop
	}
}

//////////////////////////////////////////////////////////////////
// Upgrades & Powerups
//////////////////////////////////////////////////////////////////
actor UpgradeToken_Base : SH_BaseToken
{
	scale 0.75
	Inventory.PickupSound "Mannco/PLToken"
	+SLIDESONWALLS
	+CANBLAST
	+NOBLOCKMONST
	+DROPOFF
}

//////////////////////////////////////////////////////////////////
// Ammo & Weapons
//////////////////////////////////////////////////////////////////

actor SH_Weapon : Weapon
{
	+INVENTORY.UNDROPPABLE
}

actor SH_FakeInventory : FakeInventory
{
	Activation THINGSPEC_ThingActs | THINGSPEC_ThingTargets | THINGSPEC_TriggerTargets
	FakeInventory.Respawns
	+DONTGIB

	// Hack to bypass the fact that Zan can't use A_SetScale from the client side on non-clientsided actors.
	var int user_scale_Base;
	var int user_scale_Fiend;
	var int user_scale_Kunai;
	var int user_scale_Gosimer;
	var int user_scale_MaximusPrime;
	
	// Bitfields to store who picked up an instanced item.
	// Set by OR'ing 1 << playerNum into pickedupby1 for players 0 through 31,
	// then OR'ing 1 << (playerNum-32) into pickedupby2 for players 32 through 63.
	// Currently unused, but I plan to use this for instanced health pickups.
	var int user_pickedupby1;
	var int user_pickedupby2;
}

actor AmmoGiver_Base : SH_FakeInventory
{
	Radius 20
	Height 16
	+NOTIMEFREEZE
	+INVENTORY.QUIET
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetSpecial(84, 13236, 0, false) // Set special to AMMO_PICKUP ACS Script (13236)
			TNT1 A 0 Thing_ChangeTID(0, 69069) // Tag this as PICKUP_SPRITE_TAG (69069) so it can be manipulated by ACS.
			TNT1 A 0 A_SetUserVar("user_scale_Base", 100)
			TNT1 A 35 A_Jump(256, "Init")
			wait

		Init:
			TNT1 A 0 // A_SetArg(1, AMMO_CLIP) // Ammo Type
			goto CheckSprite
			
		Base:
			UNKN A -1
			loop
			
		Idle:
			"####" "#" -1
			stop

		RecheckSprite:
			"####" "#" 1
			goto CheckSprite
		CheckSprite:
			"####" "#" -1 A_ClientsideACSExecute("SET_PICKUP_SPRITE")
			loop
	}
}

actor WeaponGiver_Base : SH_FakeInventory
{
	Radius 20
	Height 16
	+NOTIMEFREEZE
	+INVENTORY.QUIET
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetSpecial(84, 13237, 0, false) // Set special to WEAPON_PICKUP ACS Script (13237)
			TNT1 A 0 Thing_ChangeTID(0, 69069) // Tag this as PICKUP_SPRITE_TAG (69069) so it can be manipulated by ACS.
			TNT1 A 0 A_SetUserVar("user_scale_Base", 100)
			TNT1 A 35 A_Jump(256, "Init") // Force jump to the init state of descendant actors.
			wait

		Init:
			TNT1 A 0 // Example: A_SetArg(1, WEAPON_CHAINSAW) // Ammo Type
			goto CheckSprite
			
		Base:
			UNKN A -1
			loop
			
		Idle:
			"####" "#" -1
			stop

		RecheckSprite:
			"####" "#" 1
			goto CheckSprite
		CheckSprite:
			"####" "#" -1 A_ClientsideACSExecute("SET_PICKUP_SPRITE")
			loop
	}
}

