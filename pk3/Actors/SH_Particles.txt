// This is for various particle effects.
// Code by StrikeTheHedgefox

ACTOR TinyAcid : SH_BaseParticle
{
	scale 0.2
	radius 1
	height 1
	Translation "0:255=%[0.00,0.13,0.00]:[0.25,2.00,0.63]"
	Gravity 0.7
	+MISSILE
	+FORCEYBILLBOARD
	+DONTSPLASH
	+CLIENTSIDEONLY
	-NOCLIP
	-NOGRAVITY
	-EXPLODEONWATER
	-NOINTERACTION
	States
	{
		Spawn:
			BLUD Z 0 NoDelay A_SpawnItemEx("LongSmoke", 0, 0, 4, 0, 0, 0, 0, SXF_TRANSFERSCALE | SXF_CLIENTSIDE, 128)
			BLUD Z 500
			Stop
		Death:
			XDT1 EFGHIJKL 2
			stop
	}
}

actor AcidSplash : SH_BaseParticle
{
	RenderStyle Translucent
	Alpha 0.75
	+MOVEWITHSECTOR
	States
	{
		Spawn:
			ASPL E 0 NoDelay A_SetScale(frandom(0.5, 1.0))
			ASPL E 0 A_Jump(128, "Animate")
			ASPL E 0 A_SetScale(scalex*-1, scaley)
			goto Animate
		Animate:
			ASPL E 0 A_SpawnItemEx("LongSmoke", 0, 0, 4, 0, 0, 0, 0, SXF_TRANSFERSCALE | SXF_CLIENTSIDE, 128)
			ASPL EFGHIJK 3
			stop
	}
}

actor LongSmoke : SH_BaseParticle
{
	RenderStyle Translucent
	Alpha 0.4
	+FORCEYBILLBOARD
	States
	{
		Spawn:
			LSMK A 0 NoDelay A_SetScale(scalex*0.5)
			LSMK A 0 A_ChangeVelocity(0, 0, frandom(0.5*scaley, 1.0*scaley), CVF_REPLACE)
			LSMK ABCDEFHIJ 4
			stop
	}
}

ACTOR CryoSmoke : SH_BaseParticle
{
	Radius 2
	Height 2
	RenderStyle Shaded
	StencilColor "A0 A0 A0"
	Alpha 1.0
	Scale 0.4
	+NOTIMEFREEZE
	+CLIENTSIDEONLY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			SMOK A 2 NoDelay A_FadeOut (0.05)
			loop
	}
}

Actor CryoSmoke2 : CryoSmoke
{
	StencilColor "DD DD DD"
	Scale 0.7
	Alpha 0.5
	VSpeed 2.5
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	+NOINTERACTION
	States
	{
		Spawn:
			SMOK A 6 NoDelay A_FadeOut(0.04)
			loop
	}
}

ACTOR CryoSmoke3 : SH_BaseParticle
{
	Radius 2
	Height 2
	RenderStyle Shaded
	StencilColor "A0 A0 A0"
	Alpha 0.2
	Scale 0.05
	+NOTIMEFREEZE
	+CLIENTSIDEONLY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			IMST A 0
			IMST A 2 A_FadeOut (0.05)
			Wait
	}
}

Actor CryoSmoke4 : CryoSmoke3
{
	StencilColor "DD DD DD"
	Scale 0.1
	Alpha 0.25
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0
			IMST A 6 A_FadeOut(0.04)
			Wait
	}
}

Actor BigRocketSmoke : CryoSmoke3
{
	StencilColor "DD DD DD"
	Scale 0.1
	Alpha 0.5
	+CLIENTSIDEONLY
	+NOTIMEFREEZE
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetScale(frandom(0.05, 0.1))
			goto SpawnLoop
		SpawnLoop:
			IMST A 3 A_FadeOut(0.04)
			loop
	}
}

actor AcidBurst : SH_BaseParticle
{
	RenderStyle Translucent
	Alpha 0.75
	States
	{
		Spawn:
			SBS3 D 0 NoDelay A_SetScale(frandom(0.3, 0.5))
			SBS3 D 0 A_PlaySound("AcidBurst", CHAN_AUTO, 1.0, false, 0.5)
			SBS3 DEFGH 4
			stop
	}
}

actor AcidPuddle : SH_BaseParticle
{
	Translation "0:255=%[0.00,0.13,0.00]:[0.25,2.00,0.63]"
	RenderStyle Translucent
	Alpha 0.75
	Scale 0.1
	+MOVEWITHSECTOR
	+DONTSPLASH
	-FLOORCLIP
	-CLIENTSIDEONLY
	-ALLOWCLIENTSPAWN
	var int user_maxscale;
	states
	{
		Spawn:
			BPDL A 1 A_SetScale(scalex+0.05)
			BPDL A 0 A_JumpIf(scalex >= (user_maxscale/65536.0), "Idle")
			loop
		Idle:
			BPDL A 350
			goto Fade
		Fade:
			BPDL A 1 A_Fadeout(0.0125)
			loop
	}
}

actor RedShockwave : SH_BaseParticle
{
	RenderStyle Add
	Alpha 1.0
	States
	{
		Spawn:
			NULL A 0 NoDelay A_SetPitch(frandom(-22.5, 22.5))
			NULL ABCDEFGH 1 Bright A_Fadeout(0.1)
			stop
	}
}

actor GreenShockwave : SH_BaseParticle
{
	RenderStyle Add
	Alpha 1.0
	States
	{
		Spawn:
			NULL A 0 NoDelay A_SetPitch(frandom(-22.5, 22.5))
			NULL ABCDEFGH 1 Bright A_Fadeout(0.1)
			stop
	}
}

actor Shrapnel1
{
	Radius 2
	Damage 4
	mass 5
	BounceFactor 0.5
	SeeSound "darkinquisitor/shrapnel"
	Speed 25
	PROJECTILE
	+DOOMBOUNCE   
	+NOBLOCKMAP
	+CANNOTPUSH
	+GRENADETRAIL
	-NOGRAVITY
	-NOTELEPORT
	-SOLID
	States
	{
	Spawn:
		SHRP L 0
		SHRP L 0 A_Jump(64,"Alt")
		SHRP L 0 ThrustThingZ (0, (Random (20, 100)), 0, 1)
		goto See
	Alt:
		SHRP M 0 ThrustThingZ (0, (Random (40, 130)), 0, 1)
		Stop
	See:
		SHRP L 5
		SHRP L 5 ThrustThingZ(0,(Random (6, 20)),1,1)
		SHRP L 5
		SHRP L 5
		loop
	Death:
		SHRP L 0
		SHRP L 0
		SHRP L 0
		SHRP L 70
		Stop
	}
}

actor Shrapnel2
{
	Radius 2
	Damage 4
	mass 5
	BounceFactor 0.4
	SeeSound "darkinquisitor/shrapnel"
	Speed 20
	PROJECTILE
	+DOOMBOUNCE   
	+NOBLOCKMAP
	+CANNOTPUSH
	+GRENADETRAIL
	-NOGRAVITY
	-NOTELEPORT
	-SOLID
	States
	{
	Spawn:
		SHRP M 0
		SHRP M 0 A_Jump(64,"Alt")
		SHRP M 0 ThrustThingZ (0, (Random (20, 100)), 0, 1)
		goto See
	Alt:
		SHRP N 0 ThrustThingZ (0, (Random (40, 130)), 0, 1)
		Stop
	See:
		SHRP M 5
		SHRP M 5 ThrustThingZ(0,(Random (6, 20)),1,1)
		SHRP M 5
		SHRP M 5
		loop
	Death:
		SHRP M 0
		SHRP M 0
		SHRP M 0
		SHRP M 70
		Stop
	}
}

actor Shrapnel3
{
	Radius 2
	Damage 4
	mass 5
	BounceFactor 0.6
	SeeSound "darkinquisitor/shrapnel"
	Speed 23
	PROJECTILE
	+DOOMBOUNCE   
	+NOBLOCKMAP
	+CANNOTPUSH
	+GRENADETRAIL
	-NOGRAVITY
	-NOTELEPORT
	-SOLID
	States
	{
		Spawn:
			SHRP N 0
			SHRP N 0 A_Jump(64,"Alt")
			SHRP N 0 ThrustThingZ (0, (Random (20, 100)), 0, 1)
			goto See
		Alt:
			SHRP L 0 ThrustThingZ (0, (Random (40, 130)), 0, 1)
			Stop
		See:
			SHRP N 5
			SHRP N 5 ThrustThingZ(0,(Random (6, 20)),1,1)
			SHRP N 5
			SHRP N 5
			loop
		Death:
			SHRP N 0
			SHRP N 0
			SHRP N 0
			SHRP N 70
			Stop
	}
}

actor GasterLaser : SH_BaseParticle
{
	RenderStyle Add
	Scale 0.3
	States
	{
		Spawn:
			PART A 0 Bright A_JumpIf(scalex > 3.0, "Spawn2")
			PART A 1 Bright A_SetScale(scalex+1.0, scaley+1.0)
			Loop

		Spawn2:
			PART A 1 Bright A_SetScale(scalex-1.0, scaley-1.0)
			PART A 1 Bright A_SetScale(scalex+1.0, scaley+1.0)
			PART A 1 Bright A_SetScale(scalex-1.0, scaley-1.0)
			PART A 1 Bright A_SetScale(scalex+1.0, scaley+1.0)
			PART A 1 Bright A_SetScale(scalex-1.0, scaley-1.0)
			PART A 1 Bright A_SetScale(scalex+1.0, scaley+1.0)
			PART A 1 Bright A_SetScale(scalex-1.0, scaley-1.0)
			PART A 1 Bright A_SetScale(scalex+1.0, scaley+1.0)
			PART A 1 Bright A_SetScale(scalex-1.0, scaley-1.0)
			PART A 1 Bright A_SetScale(scalex+1.0, scaley+1.0)

		Spawn3:
			PART A 0 Bright A_JumpIf(scalex < 0.1, "End")
			PART A 1 Bright A_SetScale(scalex-1.0, scaley-1.0)
			Loop

		End:
			TNT1 A 0
			stop
	}
}

actor GasterLaserSmall : GasterLaser
{
	Scale 0.15
	States
	{
		Spawn:
			PART A 0 Bright A_JumpIf(scalex > 1.5, "Spawn2")
			PART A 1 Bright A_SetScale(scalex+0.5, scaley+0.5)
			Loop
	}
}