actor MayhemPlasmaMarine
{
	Tag "FAAAABULOUS Marine"
	Monster
	Radius 20
	Height 56
	Health 100
	Speed 12
	ReactionTime 8
	PainChance 170
	Mass 100
	SeeSound "grunt/sight"
	PainSound "*pain50"
	DeathSound "*death"
	ActiveSound "grunt/active"
	DropItem "Cell"
	States
	{
		Spawn:
			PLAY AB 10 A_Look
			loop
		See:
			PLAY AABBCCDD 3 A_Chase
			loop
		Missile:
			PLAY E 5 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			goto See
		Pain:
			PLAY G 3
			PLAY G 3 A_Pain
			goto See
		Death:
			PLAY H 5
			PLAY I 5 A_Scream
			PLAY J 5 A_Fall
			PLAY KLM 5
			PLAY N -1
			stop
		XDeath:
			PLAY O 5
			PLAY P 5 A_XScream
			PLAY Q 5 A_Fall
			PLAY RSTUV 5
			PLAY W -1
			stop
		Raise:
			PLAY NMLKJIH 5
			goto SEE
	}
}

actor MayhemGrell
{
	Tag "Grell"
	Monster
	Radius 24
	Height 56
	Health 300
	Speed 10
	ReactionTime 8
	PainChance 128
	Mass 400
	SeeSound "DSBRSSIT"
	PainSound "DSDMPAIN"
	DeathSound "DSBRSDTH"
	ActiveSound "DSDMACT"
	+FLOAT
	+NOGRAVITY
	States
	{
		Spawn:
			BOSS A 10 A_Look
			loop
		See:
			BOSS AABBCC 3 A_Chase
			loop
		Missile:
			BOSS AD 4 A_FaceTarget
			BOSS E 4 A_BruisAttack
			goto See
		Pain:
			BOSS F 3
			BOSS F 3 A_Pain
			goto See
		Death:
			BOSS G 5 A_Scream
			BOSS H 4 A_Fall
			BOSS IJKL 4
			BOSS M -1
			stop
		Raise:
			BOSS MLKJIHG 4
			goto See
	}
}

actor MayhemImmolator
{
	Tag "Immolator"
	Monster
	Health 2500
	Speed 12
	Radius 40
	Height 80
	ReactionTime 8
	PainChance 20
	Mass 1000
	SeeSound "DSSPISIT"
	PainSound "DSDMPAIN"
	DeathSound "DSSPIDTH"
	ActiveSound "DSDMACT"
	+NOGRAVITY
	+FLOAT
	States
	{
		Spawn:
			SSWV AB 10 A_Look
			loop
		See:
			SSWV AB 3 A_Chase
			loop
		Missile:
			SSWV C 8 A_FaceTarget
			SSWV D 4 A_FatAttack1
			SSWV D 4 A_FatAttack2
			SSWV D 4 A_FatAttack3
			goto See
		Pain:
			SSWV E 5 A_Pain
			SSWV E 5
			SSWV C 8 A_FaceTarget
			SSWV DDD 3 A_SkelMissile
			goto See
		Death:
			SSWV F 8 A_Scream
			SSWV G 8
			SSWV H 8 A_Fall
			SSWV IJKL 8
			stop
	}
}

actor Mayhem19ShotgunCultist : SH_BaseMonster
{
	Tag "Shotgun Cultist"
	Species "Cultist"
	Health 50
	Radius 20
	Height 56
	Mass 100
	Speed 9
	PainChance 150
	SeeSound "shotguy/sight"
	AttackSound "shotguy/attack"
	PainSound "shotguy/pain"
	DeathSound "shotguy/death"
	ActiveSound "shotguy/active"
	Obituary "$OB_SHOTGUY"
	DropItem "Shotgun"
	+FLOORCLIP
	States
	{
		Spawn:
			SPOS AB 10 A_Look
			Loop
		See:
			SPOS AABBCCDD 3 A_Chase
			Loop
		Missile:
			SPOS E 6 A_FaceTarget
			SPOS F 6 Bright A_SPosAttack
			SPOS E 6
			Goto See
		Pain:
			SPOS G 3
			SPOS G 3 A_Pain
			Goto See
		Death:
			SPOS H 5
			SPOS I 5 A_Scream
			SPOS J 5 A_NoBlocking
			SPOS KLM 5
			SPOS N -1
			Stop
		XDeath:
			SPOS O 5
			SPOS P 5 A_XScream
			SPOS Q 5 A_Fall
			SPOS RSTUV 5
			SPOS W -1
			Stop
		Raise:
			SPOS MLKJIH 5
			Goto See
	}
}

actor Mayhem19EliteCultist : SH_BaseMonster
{
	Tag "Elite Cultist"
	Species "Cultist"
	Health 70
	Radius 20
	Height 56
	Mass 100
	Speed 11
	PainChance 170
	SeeSound "chainguy/sight"
	PainSound "chainguy/pain"
	DeathSound "chainguy/death"
	ActiveSound "chainguy/active"
	AttackSound "depisto"
	Obituary "$OB_CHAINGUY"
	Dropitem "ClipBox"
	+FLOORCLIP
	States
	{
		Spawn:
			CPOS AB 10 A_Look
			Loop
		See:
			CPOS AABBCCDD 3 A_Chase
			Loop
		Missile:
			CPOS E 10 A_FaceTarget
			CPOS F 3 Bright A_CPosAttack
			CPOS E 2
			CPOS F 3 Bright A_CPosAttack
			CPOS E 3
			CPOS E 0 A_Jump(100, "See")
			CPOS E 3 A_CPosRefire
			Goto Missile+1
		Pain:
			CPOS G 3
			CPOS G 3 A_Pain
			Goto See
		Death:
			CPOS H 5
			CPOS I 5 A_Scream
			CPOS J 5 A_NoBlocking
			CPOS K 5
			CPOS L -1
			Stop
		XDeath:
			CPOS M 5 
			CPOS N 5 A_XScream
			CPOS O 5 A_NoBlocking
			CPOS PQRST 5
			CPOS U -1
			Stop
		Raise:
			CPOS KJIH 5
			Goto See
	}
}

actor Mayhem19PistolCultist : Mayhem19EliteCultist
{
	Tag "Pistol Cultist"
	Health 50
	Speed 10
	Dropitem "Clip"
}

ACTOR Mayhem19VampireBat
{
	Radius 11
	Height 8
	Speed 10
	Damage (20)
	Projectile
	+SEEKERMISSILE
	+RANDOMIZE
	SeeSound "DSSKLATK"
	DeathSound "DSBAREXP"
	RenderStyle Add
	States
	{
		Spawn:
			FATB ABCB 2 Bright A_Tracer
			Loop
		Death:
			FBXP A 8 Bright
			FBXP B 6 Bright
			FBXP C 4 Bright
			Stop
	}
}

actor Mayhem19Vampire : SH_BaseMonster
{
	Tag "Vampire"
	Species "Vampire"
	Health 300
	Radius 20
	Height 56
	Mass 500
	Speed 8
	PainChance 100
	Monster
	MeleeThreshold 196
	BloodColor "Gold"
	SeeSound "DSPOSIT3"
	PainSound "DSPOPAIN"
	DeathSound "DSBGDTH2"
	ActiveSound "DSPOSACT"
	MeleeSound "skeleton/melee"
	HitObituary "$OB_UNDEADHIT" // "%o was punched by a revenant."
	Obituary "$OB_UNDEAD" // "%o couldn't evade a revenant's fireball."
	DropItem "HealthBonus"
	DropItem "HealthBonus"
	DropItem "HealthBonus"
	+MISSILEMORE 
	+FLOORCLIP
	States
	{
		Spawn:
			SKEL AB 10 A_Look
			Loop
		See:
			SKEL AABB 2 A_Chase
			Loop
		Melee:
			SKEL H 0 A_FaceTarget
			SKEL H 4 A_SkelWhoosh
			SKEL I 4 A_FaceTarget
			SKEL J 6 A_CustomMeleeAttack(50, "skeleton/melee", "", "Melee", true)
			Goto See
		Missile:
			SKEL C 6 A_FaceTarget
			SKEL F 6 Bright A_FaceTarget
			SKEL GGG 5 Bright A_CustomMissile("Mayhem19VampireBat", 32.0, 0.0, 0.0, 0) 
			SKEL C 12
			Goto See
		Pain:
			SKEL K 5
			SKEL K 5 A_Pain
			Goto See
		Death:
			SKEL LM 7
			SKEL N 7 A_Scream
			SKEL O 7 A_NoBlocking
			SKEL PQR 7
			SKEL S -1
			Stop
		Raise:
			SKEL PONML 5
			Goto See
	}
}

ACTOR Mayhem19Funnybone
{
	Radius 6
	Height 8
	Speed 10
	FastSpeed 20
	Damage (8)
	Projectile
	RenderStyle Normal
	Alpha 1
	SeeSound "DSSKESWG"
	DeathSound "DSPUNCH"
	+RANDOMIZE
	States
	{
		Spawn:
			BAL1 ABCD 4
			Loop
		Death:
			PUFF C 0 A_SetTranslucent(0.75, 0)
			PUFF CCCDDD 1 A_SetScale(scalex+0.25)
			Stop
	}
}

actor Mayhem19Skellie : SH_BaseMonster
{
	Tag "Skellie"
	Species "Skellie"
	Health 120
	PainChance 180
	Speed 14
	Radius 20
	Height 56
	Mass 400
	BloodColor "DarkGray"
	SeeSound "DSSKESIT"
	AttackSound "DSSKESWG"
	PainSound "DSPOPAIN"
	DeathSound "DSSKEDTH"
	ActiveSound "DSSKEACT"
	Obituary "%o took a funnyboner to the butt."
	+FLOORCLIP
	States
	{
		Spawn:
			SARG A 0 NoDelay A_SetUserVar("user_hpbar_offset", 24)
			goto SpawnLoop
		SpawnLoop:
			SARG AB 10 A_Look
			Loop
		See:
			SARG AABBCCDD 2 Fast A_Chase
			Loop
		Melee:
		Missile:
			SARG F 3 Fast A_FaceTarget
			SARG GH 2 Fast A_FaceTarget
			SARG I 8 Fast A_CustomComboAttack("Mayhem19Funnybone", 32, 3 * random(1, 8), "DSSKEPCH")
			Goto See
		Pain:
			SARG L 4 Fast
			SARG L 4 Fast A_Pain
			Goto See
		Death:
			SARG M 8
			SARG N 8 A_Scream
			SARG O 8
			SARG P 5 A_NoBlocking
			SARG Q -1
			Stop
		Raise:
			SARG PONM 8
			Goto See
	}
}

actor Mayhem19ShadowImp : Hardimp
{
	Tag "Shadow Imp"
	RenderStyle Fuzzy
	+SHADOW
}

ACTOR Mayhem19BallOfLightning : SH_BaseMonster
{
	Tag "Ball of Lightning"
	Species "Electricity"
	Health 40
	Radius 16
	Height 56
	Mass 50
	Speed 5
	Damage 3
	DamageType "Electric"
	PainChance 0
	ReactionTime 16
	SeeSound "DSBSPWLK"
	AttackSound "DSBSPWLK"
	PainSound "DSFIRXPL"
	DeathSound "DSBAREXP"
	ActiveSound ""
	RenderStyle Add
	Alpha 1.0
	Obituary "%o was electro-cocksuckered by a ball of lightning."
	+FLOAT
	+FLOATBOB
	+NOGRAVITY
	+MISSILEMORE
	+DONTFALL
	+NOICEDEATH
	+NOBLOOD
	+QUICKTORETALIATE
	+LOOKALLAROUND
	+THRUSPECIES
	States
	{
		Spawn:
			PLSS AB 8 Bright A_Look
			Loop
		See:
			PLSS AA 1 Bright A_Wander
			PLSS AA 1 Bright A_Chase
			PLSS AA 0 Bright A_SpawnItemEx("Particle_Lightning5", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(24.0, 48.0), 0, 0, 0, 0, SXF_CLIENTSIDE, 128)
			PLSS BB 1 Bright A_Wander
			PLSS BB 1 Bright A_Chase
			PLSS BB 0 Bright A_SpawnItemEx("Particle_Lightning5", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(24.0, 48.0), 0, 0, 0, 0, SXF_CLIENTSIDE, 128)
			Loop
		Melee:
			PLSS A 1 Bright A_Die
			wait
		Missile:
			PLSS A 6 Bright A_FaceTarget
			PLSS B 4 Bright A_SkullAttack
			goto MissileLoop
		MissileLoop:
			PLSS AB 4 Bright A_SpawnItemEx("Particle_Lightning5", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(24.0, 48.0), 0, 0, 0, 0, SXF_CLIENTSIDE, 128)
			loop
		Death:
			PLSS A 0 Bright A_ChangeFlag("FLOATBOB", false)
			PLSS C 0 Bright A_Stop
			PLSS C 4 Bright A_ScreamAndUnblock
			PLSS D 4 Bright A_Explode(64, 128)
			PLSS EFG 4 Bright A_SpawnItemEx("Particle_Lightning10", frandom(-32.0, 32.0), frandom(-32.0, 32.0), frandom(8.0, 64.0), 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			Stop
	}
}

actor Mayhem19SludgeShotTrail : SH_BaseParticle
{
	Renderstyle Add
	Alpha 0.75
	States
	{
		Spawn:
			BAL2 A 0 Bright A_SetScale(scalex-0.1)
			BAL2 A 1 Bright A_FadeOut(0.125)
			loop
	}
}

actor Mayhem19SludgeShot
{
	Radius 8
	Height 8
	Speed 20
	Damage (32)
	Projectile 
	RenderStyle Add
	Alpha 0.75
	SeeSound "fatso/attack"
	DeathSound "fatso/shotx"
	+RANDOMIZE
	States
	{
		Spawn:
			BAL2 AAABBB 1 Bright A_SpawnItemEx("Mayhem19SludgeShotTrail", 0, 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), 0, SXF_CLIENTSIDE, 0)
			Loop
		Death:
			BAL2 CCCDDDEEE 1 Bright A_SetScale(scalex+0.125)
			Stop
	}
}

actor Mayhem19SludgeMonster : SH_BaseMonster
{
	Tag "Sludge Monster"
	Species "SludgeMonster"
	Health 600
	Radius 40
	Height 64
	Mass 1000
	Speed 8
	PainChance 80
	SeeSound "fatso/sight"
	PainSound "fatso/pain"
	DeathSound "fatso/death"
	ActiveSound "fatso/active"
	Obituary "%o was slimed by a sludge monster."
	BloodColor "17 36 16"
	+FLOORCLIP
	+BOSSDEATH
	States
	{
		Spawn:
			FATT AB 15 A_Look
			Loop
		See:
			FATT AABBCCDD 4 A_Chase
			Loop
		Missile:
			FATT E 10 A_FatRaise
			FATT F 10 A_FaceTarget
			FATT G 0 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, 0, CMF_OFFSETPITCH, 0.0) 
			FATT G 10 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, 11.25, CMF_OFFSETPITCH, 0.0) 
			FATT F 10 A_FaceTarget
			FATT G 0 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, 0, CMF_OFFSETPITCH, 0.0) 
			FATT G 10 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, -11.25, CMF_OFFSETPITCH, 0.0) 
			FATT F 10 A_FaceTarget
			FATT G 0 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, -11.25, CMF_OFFSETPITCH, 0.0) 
			FATT G 10 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, 11.25, CMF_OFFSETPITCH, 0.0) 
			FATT F 5 A_FaceTarget
			FATT E 20 A_FaceTarget
			Goto See
		Pain:
			FATT H 3
			FATT H 3 A_Pain
			Goto See
		Death:
			FATT I 8
			FATT J 8 A_Scream
			FATT K 8 A_NoBlocking
			FATT LMN 8
			FATT O -1 A_BossDeath
			Stop
		Raise:
			FATT NMLKJI 8
			Goto See
	}
}

actor Mayhem19DeathCube : SH_BaseMonster
{
	Tag "Deathcube"
	Species "Deathcube"
	Health 520
	Radius 16
	Height 56
	Mass 100000
	Speed 0
	PainChance 50
	SeeSound "DSBOSCUB"
	PainSound ""
	DeathSound "DSBAREXP"
	ActiveSound "DSBOSCUB"
	Obituary "%o was cubed."
	+FLOORCLIP
	+BOSSDEATH
	+NOBLOOD
	+NODAMAGETHRUST
	+FLOATBOB
	+NOINFIGHTING
	+QUICKTORETALIATE
	States
	{
		Spawn:
			BSPI A 10 A_Look
			Loop
		See:
			BSPI ABC 5 A_Chase
			loop
		Missile:
			BSPI D 10 A_FaceTarget
			goto MissileLoop
		MissileLoop:
			BSPI E 4 Bright A_BspiAttack
			BSPI D 4 Bright A_FaceTarget
			BSPI D 0 Bright A_SpidRefire
			loop
		Pain:
			BSPI D 3
			BSPI D 3 A_Pain
			goto See
		Death:
			BSPI F 0 Bright A_ChangeFlag("FLOATBOB", false)
			BSPI F 5 Bright A_ScreamAndUnblock
			BSPI G 5 Bright
			BSPI H 5 Bright A_Explode
			BSPI H 0 A_BossDeath
			Stop
	}
}

actor Mayhem19Satyr: SH_BaseMonster
{
	Tag "Satyr"
	Species "Satyr"
	Health 320
	Speed 14
	Radius 24
	Height 64
	Damage 0
	ReactionTime 8
	PainChance 100
	Mass 500
	Obituary "%o was gored by a satyr."
	SeeSound "DSKNTSIT"
	PainSound "DSDMPAIN"
	DeathSound "DSBGDTH2"
	ActiveSound "DSDMACT"
	+FLOORCLIP
	States
	{
		Spawn:
			KEEN AB 6 A_Look
			loop
		See:
			KEEN ABCD 4 A_Chase
			loop
		Melee:
			KEEN EF 3 A_FaceTarget
			KEEN G 6 A_CustomMeleeAttack(40, "skeleton/melee", "", "Melee", true)
			goto See
		Pain:
			KEEN H 8 A_Pain
			goto See
		Death:
			KEEN I 6
			KEEN J 6 A_ScreamAndUnblock
			KEEN KLMN 6
			KEEN O -1
			stop
		Raise:
			KEEN NMLKJI 6
			goto See
	}
}

actor Mayhem19Bat : SH_BaseMonster
{
	Tag "Bat"
	Species "Bat"
	Health 10
	Speed 24
	Radius 20
	Height 32
	Damage 3
	ReactionTime 8
	PainChance 256
	Mass 1000
	Obituary "%o became batman."
	SeeSound "DSSKLATK"
	AttackSound "DSSKLATK"
	PainSound "DSRADIO"
	DeathSound "DSCHGUN"
	ActiveSound "DSSKLATK"
	+FLOAT
	+NOGRAVITY
	+SPAWNCEILING
	-COUNTKILL
	States
	{
		Spawn:
			SKUL K 10 A_Look
			loop
		See:
			SKUL A 0 A_ChangeFlag("FLOATBOB", true)
			goto SeeLoop
		SeeLoop:
			SKUL ABCB 3 A_Chase
			loop
		Melee:
			SKUL C 8 A_FaceTarget
			SKUL B 8 A_CustomMeleeAttack(8, "DSCLAW", "", "Melee", true)
			goto SeeLoop
		Pain:
			SKUL D 6 A_Pain
			goto SeeLoop
		Death:
			SKUL D 0 A_ChangeFlag("FLOATBOB", false)
			SKUL D 4 A_ScreamAndUnblock
			SKUL EFG 4
			SKUL H 2
			stop
	}
}

actor Mayhem19ArmlessElemental : SH_BaseMonster
{
	Tag "Pain Elemental?"
	Species "PainElemental"
	Health 150
	Radius 31
	Height 56
	Mass 400
	Speed 8
	PainChance 128
	SeeSound "pain/sight"
	PainSound "pain/pain"
	DeathSound "pain/death"
	ActiveSound "pain/active"
	+FLOAT
	+NOGRAVITY
	States
	{
		Spawn:
			PAIN A 10 A_Look
			Loop
		See:
			PAIN A 3 A_Chase
			Loop
		Missile:
			PAIN B 8 A_FaceTarget
			PAIN C 5 Bright A_FaceTarget
			PAIN D 0 Bright A_Jump(128, "Missile2")
			goto Missile1
		Missile1:
			PAIN D 8 Bright A_FatAttack2 
			Goto See
		Missile2:
			PAIN D 8 Bright A_FatAttack1 
			Goto See
		Pain:
			PAIN E 6
			PAIN E 6 A_Pain
			Goto See
		Death:
			PAIN G 8 Bright
			PAIN H 8 Bright A_Scream
			PAIN J 8 Bright
			PAIN K 7 Bright A_Explode
			Stop
	}
}

actor Mayhem19Hellhound : SH_BaseMonster
{
	Tag "Hellhound"
	Species "Hound"
	Health 140
	Speed 25
	Radius 24
	Height 56
	Damage 25
	ReactionTime 8
	PainChance 50
	Mass 300
	SeeSound "DSDGSIT"
	AttackSound "DSDGATK"
	PainSound "DSDGPAIN"
	DeathSound "DSDGDTH"
	ActiveSound "DSDGACT"
	+FLOORCLIP
	States
	{
		Spawn:
			HDB4 AI 10 A_Look
			loop
		See:
			HDB4 ABCDEF 3 A_Chase
			loop
		Melee:
			HDB4 GH 3 A_FaceTarget
			HDB4 I 8 A_SargAttack
			goto See
		Pain:
			HDB4 J 10 A_Pain
			goto See
		Death:
			HDB4 K 4
			HDB4 L 4 A_ScreamAndUnblock
			HDB4 MNOP 4
			HDB4 Q -1
			stop
		Raise:
			HDB4 PONMLK 4
			goto See
	}
}

actor Mayhem19Shadowhound : Mayhem19Hellhound
{
	Tag "Shadowhound"
	Renderstyle Fuzzy
	+SHADOW
}

ACTOR Mayhem19CacolanternBall
{
	Radius 6
	Height 8
	Speed 14
	FastSpeed 28
	Damage (20)
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 1.0
	SeeSound "caco/attack"
	DeathSound "caco/shotx"
	States
	{
		Spawn:
			BAL2 AB 4 Bright
			Loop
		Death:
			BAL2 CDE 6 Bright
			Stop
	}
}

actor Mayhem19Cacolantern : SH_BaseMonster
{
	Tag "Cacolantern"
	Species "Cacodemon"
	Health 400
	Speed 8
	Radius 31
	Height 56
	Damage 0
	ReactionTime 8
	PainChance 128
	Mass 400
	SeeSound "DSCACSIT"
	PainSound "DSDMPAIN"
	DeathSound "DSCACDTH"
	ActiveSound "DSDMACT"
	Obituary "%o was torched by a cacolantern."
	+NOGRAVITY
	+FLOAT
	States
	{
		Spawn:
			HEAD A 10 A_Look
			loop
		See:
			HEAD A 3 A_Chase
			loop
		Missile:
			HEAD BC 5 A_FaceTarget
			HEAD DCD 6 Bright A_CustomComboAttack("Mayhem19CacolanternBall", 32, 20)
			HEAD C 2 Bright A_FaceTarget
			HEAD B 2 A_FaceTarget
			goto See
		Pain:
			HEAD E 3
			HEAD E 3 A_Pain
			HEAD F 6
			goto See
		Death:
			HEAD G 8
			HEAD H 8 A_ScreamAndUnblock
			HEAD IJ 8
			HEAD K 8 A_Fall
			HEAD L -1
			stop
		Raise:
			HEAD KJIHG 8
			goto See
	}
}

actor Mayhem19CacoSpawner : SH_BaseMonsterRandomSpawner
{
	Radius 31
	Height 56
	Dropitem "Mayhem19Cacolantern"			255 10
	Dropitem "Cacomother"					255 1
	Dropitem "GasterBlaster"				255 1
	Dropitem "Poe"							255 1
	Dropitem "Crackodemon"					255 2
	Dropitem "CCCDroid"						255 1
	Dropitem "Maludemon"					255 4
	Dropitem "Duke3D_SentryDrone"			255 1
	Dropitem "Chiller"						255 1
}

actor Mayhem19TeslaCoil : SH_BaseMonster
{
	Tag "Tesla Coil"
	Species "Electricity"
	Health 150
	Speed 0
	Radius 20
	Height 64
	Damage 0
	ReactionTime 8
	PainChance 0
	Mass 100000
	DeathSound "DSBAREXP"
	+QUICKTORETALIATE
	+LOOKALLAROUND
	+NOBLOOD
	+NODAMAGETHRUST
	-COUNTKILL
	States
	{
		Spawn:
			PLS1 A 10 A_Look
			loop
		See:
			PLS1 A 6 A_Chase
			loop
		Missile:
			PLS1 AAAAAA 1 A_SpawnItemEx("Particle_Lightning8", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(48.0, 56.0), 5, 0, 0, 0, SXF_CLIENTSIDE, 0)
			PLS1 D 4 Bright A_FaceTarget
			PLS1 DD 0 A_SpawnItemEx("Particle_Lightning8", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(56.0, 64.0), 5, 0, 0, 0, SXF_CLIENTSIDE, 0)
			PLS1 E 4 Bright A_FaceTarget
			PLS1 EE 0 A_SpawnItemEx("Particle_Lightning8", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(56.0, 64.0), 5, 0, 0, 0, SXF_CLIENTSIDE, 0)
			PLS1 C 4 Bright A_SpawnItemEx("Mayhem19BallOfLightning", 0, 0, 48, 5, 0, 0, 0, SXF_TRANSFERPOINTERS, 0)
			PLS1 C 0 A_SpawnItemEx("Particle_Lightning8", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(56.0, 64.0), 5, 0, 0, 0, SXF_CLIENTSIDE, 0)
			goto See
		Death:
			PLS1 F 6 Bright A_Scream
			PLS1 G 5 Bright
			PLS1 H 4 Bright
			PLS1 B -1
			stop
	}
}

actor Mayhem19Mummy : SH_BaseMonster
{
	Tag "Mummy"
	Species "Mummy"
	Health 440
	Speed 7
	Radius 24
	Height 64
	Damage 0
	ReactionTime 8
	PainChance 50
	Mass 1000
	SeeSound "DSSGTSIT"
	PainSound "DSPEDTH"
	DeathSound "DSSGTDTH"
	BloodColor "Black"
	+FLOORCLIP
	States
	{
		Spawn:
			BOS2 AB 10 A_Look
			loop
		See:
			BOS2 AABBCCDD 2 A_Chase
			loop
		Missile:
			BOS2 E 8 A_FaceTarget
			BOS2 F 4 A_FaceTarget
			BOS2 G 4 A_CustomComboAttack("Mayhem19CacolanternBall", 32, 10 * random(1, 6))
			BOS2 H 8 A_FaceTarget
			BOS2 I 4 A_FaceTarget
			BOS2 J 4 A_CustomComboAttack("Mayhem19CacolanternBall", 32, 10 * random(1, 6))
			goto See
		Pain:
			BOS2 K 2
			BOS2 L 2 A_Pain
			goto See
		Death:
			BOS2 O 6 A_ScreamAndUnblock
			BOS2 P 6
			BOS2 QRSTUV 6
			BOS2 W -1
			stop
		Raise:
			BOS2 VUTSRQPO 6
			goto See
	}
}

actor Mayhem19DeepOne : SH_BaseMonster
{
	Tag "Deep One"
	Species "DeepOne"
	Health 800
	Speed 9
	Radius 24
	Height 64
	ReactionTime 8
	PainChance 40
	Mass 100
	BloodColor "28 44 b5"
	SeeSound "DSBRSSIT"
	PainSound "DSDMPAIN"
	DeathSound "DSBRSDTH"
	ActiveSound "DSDMACT"
	+FLOORCLIP
	+BOSSDEATH
	States
	{
		Spawn:
			BOSS AB 10 A_Look
			loop
		See:
			BOSS AABBCCDD 2 A_Chase
			loop
		Melee:
			BOSS E 4 A_FaceTarget
			BOSS F 3 A_FaceTarget
			BOSS G 3 A_CustomMeleeAttack(66, "DSSKEPCH", "DSSKESWG", "Melee", true)
			BOSS HI 2
			goto See
		Missile:
			BOSS J 12 A_FaceTarget
			BOSS K 2 Bright A_CustomMissile("BaronBall", 40, 12, frandom(-2.0, 2.0))
			BOSS J 2 A_FaceTarget
			BOSS K 2 Bright A_CustomMissile("BaronBall", 40, 12, frandom(-2.0, 2.0))
			BOSS J 2 A_FaceTarget
			BOSS K 2 Bright A_CustomMissile("BaronBall", 40, 12, frandom(-2.0, 2.0))
			BOSS J 2 A_FaceTarget
			BOSS K 2 Bright A_CustomMissile("BaronBall", 40, 12, frandom(-2.0, 2.0))
			BOSS J 2 A_FaceTarget
			goto See
		Pain:
			BOSS L 2
			BOSS L 8 A_Pain
			goto See
		Death:
			BOSS M 8 A_ScreamAndUnblock
			BOSS NOP 8
			BOSS Q 0 A_BossDeath
			BOSS Q -1
			stop
		Raise:
			BOSS PONM 8
			goto See
	}
}

actor Mayhem19FlyingBrain : SH_BaseMonster
{
	Tag "Flying Brain"
	Species "Brain"
	Health 80
	Speed 10
	Radius 16
	Height 54
	Damage 0
	ReactionTime 8
	PainChance 128
	Mass 100
	SeeSound "DSBSPSIT"
	DeathSound "DSBSPDTH"
	ActiveSound "DSBSPACT"
	+NOGRAVITY
	+FLOAT
	+FLOATBOB
	-FLOORCLIP
	States
	{
		Spawn:
			HDB6 A 8 A_Look
			loop
		See:
			HDB6 A 4 A_Chase
			loop
		Missile:
			HDB6 B 12 A_FaceTarget
			goto MissileLoop
		MissileLoop:
			HDB6 C 4 Bright A_BspiAttack
			HDB6 B 4 A_FaceTarget
			HDB6 B 4 A_SpidRefire
			goto MissileLoop
		Pain:
			HDB6 I 6
			HDB6 I 6 A_Pain
			goto See
		Death:
			HDB6 D 6 A_ChangeFlag("FLOATBOB", false)
			HDB6 E 6 A_Scream
			HDB6 F 6 A_Fall
			HDB6 G 6 A_UnsetFloorClip
			HDB6 H -1
			stop
		Raise:
			HDB6 GFED 6
			goto See
	}
}

actor Mayhem19Lich : SH_BaseMonster
{
	Tag "Lich"
	Species "Lich"
	Health 500
	Speed 14
	Radius 24
	Height 64
	Damage 2
	ReactionTime 8
	PainChance 10
	Mass 200
	SeeSound "DSBOSPIT"
	AttackSound "DSFLAMST"
	PainSound "DSSKLDTH"
	DeathSound "DSSSDTH"
	States
	{
		Spawn:
			BON4 A 10 A_Look
			loop
		See:
			BON4 A 4 A_Chase
			loop
		Missile:
			BON4 A 5 A_PlaySound("DSSSSIT", CHAN_AUTO)
			BON4 BC 14 A_FaceTarget
			BON4 P 14 Bright A_FaceTarget
			BON4 P 0 Bright A_CPosRefire
			goto MissileLoop
		MissileLoop:
			BON4 O 3 Bright A_BetaSkullAttack
			BON4 D 3 Bright A_CPosRefire
			loop
		Pain:
			BON4 E 4
			BON4 E 16 A_Pain
			goto See
		Death:
			BON4 E 8 A_Scream
			BON4 F 4 Bright
			BON4 GH 5 Bright
			BON4 IJ 5 Bright
			BON4 K 5 bright A_Fall
			BON4 LM 4
			BON4 N -1
			stop
	}
}

actor Mayhem19Minotaur : SH_BaseMonster
{
	Tag "Minotaur"
	Species "Minotaur"
	Health 3200
	Speed 16
	Radius 32
	Height 110
	Damage 20
	ReactionTime 8
	PainChance 20
	Mass 9999
	SeeSound "DSCYBSIT"
	AttackSound "DSCYBSIT"
	PainSound "DSDMPAIN"
	DeathSound "DSCYBDTH"
	ActiveSound "DSDMACT"
	+FLOORCLIP
	+BOSS
	States
	{
		Spawn:	
			CYBR AB 10 A_Look
			loop
		See:
			CYBR A 3 A_Hoof
			CYBR A 3 A_Chase
			CYBR BB 3 A_Chase
			CYBR C 3 A_Hoof
			CYBR C 3 A_Chase
			CYBR DD 3 A_Chase
			loop
		Melee:
			CYBR E 6 A_FaceTarget
			CYBR F 4 A_PlaySound("DSMETAL", CHAN_AUTO)
			CYBR G 6 A_Explode
			CYBR J 10 A_FaceTarget
			CYBR K 4 A_PlaySound("DSMETAL", CHAN_AUTO)
			CYBR L 6 A_Explode
			goto See
		Missile:
			CYBR J 0 A_Jump(127, "Missile2")
			CYBR J 12 A_FaceTarget
			CYBR M 5 A_FaceTarget
			CYBR N 5 Bright A_CyberAttack
			goto See
		Missile2:
			CYBR E 12 A_FaceTarget
			CYBR H 6 Bright A_FatAttack3
			CYBR I 6 Bright A_FatAttack2
			CYBR J 8 A_FaceTarget
			CYBR M 6 Bright A_FatAttack3
			CYBR N 6 Bright A_FatAttack1
			goto See
		Pain:
			CYBR P 4
			CYBR P 8 A_Pain
			goto See
		Death:
			CYBR Q 6 A_ScreamAndUnblock
			CYBR RST 6
			CYBR U 6 A_Fall
			CYBR V 6
			CYBR W 6 A_KeenDie
			CYBR X 6 A_PlaySound("DSMETAL", CHAN_AUTO)
			CYBR Y -1
			stop
	}
}

actor Mayhem19Spooklord : SH_BaseMonster
{
	Tag "Spooklord"
	Species "Spooklord"
	Health 5000
	Speed 20
	Radius 32
	Height 100
	Damage 2
	ReactionTime 8
	PainChance 5
	Mass 1000
	SeeSound "DSBOSSIT"
	AttackSound "DSFLAMST"
	PainSound "DSKEENPN"
	DeathSound "DSBOSDTH"
	+FLOORCLIP
	+BOSS
	States
	{
		Spawn:
			SMIT BC 8 A_Look
			loop
		See:
			SMIT ABCD 4 A_Chase
			loop
		Missile:
			SMIT G 6 A_FaceTarget
			SMIT G 0 A_Jump(128, "Missile2", "Missile3")
			SMIT K 4 Bright A_FaceTarget
			SMIT L 8 Bright A_CyberAttack
			SMIT K 8 Bright A_FaceTarget
			SMIT L 8 Bright A_CyberAttack
			SMIT K 8 Bright A_FaceTarget
			SMIT L 8 Bright A_CyberAttack
			goto See
		Missile2:
			SMIT I 10 Bright A_FaceTarget
			SMIT J 4 Bright A_FaceTarget
			SMIT J 8 Bright A_PainAttack("Mayhem19BallOfLightning")
			goto See
		Missile3:
			SMIT E 12 Bright A_FaceTarget
			SMIT FFFFF 5 Bright A_CustomMissile("Mayhem19VampireBat", 32.0, 0.0, 0.0, frandom(-4.0, 4.0)) 
			goto See
		Pain:
			SMIT M 9 A_Pain
			goto See
		Death:
			SMIT N 8 A_Scream
			SMIT OPQ 8 Bright
			SMIT R 8 A_Fall
			SMIT ST 8
			SMIT U 8
			SMIT V 35
			SMIT V -1 A_BrainDie
			stop
	}
}