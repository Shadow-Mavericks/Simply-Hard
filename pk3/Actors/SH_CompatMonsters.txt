actor MayhemPlasmaMarine
{
	Tag "FAAAABULOUS Marine"
	Monster
	Radius 20
	Height 56
	Health 100
	Speed 12
	ReactionTime 8
	PainChance 170
	Mass 100
	SeeSound "grunt/sight"
	PainSound "*pain50"
	DeathSound "*death"
	ActiveSound "grunt/active"
	DropItem "Cell"
	States
	{
		Spawn:
			PLAY AB 10 A_Look
			loop
		See:
			PLAY AABBCCDD 3 A_Chase
			loop
		Missile:
			PLAY E 5 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			PLAY F 2 A_BspiAttack
			PLAY E 3 A_FaceTarget
			goto See
		Pain:
			PLAY G 3
			PLAY G 3 A_Pain
			goto See
		Death:
			PLAY H 5
			PLAY I 5 A_Scream
			PLAY J 5 A_Fall
			PLAY KLM 5
			PLAY N -1
			stop
		XDeath:
			PLAY O 5
			PLAY P 5 A_XScream
			PLAY Q 5 A_Fall
			PLAY RSTUV 5
			PLAY W -1
			stop
		Raise:
			PLAY NMLKJIH 5
			goto SEE
	}
}

actor MayhemGrell
{
	Tag "Grell"
	Monster
	Radius 24
	Height 56
	Health 300
	Speed 10
	ReactionTime 8
	PainChance 128
	Mass 400
	SeeSound "DSBRSSIT"
	PainSound "DSDMPAIN"
	DeathSound "DSBRSDTH"
	ActiveSound "DSDMACT"
	+FLOAT
	+NOGRAVITY
	States
	{
		Spawn:
			BOSS A 10 A_Look
			loop
		See:
			BOSS AABBCC 3 A_Chase
			loop
		Missile:
			BOSS AD 4 A_FaceTarget
			BOSS E 4 A_BruisAttack
			goto See
		Pain:
			BOSS F 3
			BOSS F 3 A_Pain
			goto See
		Death:
			BOSS G 5 A_Scream
			BOSS H 4 A_Fall
			BOSS IJKL 4
			BOSS M -1
			stop
		Raise:
			BOSS MLKJIHG 4
			goto See
	}
}

actor MayhemImmolator
{
	Tag "Immolator"
	Monster
	Health 2500
	Speed 12
	Radius 40
	Height 80
	ReactionTime 8
	PainChance 20
	Mass 1000
	SeeSound "DSSPISIT"
	PainSound "DSDMPAIN"
	DeathSound "DSSPIDTH"
	ActiveSound "DSDMACT"
	+NOGRAVITY
	+FLOAT
	States
	{
		Spawn:
			SSWV AB 10 A_Look
			loop
		See:
			SSWV AB 3 A_Chase
			loop
		Missile:
			SSWV C 8 A_FaceTarget
			SSWV D 4 A_FatAttack1
			SSWV D 4 A_FatAttack2
			SSWV D 4 A_FatAttack3
			goto See
		Pain:
			SSWV E 5 A_Pain
			SSWV E 5
			SSWV C 8 A_FaceTarget
			SSWV DDD 3 A_SkelMissile
			goto See
		Death:
			SSWV F 8 A_Scream
			SSWV G 8
			SSWV H 8 A_Fall
			SSWV IJKL 8
			stop
	}
}

actor Mayhem19ShotgunCultist
{
	Tag "Shotgun Cultist"
	Health 50
	Radius 20
	Height 56
	Mass 100
	Speed 9
	PainChance 150
	Monster
	+FLOORCLIP
	SeeSound "shotguy/sight"
	AttackSound "shotguy/attack"
	PainSound "shotguy/pain"
	DeathSound "shotguy/death"
	ActiveSound "shotguy/active"
	Obituary "$OB_SHOTGUY"
	DropItem "Shotgun"
	States
	{
		Spawn:
			SPOS AB 10 A_Look
			Loop
		See:
			SPOS AABBCCDD 3 A_Chase
			Loop
		Missile:
			SPOS E 6 A_FaceTarget
			SPOS F 6 Bright A_SPosAttack
			SPOS E 6
			Goto See
		Pain:
			SPOS G 3
			SPOS G 3 A_Pain
			Goto See
		Death:
			SPOS H 5
			SPOS I 5 A_Scream
			SPOS J 5 A_NoBlocking
			SPOS KLM 5
			SPOS N -1
			Stop
		XDeath:
			SPOS M 5
			SPOS N 5 A_XScream
			SPOS O 5 A_NoBlocking
			SPOS PQRSTUV 5
			SPOS W -1
			Stop
		Raise:
			SPOS MLKJIH 5
			Goto See
	}
}

actor Mayhem19EliteCultist
{
	Tag "Elite Cultist"
	Health 70
	Radius 20
	Height 56
	Mass 100
	Speed 11
	PainChance 170
	Monster
	+FLOORCLIP
	SeeSound "chainguy/sight"
	PainSound "chainguy/pain"
	DeathSound "chainguy/death"
	ActiveSound "chainguy/active"
	AttackSound "depisto"
	Obituary "$OB_CHAINGUY"
	Dropitem "ClipBox"
	States
	{
		Spawn:
			CPOS AB 10 A_Look
			Loop
		See:
			CPOS AABBCCDD 3 A_Chase
			Loop
		Missile:
			CPOS E 10 A_FaceTarget
			CPOS F 3 Bright A_CPosAttack
			CPOS E 2
			CPOS F 3 Bright A_CPosAttack
			CPOS E 3
			CPOS E 0 A_Jump(100, "See")
			CPOS E 3 A_CPosRefire
			Goto Missile+1
		Pain:
			CPOS G 3
			CPOS G 3 A_Pain
			Goto See
		Death:
			CPOS H 5
			CPOS I 5 A_Scream
			CPOS J 5 A_NoBlocking
			CPOS K 5
			CPOS L -1
			Stop
		XDeath:
			CPOS M 5 
			CPOS N 5 A_XScream
			CPOS O 5 A_NoBlocking
			CPOS PQRST 5
			CPOS U -1
			Stop
		Raise:
			CPOS KJIH 5
			Goto See
	}
}

actor Mayhem19PistolCultist : Mayhem19EliteCultist
{
	Tag "Pistol Cultist"
	Health 50
	Speed 10
	Dropitem "Clip"
}

ACTOR Mayhem19VampireBat
{
	Radius 11
	Height 8
	Speed 10
	Damage (20)
	Projectile
	+SEEKERMISSILE
	+RANDOMIZE
	SeeSound "DSSKLATK"
	DeathSound "DSBAREXP"
	RenderStyle Add
	States
	{
		Spawn:
			FATB ABCB 2 Bright A_Tracer
			Loop
		Death:
			FBXP A 8 Bright
			FBXP B 6 Bright
			FBXP C 4 Bright
			Stop
	}
}

actor Mayhem19Vampire
{
	Tag "Vampire"
	Health 300
	Radius 20
	Height 56
	Mass 500
	Speed 8
	PainChance 100
	Monster
	MeleeThreshold 196
	+MISSILEMORE 
	+FLOORCLIP
	SeeSound "DSPOSIT3"
	PainSound "DSPOPAIN"
	DeathSound "DSBGDTH2"
	ActiveSound "DSPOSACT"
	MeleeSound "skeleton/melee"
	HitObituary "$OB_UNDEADHIT" // "%o was punched by a revenant."
	Obituary "$OB_UNDEAD" // "%o couldn't evade a revenant's fireball."
	DropItem "HealthBonus"
	DropItem "HealthBonus"
	DropItem "HealthBonus"
	States
	{
		Spawn:
			SKEL AB 10 A_Look
			Loop
		See:
			SKEL AABB 2 A_Chase
			Loop
		Melee:
			SKEL H 0 A_FaceTarget
			SKEL H 4 A_SkelWhoosh
			SKEL I 4 A_FaceTarget
			SKEL J 6 A_CustomMeleeAttack(50, "skeleton/melee", "", "Melee", true)
			Goto See
		Missile:
			SKEL C 6 A_FaceTarget
			SKEL F 6 Bright A_FaceTarget
			SKEL GGG 5 Bright A_CustomMissile("Mayhem19VampireBat", 32.0, 0.0, 0.0, 0) 
			SKEL C 12
			Goto See
		Pain:
			SKEL K 5
			SKEL K 5 A_Pain
			Goto See
		Death:
			SKEL LM 7
			SKEL N 7 A_Scream
			SKEL O 7 A_NoBlocking
			SKEL PQR 7
			SKEL S -1
			Stop
		Raise:
			SKEL PONML 5
			Goto See
	}
}

ACTOR Mayhem19Funnybone
{
	Radius 6
	Height 8
	Speed 10
	FastSpeed 20
	Damage (8)
	Projectile
	+RANDOMIZE
	RenderStyle Normal
	Alpha 1
	SeeSound "DSSKESWG"
	DeathSound "DSPUNCH"
	States
	{
		Spawn:
			BAL1 ABCD 4
			Loop
		Death:
			PUFF C 0 A_SetTranslucent(0.75, 0)
			PUFF CCCDDD 1 A_SetScale(scalex+0.25)
			Stop
	}
}

actor Mayhem19Skellie : SH_BaseMonster
{
	Tag "Skellie"
	Health 120
	PainChance 180
	Speed 14
	Radius 20
	Height 56
	Mass 400
	Monster
	+FLOORCLIP
	SeeSound "DSSKESIT"
	AttackSound "demon/melee"
	PainSound "DSPOPAIN"
	DeathSound "DSSKEDTH"
	ActiveSound "DSSKEACT"
	Obituary "%o took a funnyboner to the butt."
	States
	{
		Spawn:
			SARG A 0 NoDelay A_SetUserVar("user_hpbar_offset", 24)
			goto SpawnLoop
		SpawnLoop:
			SARG AB 10 A_Look
			Loop
		See:
			SARG AABBCCDD 2 Fast A_Chase
			Loop
		Melee:
		Missile:
			SARG F 3 Fast A_FaceTarget
			SARG GH 2 Fast A_FaceTarget
			SARG I 8 Fast A_CustomComboAttack("Mayhem19Funnybone", 32, 3 * random(1, 8), "DSSKEPCH")
			Goto See
		Pain:
			SARG L 4 Fast
			SARG L 4 Fast A_Pain
			Goto See
		Death:
			SARG M 8
			SARG N 8 A_Scream
			SARG O 8
			SARG P 5 A_NoBlocking
			SARG Q -1
			Stop
		Raise:
			SARG PONM 8
			Goto See
	}
}

actor Mayhem19ShadowImp : Hardimp
{
	Tag "Shadow Imp"
	RenderStyle Fuzzy
	+SHADOW
}

ACTOR Mayhem19BallOfLightning : SH_BaseMonster
{
	Tag "Ball of Lightning"
	Health 40
	Radius 16
	Height 56
	Mass 50
	Speed 5
	Damage 3
	DamageType "Electric"
	PainChance 0
	ReactionTime 16
	+FLOAT
	+FLOATBOB
	+NOGRAVITY
	+MISSILEMORE
	+DONTFALL
	+NOICEDEATH
	+NOBLOOD
	+QUICKTORETALIATE
	SeeSound "DSBSPWLK"
	AttackSound "DSBSPWLK"
	PainSound "DSFIRXPL"
	DeathSound "DSBAREXP"
	ActiveSound ""
	RenderStyle Add
	Alpha 1.0
	Obituary "%o was electro-cocksuckered by a ball of lightning."
	States
	{
		Spawn:
			PLSS AB 8 Bright A_Look
			Loop
		See:
			PLSS AA 1 Bright A_Wander
			PLSS AA 1 Bright A_Chase
			PLSS BB 1 Bright A_Wander
			PLSS BB 1 Bright A_Chase
			Loop
		Melee:
			PLSS A 1 Bright A_Die
			wait
		Missile:
			PLSS A 6 Bright A_FaceTarget
			PLSS B 4 Bright A_SkullAttack
			goto MissileLoop
		MissileLoop:
			PLSS AB 4 Bright
			loop
		Death:
			PLSS A 0 Bright A_ChangeFlag("FLOATBOB", false)
			PLSS C 0 Bright A_Stop
			PLSS C 4 Bright A_ScreamAndUnblock
			PLSS D 4 Bright A_Explode
			PLSS EFG 4 Bright
			Stop
	}
}

actor Mayhem19SludgeShotTrail : Particle_Base
{
	Renderstyle Add
	Alpha 0.75
	States
	{
		Spawn:
			BAL2 A 0 Bright A_SetScale(scalex-0.1)
			BAL2 A 1 Bright A_FadeOut(0.125)
			loop
	}
}

actor Mayhem19SludgeShot
{
	Radius 8
	Height 8
	Speed 20
	Damage (32)
	Projectile 
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.75
	SeeSound "fatso/attack"
	DeathSound "fatso/shotx"
	States
	{
		Spawn:
			BAL2 AAABBB 1 Bright A_SpawnItemEx("Mayhem19SludgeShotTrail", 0, 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), 0, SXF_CLIENTSIDE, 0)
			Loop
		Death:
			BAL2 CCCDDDEEE 1 Bright A_SetScale(scalex+0.125)
			Stop
	}
}

actor Mayhem19SludgeMonster : SH_BaseMonster
{
	Tag "Sludge Monster"
	Health 600
	Radius 40
	Height 64
	Mass 1000
	Speed 8
	PainChance 80
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "fatso/sight"
	PainSound "fatso/pain"
	DeathSound "fatso/death"
	ActiveSound "fatso/active"
	Obituary "%o was slimed by a sludge monster."
	States
	{
		Spawn:
			FATT AB 15 A_Look
			Loop
		See:
			FATT AABBCCDD 4 A_Chase
			Loop
		Missile:
			FATT E 10 A_FatRaise
			FATT F 10 A_FaceTarget
			FATT G 0 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, 0, CMF_OFFSETPITCH, 0.0) 
			FATT G 10 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, 11.25, CMF_OFFSETPITCH, 0.0) 
			FATT F 10 A_FaceTarget
			FATT G 0 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, 0, CMF_OFFSETPITCH, 0.0) 
			FATT G 10 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, -11.25, CMF_OFFSETPITCH, 0.0) 
			FATT F 10 A_FaceTarget
			FATT G 0 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, -11.25, CMF_OFFSETPITCH, 0.0) 
			FATT G 10 Bright A_CustomMissile("Mayhem19SludgeShot", 56.0, 0.0, 11.25, CMF_OFFSETPITCH, 0.0) 
			FATT F 5 A_FaceTarget
			FATT E 20 A_FaceTarget
			Goto See
		Pain:
			FATT H 3
			FATT H 3 A_Pain
			Goto See
		Death:
			FATT I 8
			FATT J 8 A_Scream
			FATT K 8 A_NoBlocking
			FATT LMN 8
			FATT O -1 A_BossDeath
			Stop
		Raise:
			FATT NMLKJI 8
			Goto See
	}
}

actor Mayhem19DeathCube : SH_BaseMonster
{
	Tag "Deathcube"
	Health 520
	Radius 16
	Height 56
	Mass 100000
	Speed 0
	PainChance 50
	+FLOORCLIP
	+BOSSDEATH
	+NOBLOOD
	+NODAMAGETHRUST
	+FLOATBOB
	+NOINFIGHTING
	+QUICKTORETALIATE
	SeeSound "DSBOSCUB"
	PainSound ""
	DeathSound "DSBAREXP"
	ActiveSound "DSBOSCUB"
	Obituary "%o was cubed."
	States
	{
		Spawn:
			BSPI A 10 A_Look
			Loop
		See:
			BSPI ABC 5 A_Chase
			loop
		Missile:
			BSPI D 10 A_FaceTarget
			goto MissileLoop
		MissileLoop:
			BSPI E 4 Bright A_BspiAttack
			BSPI D 4 Bright A_FaceTarget
			BSPI D 0 Bright A_SpidRefire
			loop
		Pain:
			BSPI D 3
			BSPI D 3 A_Pain
			goto See
		Death:
			BSPI F 0 Bright A_ChangeFlag("FLOATBOB", false)
			BSPI F 5 Bright A_ScreamAndUnblock
			BSPI G 5 Bright
			BSPI H 5 Bright A_Explode
			BSPI H 0 A_BossDeath
			Stop
	}
}

actor Mayhem19Satyr: SH_BaseMonster
{
	Tag "Satyr"
	Health 320
	Speed 14
	Radius 24
	Height 64
	Damage 0
	ReactionTime 8
	PainChance 100
	Mass 500
	Obituary "%o was gored by a satyr."
	SeeSound "DSKNTSIT"
	PainSound "DSDMPAIN"
	DeathSound "DSBGDTH2"
	ActiveSound "DSDMACT"
	+FLOORCLIP
	States
	{
		Spawn:
			KEEN AB 6 A_Look
			loop
		See:
			KEEN ABCD 4 A_Chase
			loop
		Melee:
			KEEN EF 3 A_FaceTarget
			KEEN G 6 A_CustomMeleeAttack(40, "skeleton/melee", "", "Melee", true)
			goto See
		Pain:
			KEEN H 8 A_Pain
			goto See
		Death:
			KEEN I 6
			KEEN J 6 A_ScreamAndUnblock
			KEEN KLMN 6
			KEEN O -1
			stop
		Raise:
			KEEN NMLKJI 6
			goto See
	}
}