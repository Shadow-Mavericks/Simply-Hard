#library "SH_LivesSystem"
#include "zcommon.acs"
#import "SH_GlobalData.acsx"

// Stuff pertaining to the lives system. Written by StrikerTheHedgefox.

global int 1:lives_at_end[];
bool justRevived;

function void update_lives(int lives)
{
	int pNum = PlayerNumber();
	
	if(lives >= 0)
		SetPlayerLivesLeft(pNum, lives);

	// Hack to make sure lives are tracked on HUD correctly.
	int current_lives = GetPlayerLivesLeft(pNum);
	TakeInventory("LifeTrackerHack", 9999);
	if(current_lives > 0)
		GiveInventory("LifeTrackerHack", current_lives);
}

script "LIFEUSED" RESPAWN
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;
	if(!GetCVar("survival") && !GetCVar("cooperative"))
		terminate;
		
	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);
	
	if(!justRevived)
	{
		if(current_lives == 1)
			print(s:"You have ", d:current_lives, s:" life remaining!");
		else
			print(s:"You have ", d:current_lives, s:" lives remaining!");
	}
	
	justRevived = false;
	Ambientsound("oneman/usedup", 127);
	GiveInventory("Res_protection", 1);
	
	update_lives(-1);
}

script "STORELIVES" UNLOADING
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	for(int i = 0; i < MAX_PLAYERS; i++)
	{
		lives_at_end[i] = GetPlayerLivesLeft(i);
	}
}

script "CARRYLIVES" ENTER // Carries lives over to the next level.
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);
	p_status[pNum].time_of_death = 0x7FFFFFFF;

	if(lives_at_end[pNum] > current_lives)
		update_lives(lives_at_end[pNum]);

	lives_at_end[pNum] = 0; // Clear it.
	GiveInventory("Res_protection", 1); // Some spawn protection for those BS maps that have enemies in your face the second you enter.
}

script "ADDLIFE" (void)
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);

/*
	if(current_lives >= 3)
	{
		SetResultValue(0);
		terminate;
	}
*/

	current_lives++; // 1 UP!
	update_lives(current_lives);

	if(current_lives == 1)
		print(s:"You now have ", d:current_lives, s:" extra life!");
	else
		print(s:"You now have ", d:current_lives, s:" extra lives!");

	SetResultValue(1);
}

script "RESETTIMEOFDEATH" OPEN // Resets time of death on level open.
{
	for(int i = 0; i < MAX_PLAYERS; i++)
		p_status[i].time_of_death = 0x7FFFFFFF;
}

script "RECORDDEATHTIME" DEATH // Records time of death to choose revive order.
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1) // Only execute on server.
		terminate;

	int pNum = PlayerNumber();
	p_status[pNum].time_of_death = Timer();
}

script "REVIVEPLAYER" (void) NET
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1) // Only execute on server.
		terminate;

	int pNum = PlayerNumber();
	int dead_longest = -1;
	int earliest_death_time = 0x7FFFFFFF;
	
	int current_lives = GetPlayerLivesLeft(pNum);
	if(current_lives <= 0)
	{
		Print(s:"No extra lives to spare for revive!");
		terminate;
	}
	
	for(int i = 0; i < MAX_PLAYERS; i++)
	{
		if(PlayerIsSpectator(i) == 2)
		{
			if(p_status[i].time_of_death < earliest_death_time)
			{
				earliest_death_time = p_status[i].time_of_death;
				dead_longest = i;
			}
		}
	}
	
	if(dead_longest != -1)
	{
		justRevived = true;
		SetDeadSpectator(dead_longest, 0);
		SetPlayerLivesLeft(dead_longest, 0);

		PrintBold(n:dead_longest+1, s:" was revived by ", n:0);
		current_lives--;
		update_lives(current_lives);
	}
	else
	{
		Print(s:"Couldn't find anyone to revive!");
	}
}