#library "ExtraLifeThingy"
#include "zcommon.acs"

Script "oneman" (void)
{
	Activatorsound("oneman", 127);
}

script "LIFEUSED" RESPAWN
{
	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);
	
	if(!GetCVar("survival") && !GetCVar("cooperative"))
		terminate;
	
	if(current_lives == 1)
		print(s:"You have ", d:current_lives, s:" life remaining!");
	else
		print(s:"You have ", d:current_lives, s:" lives remaining!");
	
	Ambientsound("oneman/usedup", 127);
	GiveInventory("Res_protection", 1);
	
	// Hack to update lives on HUD and carry extras over to next level.
	TakeInventory("LifeTrackerHack", 9999);
	if(current_lives > 0)
		GiveInventory("LifeTrackerHack", current_lives);
}

script "CARRYLIVES" ENTER // Carries lives over to the next level.
{
	int pNum = PlayerNumber();
	int max_lives = GetCVar("sv_maxlives");
	int current_lives = CheckInventory("LifeTrackerHack");
	
	if(current_lives > max_lives)
	{
		SetPlayerLivesLeft(pNum, current_lives);
	}
	else
	{
		current_lives = GetPlayerLivesLeft(pNum);
		TakeInventory("LifeTrackerHack", 9999);
		GiveInventory("LifeTrackerHack", current_lives);
	}
}

script "ADDLIFE" (void)
{
	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);
	
	if(current_lives >= 3)
	{
		SetResultValue(0);
		terminate;
	}

	current_lives++; // 1 UP!
	SetPlayerLivesLeft(pNum, current_lives);
	// Hack to update lives on HUD and carry extras over to next level.
	TakeInventory("LifeTrackerHack", 9999);
	GiveInventory("LifeTrackerHack", current_lives);

	if(current_lives == 1)
		print(s:"You now have ", d:current_lives, s:" extra life!");
	else
		print(s:"You now have ", d:current_lives, s:" extra lives!");

	SetResultValue(1);
}