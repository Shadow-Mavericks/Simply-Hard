#library "ExtraLifeThingy"
#include "zcommon.acs"

// Stuff pertaining to the lives system. Written by StrikerTheHedgefox.

global int 1:lives_at_end[];
int timeOfDeath[64];
bool justRevived;

int spawn_pos[4];

function void update_lives(int lives)
{
	int pNum = PlayerNumber();
	
	if(lives >= 0)
		SetPlayerLivesLeft(pNum, lives);

	// Hack to make sure lives are tracked on HUD correctly.
	int current_lives = GetPlayerLivesLeft(pNum);
	TakeInventory("LifeTrackerHack", 9999);
	if(current_lives > 0)
		GiveInventory("LifeTrackerHack", current_lives);
}

script "LIFEUSED" RESPAWN
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;
	if(!GetCVar("survival") && !GetCVar("cooperative"))
		terminate;
		
	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);
	
	if(!justRevived)
	{
		if(current_lives == 1)
			print(s:"You have ", d:current_lives, s:" life remaining!");
		else
			print(s:"You have ", d:current_lives, s:" lives remaining!");
	}
	
	justRevived = false;
	Ambientsound("oneman/usedup", 127);
	GiveInventory("Res_protection", 1);
	
	update_lives(-1);
}

script "STORELIVES" UNLOADING
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	for(int i = 0; i < 64; i++)
	{
		lives_at_end[i] = GetPlayerLivesLeft(i);
	}
}

script "CARRYLIVES" ENTER // Carries lives over to the next level.
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;
		
	if(!IsNetworkGame()) // Record spawn position in SP.
	{
		spawn_pos[0] = GetActorX(0);
		spawn_pos[1] = GetActorY(0);
		spawn_pos[2] = GetActorZ(0);
		spawn_pos[3] = GetActorAngle(0);
	}

	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);
	timeOfDeath[pNum] = 0x7FFFFFFF;
	
	if(lives_at_end[pNum] > current_lives)
		update_lives(lives_at_end[pNum]);

	GiveInventory("Res_protection", 1); // Some spawn protection for those BS maps that have enemies in your face the second you enter.
}

script "ADDLIFE" (void)
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);

/*
	if(current_lives >= 3)
	{
		SetResultValue(0);
		terminate;
	}
*/

	current_lives++; // 1 UP!
	update_lives(current_lives);

	if(current_lives == 1)
		print(s:"You now have ", d:current_lives, s:" extra life!");
	else
		print(s:"You now have ", d:current_lives, s:" extra lives!");

	SetResultValue(1);
}

script "RESETTIMEOFDEATH" OPEN // Resets time of death on level open.
{
	for(int i = 0; i < 64; i++)
		timeOfDeath[i] = 0x7FFFFFFF;
}

script "RECORDDEATHTIME" DEATH // Records time of death to choose revive order.
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1) // Only execute on server.
		terminate;

	int pNum = PlayerNumber();
	timeOfDeath[pNum] = Timer();
}

script "REVIVEPLAYER" (void) NET
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1) // Only execute on server.
		terminate;

	int pNum = PlayerNumber();
	int dead_longest = -1;
	int earliest_death_time = 0x7FFFFFFF;
	
	int current_lives = GetPlayerLivesLeft(pNum);
	if(current_lives <= 0)
	{
		Print(s:"No extra lives to spare for revive!");
		terminate;
	}
	
	for(int i = 0; i < 64; i++)
	{
		if(PlayerIsSpectator(i) == 2)
		{
			if(timeOfDeath[i] < earliest_death_time)
			{
				earliest_death_time = timeOfDeath[i];
				dead_longest = i;
			}
		}
	}
	
	if(dead_longest != -1)
	{
		justRevived = true;
		SetDeadSpectator(dead_longest, 0);
		SetPlayerLivesLeft(dead_longest, 0);

		PrintBold(n:dead_longest+1, s:" was revived by ", n:0);
		current_lives--;
		update_lives(current_lives);
	}
	else
	{
		Print(s:"Couldn't find anyone to revive!");
	}
}