#library "ExtraLifeThingy"
#include "zcommon.acs"

// Stuff pertaining to the lives system. Written by StrikerTheHedgefox.

global int 1:lives_at_end[];
int timeOfDeath[64];

script "LIFEUSED" RESPAWN
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);
	
	if(!GetCVar("survival") && !GetCVar("cooperative"))
		terminate;
	
	if(current_lives == 1)
		print(s:"You have ", d:current_lives, s:" life remaining!");
	else
		print(s:"You have ", d:current_lives, s:" lives remaining!");
	
	Ambientsound("oneman/usedup", 127);
	GiveInventory("Res_protection", 1);
	
	// Hack to update lives on HUD and carry extras over to next level.
	TakeInventory("LifeTrackerHack", 9999);
	if(current_lives > 0)
		GiveInventory("LifeTrackerHack", current_lives);
}

script "STORELIVES" UNLOADING
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	for(int i = 0; i < 64; i++)
		lives_at_end[i] = GetPlayerLivesLeft(i)+1;
}

script "CARRYLIVES" ENTER // Carries lives over to the next level.
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int max_lives = GetCVar("sv_maxlives");
	int current_lives = lives_at_end[pNum];
	
	if(current_lives > max_lives)
		SetPlayerLivesLeft(pNum, current_lives);
		
	current_lives = GetPlayerLivesLeft(pNum);
	TakeInventory("LifeTrackerHack", 9999);
	GiveInventory("LifeTrackerHack", current_lives);
	GiveInventory("Res_protection", 1); // Some spawn protection for those BS maps that have enemies in your face the second you enter.
}

script "ADDLIFE" (void)
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);

/*
	if(current_lives >= 3)
	{
		SetResultValue(0);
		terminate;
	}
*/

	current_lives++; // 1 UP!
	SetPlayerLivesLeft(pNum, current_lives);
	// Hack to update lives on HUD and carry extras over to next level.
	TakeInventory("LifeTrackerHack", 9999);
	GiveInventory("LifeTrackerHack", current_lives);

	if(current_lives == 1)
		print(s:"You now have ", d:current_lives, s:" extra life!");
	else
		print(s:"You now have ", d:current_lives, s:" extra lives!");

	SetResultValue(1);
}