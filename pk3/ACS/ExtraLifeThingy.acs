#library "ExtraLifeThingy"
#include "zcommon.acs"

// Stuff pertaining to the lives system. Written by StrikerTheHedgefox.

global int 1:lives_at_end[];
int timeOfDeath[64];

script "RESETTIMEOFDEATH" OPEN
{
	for(int i = 0; i < 64; i++)
		timeOfDeath[i] = 0x7FFFFFFF;
}

script "RECORDDEATHTIME" DEATH
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	timeOfDeath[pNum] = Timer();
}

script "LIFEUSED" RESPAWN
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);
	
	if(!GetCVar("survival") && !GetCVar("cooperative"))
		terminate;
	
	if(current_lives == 1)
		print(s:"You have ", d:current_lives, s:" life remaining!");
	else
		print(s:"You have ", d:current_lives, s:" lives remaining!");
	
	Ambientsound("oneman/usedup", 127);
	GiveInventory("Res_protection", 1);
	
	// Hack to update lives on HUD and carry extras over to next level.
	TakeInventory("LifeTrackerHack", 9999);
	if(current_lives > 0)
		GiveInventory("LifeTrackerHack", current_lives);
}

script "STORELIVES" UNLOADING
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	for(int i = 0; i < 64; i++)
	{
		lives_at_end[i] = GetPlayerLivesLeft(i);
	}
}

script "CARRYLIVES" ENTER // Carries lives over to the next level.
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);
	timeOfDeath[pNum] = 0x7FFFFFFF;
	
	if(lives_at_end[pNum] > current_lives)
		SetPlayerLivesLeft(pNum, lives_at_end[pNum]);
		
	current_lives = GetPlayerLivesLeft(pNum); // Get again, just in case.
	TakeInventory("LifeTrackerHack", 9999);
	GiveInventory("LifeTrackerHack", current_lives);
	GiveInventory("Res_protection", 1); // Some spawn protection for those BS maps that have enemies in your face the second you enter.
}

script "ADDLIFE" (void)
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int current_lives = GetPlayerLivesLeft(pNum);

/*
	if(current_lives >= 3)
	{
		SetResultValue(0);
		terminate;
	}
*/

	current_lives++; // 1 UP!
	SetPlayerLivesLeft(pNum, current_lives);
	// Hack to update lives on HUD and carry extras over to next level.
	TakeInventory("LifeTrackerHack", 9999);
	GiveInventory("LifeTrackerHack", current_lives);

	if(current_lives == 1)
		print(s:"You now have ", d:current_lives, s:" extra life!");
	else
		print(s:"You now have ", d:current_lives, s:" extra lives!");

	SetResultValue(1);
}

script "REVIVEPLAYER" (void) NET
{
	if(IsNetworkGame() && ConsolePlayerNumber() != -1)
		terminate;

	int pNum = PlayerNumber();
	int dead_longest = -1;
	int longest_time = 0x7FFFFFFF;
	
	int current_lives = GetPlayerLivesLeft(pNum);
	if(current_lives <= 0)
	{
		Print(s:"No extra lives to spare for revive!");
		terminate;
	}
	
	for(int i = 0; i < 64; i++)
	{
		if(PlayerIsSpectator(i) == 2)
		{
			if(timeOfDeath[i] < longest_time)
				dead_longest = i;
		}
	}
	
	if(dead_longest != -1)
	{
		SetDeadSpectator(dead_longest, 0);
		SetPlayerLivesLeft(dead_longest, 0);

		PrintBold(n:dead_longest+1, s:" was revived by ", n:0);
		current_lives--;
		SetPlayerLivesLeft(pNum, current_lives);
	}
	else
	{
		Print(s:"Couldn't find anyone to revive!");
	}
}