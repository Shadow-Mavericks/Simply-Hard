#library "SH_PlayerTicker"
#include "Zcommon.acs"
#import "SH_GlobalData.acsx"

//////////////////////
// Player Tick Handler
//////////////////////
#define INVUL_NONE 0
#define INVUL_NORMAL 1
#define INVUL_ULT 2

str inv_music_list[] = {
	"FP_INVUL",
	"UT_Asgor",
	"UT_DANCE",
	"UT_BTIME",
	"E2_Tange",
	"E1_Buttv",
	"CB1_MASK",
	"S&k_star",
	"S3_STAR",
	"SM_Super",
	"SMG_STAR"
};

script "SV_PLAYERTICKER" OPEN
{
	if(IsNetworkGame() && (ConsolePlayerNumber() != -1)) // Hack to prevent Zandronum from executing this on a client
		terminate;

	int tickNum = 0;
	SetActivator(-1);

	while(1)
	{
		tickNum++;
		for(int p = 0; p < MAX_PLAYERS; p++)
		{
			if(!PlayerInGame(p))
				continue;

			SetActivatorToPlayer(p);

			if(GetActorProperty(0, APROP_Health) <= 0)
				continue;

			// Check for invulnerability types.
			if(CheckInventory("PowerFiendUlt"))
				p_status[p].invul_type = INVUL_ULT;
			else if(CheckInventory("PowerInvulnerable"))
				p_status[p].invul_type = INVUL_NORMAL;
			else
				p_status[p].invul_type = INVUL_NONE;

			// If our invulnerability type changes from the last recorded, set music.
			if(p_status[p].last_invul_type != p_status[p].invul_type)
			{
				switch (p_status[p].invul_type)
				{
					case INVUL_ULT:
						LocalSetMusic("S4_METAL");
						break;
					case INVUL_NORMAL:
						int list_size = sizeof(inv_music_list)-1;
						LocalSetMusic(inv_music_list[random(0, list_size)]);
						break;
					case INVUL_NONE:
						LocalSetMusic("*");
						break;
				}

				p_status[p].last_invul_type = p_status[p].invul_type;
			}

			//////////////////////////////////////////////
			// Heavy Metal Effect
			//////////////////////////////////////////////
			// does a player have the heavy metal effect?
			if(CheckInventory("Debuff_HeavyMetal") && p_status[p].debuff_gravity == false)
			{
				p_status[p].debuff_gravity = true;
				SetActorProperty (0, APROP_Gravity, 3.0);
			}
			else if(!CheckInventory("Debuff_HeavyMetal") && p_status[p].debuff_gravity == true)
			{
				p_status[p].debuff_gravity = false;
				if(CheckActorClass(0, "Gosimer"))
					SetActorProperty(0, APROP_Gravity, 0.875);
				else
					SetActorProperty(0, APROP_Gravity, 1.0);
			}

			//////////////////////////////////////////////
			// Damage Over Time
			//////////////////////////////////////////////
			// does a player have the burn debuff?
			if(CheckInventory("Debuff_Burn"))
			{
				if(!(tickNum % 17))
					Thing_Damage2(0, random(2, 4), "Burn");
			}

			if(CheckActorClass(0, "MaximusPrime"))
			{
				// Ability Charge Regen
				if(CheckInventory("PowerStrength") && !(tickNum % 52))
					GiveInventory("AbilityCharge", 1);

				if(!(tickNum % 105))
				{
					if((GetActorProperty(0, APROP_Health) > 200))
						SetActorProperty(0, APROP_Health, GetActorProperty(0, APROP_Health)-1);
					if(CheckInventory("BasicArmor") > 200)
						TakeInventory("BasicArmor", 1);

				}

				if(CheckInventory("Armor") > 0 && !GetArmorType("NullArmorHack", p))
				{
					int oldArmor = CheckInventory("BasicArmor");
					TakeInventory("BasicArmor", 99999);
					GiveInventory("NullArmorHack", oldArmor);
					if(GetCVar("sh_debug_armor"))
						log(s:"Swapping armor type to NullArmorHack");
				}

				// Don't bleed if we're over 50 shield.
				if(CheckInventory("BasicArmor") > 50)
				{
					if(!CheckFlag(0, "NOBLOOD"))
						ChangeFlag("NOBLOOD", true); //SetActorFlag("NOBLOOD", true);
				}
				else if(CheckFlag(0, "NOBLOOD"))
					ChangeFlag("NOBLOOD", false); //SetActorFlag(0, "NOBLOOD", false);
			}
			
			// Dual Wielding Hax
			int oldbuttons = GetPlayerInput(-1, INPUT_OLDBUTTONS);
			int buttons = GetPlayerInput(-1, INPUT_BUTTONS);
			if((buttons & BT_ALTATTACK) && !(oldbuttons & BT_ALTATTACK))
			{
				GiveInventory("DualFireHax", 1);
			}
			else if(!(buttons & BT_ALTATTACK) && (oldbuttons & BT_ALTATTACK))
			{
				TakeInventory("DualFireHax", 1);
			}
		}

		SetActivator(-1);
		delay(1);
	}
}

int lastCameraPlayerClass;
script "CL_PLAYERTICKER" OPEN CLIENTSIDE
{
	int tickNum = 0;
	int consolePlayer = ConsolePlayerNumber();
	SetActivator(-1);

	while(1)
	{
		tickNum++;
		for(int p = 0; p < MAX_PLAYERS; p++)
		{
			if(p == consolePlayer)
			{
				SetActivatorToPlayer(p);
				if(SetActivator(0, AAPTR_PLAYER_GETCAMERA))
				{
					int pCam = PlayerNumber();
					if(pCam != -1)
					{
						if(PlayerClass(pCam) != lastCameraPlayerClass)
						{
							SetActorState(PICKUP_SPRITE_TAG, "RecheckSprite", 0);
							lastCameraPlayerClass = PlayerClass(pCam);
						}
					}
				}
			}

			if(!PlayerInGame(p))
				continue;

			SetActivatorToPlayer(p);

			if(GetActorProperty(0, APROP_Health) <= 0)
				continue;

			/////////////////////////////////////////////
			// Blinded Lightning Bolt Effect
			/////////////////////////////////////////////
			if(CheckInventory("Debuff_Blind"))
			{
				if(!(tickNum % 3))
					Spawn("Particle_Lightning7", GetActorX(0), GetActorY(0), GetActorZ(0) + 30.0, 0, 0);
			}

			// low health alarm
			if(GetActorProperty(0, APROP_Health) < 35)
			{
				if(!(tickNum % 70))
				{
					if(p_status[p].alarm_volume > 0.5)
						p_status[p].alarm_volume -= 0.1;

					PlayActorSound(0, SOUND_Bounce, CHAN_BODY, p_status[p].alarm_volume, false, 1.0);
				}
			}
			else
			{
				p_status[p].alarm_volume = 1.0;
			}
			
			// Dual Wielding Hax
			int oldbuttons = GetPlayerInput(-1, INPUT_OLDBUTTONS);
			int buttons = GetPlayerInput(-1, INPUT_BUTTONS);
			if((buttons & BT_ALTATTACK) && !(oldbuttons & BT_ALTATTACK))
			{
				GiveInventory("DualFireHax", 1);
			}
			else if(!(buttons & BT_ALTATTACK) && (oldbuttons & BT_ALTATTACK))
			{
				TakeInventory("DualFireHax", 1);
			}
		}

		SetActivator(-1);
		delay(1);
	}
}