#library "PlayerTicker"
#include "Zcommon.acs"

//////////////////////
// Player Tick Handler
//////////////////////
#define STATUS_UNUSED 0
#define STATUS_GRAVITY 1

int p_status[64][2];
script "SV_PLAYERTICKER" OPEN
{
	if(IsNetworkGame() && (ConsolePlayerNumber() != -1)) // Hack to prevent Zandronum from executing this on a client
		terminate;

	SetActivator(-1);

	while(1)
	{
		int tickNum = timer();
		for(int p = 0; p < playercount(); p++)
		{
			SetActivatorToPlayer(p);
			//////////////////////////////////////////////
			// Heavy Metal Effect
			//////////////////////////////////////////////
			// does a player have the heavy metal effect?
			if(CheckInventory("Debuff_HeavyMetal") && p_status[p][STATUS_GRAVITY] == false)
			{
				p_status[p][STATUS_GRAVITY] = true;
				SetActorProperty (0, APROP_Gravity, 3.0);
			}
			else if(!CheckInventory("Debuff_HeavyMetal") && p_status[p][STATUS_GRAVITY] == true)
			{
				p_status[p][STATUS_GRAVITY] = false;
				SetActorProperty(0, APROP_Gravity, 1.0);
			}

			//////////////////////////////////////////////
			// Damage Over Time
			//////////////////////////////////////////////
			// does a player have the burn debuff?
			if(CheckInventory("Debuff_Burn"))
			{
				if(!(tickNum % 17))
					Thing_Damage2(0, random(2, 4), "Burn");
			}
			
			// Ability Charge Regen
			if(CheckActorClass(0, "MaximusPrime"))
			{
				if(CheckInventory("PowerStrength") && !(tickNum % 70))
					GiveInventory("AbilityCharge", 1);
			}
		}
		
		SetActivator(-1);
		delay(1);
	}
}